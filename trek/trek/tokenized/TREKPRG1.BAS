10 IF SS=0 THEN CHAIN"TREK"
30 PS=1:PH=3:PH1=3:Q1=1
40 C(6,2)=1:C(2,1)=1:C(4,2)=-1:C(8,1)=-1:KM(3)=7:KM(2)=15:KM(1)=20
50 S$(1)="Forward":S$(2)="Starboard":S$(3)="Aft":S$(4)="Port":DF=1
60 T$(1)="UNARMED":T$(2)="ARMED":T$(0)="DAMAGED"
70 L=8:L1=1:K=8:K1=25:PX=5+LEN(P1$):PX$="FIRE "+P1$:PY=9+LEN(P1$):PY$="FIRE ALL "+P1$
80 CLS:DCLK=0:LOCATE 1:PRINT CHR$(218);STRING$(51,196);CHR$(191)
90 FOR A=2 TO 17:LOCATE A:PRINT CHR$(179):LOCATE A,53:PRINT CHR$(179):NEXT
100 LOCATE 18:PRINT CHR$(192);STRING$(51,196);CHR$(217):IF SS1<=0 THEN 3760
140 COLOR 15:LOCATE L+1,L1*2:PRINT E$(DF):IF CLO=0 THEN COLOR 4:LOCATE K+1,K1*2:PRINT K$
150 COLOR 7:GOSUB 1830:IF CLO=1 AND PH<3 AND RND*10<PH^2 AND D<KM(2)/2 THEN CLO=0:FOR A=1 TO 16*TR:NEXT:LOCATE K+1,K1*2:PRINT "::":FOR A=1 TO 16*TR:NEXT:LOCATE K+1,K1*2:PRINT K$:DCLK=1
160 IF C1=>97 THEN 3760
170 LOCATE 1,54:PRINT CHR$(218);STRING$(24,196);CHR$(191):IF KP<=0 THEN 3760
180 FOR A=2 TO 10:LOCATE A,54:PRINT CHR$(179):LOCATE A,79:PRINT CHR$(179):NEXT
190 LOCATE 11,54:PRINT CHR$(192);STRING$(24,196);CHR$(217):LOCATE 12,57:PRINT "Short-Range Scanner"
200 COLOR 6:GOSUB 4030:COLOR 7
210 LOCATE 14,56:PRINT "Facing ";S$(DF);STRING$(15-LEN(S$(DF)),32):IF SS<=0 THEN 3680
220 Q10=0:GOSUB 1840:X$="":IF SH(D1)<1 AND CLO=0 THEN X$="!"
230 IF CLO=0 THEN LOCATE 15,56:PRINT AD$"s to ";S$(D1);X$;STRING$(9-LEN(S$(D1)),32)
240 GOSUB 1820:IF CLO=0 THEN LOCATE 16,56:PRINT "Distance: ";INT((D*100)/100)
250 IF ZZ=1 THEN RETURN ELSE LOCATE 17,56:PRINT STR$(C);"% Casualties":IF C=>97 THEN 3680
260 LOCATE 18,56:PRINT "Superstructure: ";SS:TRY=0:KB1=0:KSH=0
270 FOR A=1 TO 4:IF S(A)=-1 THEN SH(A)=-1
280 NEXT:LOCATE 19,15:PRINT STRING$(24,240):IF P<=0 THEN 3680
290 IF SS<5 THEN COLOR 16,1:LOCATE 18,55:PRINT "SUPERSTRUCTURE CRITICAL":COLOR 7,0
300 IF SE=-1 THEN LOCATE 19,57:PRINT ":SENSORS OUT:"
310 IF AA>0 THEN 2980
320 FOR A=15 TO 38:COLOR 15,0:LOCATE 19,A:PRINT CHR$(254):COLOR 7,0:FOR A1=1 TO 1.8*TR:NEXT
330 IF INKEY$<>"" THEN 350 ELSE LOCATE 19,A:PRINT CHR$(240):NEXT
340 GOTO 290
350 NTK=0:LOCATE 19,15:PRINT STRING$(24,240)
360 IF PH2=1 THEN PH2=0:PH=2:GOTO 430
370 IF PH=3 AND PH1=3 THEN PH=1:PH1=1:GOTO 670
380 IF PH=2 THEN 430
390 IF PH=1 THEN PH=2:GOTO 2970
400 IF PH=3 AND PH1<3 THEN PH=1:PH1=PH1+1
410 IF PL>0 THEN KTD=0
420 REM *** COMMAND ***
430 IF PH<>2 THEN 360
440 FOR A=1 TO NP:IF PH(A)<0 THEN PB(A)=0
450 NEXT:FOR A=20 TO 23:LOCATE A,10:PRINT STRING$(70,32):NEXT:LOCATE 20
460 DA=0:PRINT STRING$(75,32);CHR$(30);TAB(10);:LINE INPUT "Orders, Captain? ";O$:LOCATE 20:PRINT STRING$(70,32)
470 IF RIGHT$(O$,1)="!" THEN LOCATE 22,10:PRINT "YEOMAN:  No need to shout, Captain."
480 PH=3:IF LEN(O$)<3 THEN PRINT TAB(10);"Standing by, Captain.";STRING$(30,32):GOTO 2040
490 FOR A=1 TO LEN(O$):IF ASC(MID$(O$,A,1))>96 THEN MID$(O$,A,1)=CHR$(ASC(MID$(O$,A,1))-32)
500 NEXT
510 FOR A=1 TO LEN(O$):IF MID$(O$,A,4)="FIRE" AND CLO=1 THEN LOCATE 21,10:PRINT "The "AD$"s are cloaked, Captain.":LOCATE 20:GOTO 460
520 IF MID$(O$,A,4)="FIRE" AND D=0 THEN PRINT "HELMSMAN:  We can't fire at this distance, Captain!":FOR A=1 TO 25*TR:NEXT:PH=3:GOTO 80
530 IF MID$(O$,A,PX)=PX$ OR MID$(O$,A,PY)=PY$ THEN IF SE=-1 THEN 650 ELSE 1170
540 IF MID$(O$,A,14)="FIRE TORPEDOES" OR MID$(O$,A,18)="FIRE ALL TORPEDOES" THEN IF SE=-1 THEN 650 ELSE 1460
550 IF MID$(O$,A,8)="TACTICAL" THEN PH2=1:GOTO 1080
560 IF MID$(O$,A,13)="SELF-DESTRUCT" THEN 3460
570 IF MID$(O$,A,8)="STAND BY" THEN O$="":GOTO 480
580 IF MID$(O$,A,9)="SOUND OFF" THEN SD=0:LOCATE 20,10:PRINT "Sound off, Captain.":GOTO 460
590 IF MID$(O$,A,8)="SOUND ON" THEN SD=1:LOCATE 20,10:PRINT "Sound on, Captain.":GOTO 460
600 IF MID$(O$,A,4)="SCAN" AND TRY=1 THEN LOCATE 21,10:PRINT "I have already done so, Captain.":GOTO 460
610 IF MID$(O$,A,4)="SCAN" THEN TRY=1:ON CLO+1 GOTO 4830,4590
620 IF MID$(O$,A,6)="STATUS" THEN 5100 ELSE IF MID$(O$,A,6)="FIRING" OR MID$(O$,A,7)="COMPUTE" THEN 5750
630 IF MID$(O$,A,1)="?" OR MID$(O$,A,4)="HELP" OR MID$(O$,A,8)="COMMANDS" THEN 5700
640 NEXT:LOCATE 21,10:PRINT "FIRST OFFICER:  I didn't quite catch that, Captain.":LOCATE 20:GOTO 460
650 LOCATE 22:PRINT STRING$(75,32):LOCATE 22,10:PRINT "HELMSMAN:  Sensor damage prevents the use of our weapons.":DA=0:PH=3:LOCATE 20:GOTO 460
660 REM *** POWER ALLOCATION PHASE ***
670 P1=P:MP=0:SE1=0:FOR A=1 TO NP:PB(A)=0:IF PH(A)>0 THEN PH(A)=0
680 LR=3:PB1(A)=0:ZX=12+LEN(P1$):IF FPL<1 THEN ZX$="Dmg"+STR$(TD) ELSE ZX$="RPL-"+RIGHT$(STR$(FPL),1)
690 NEXT:FOR A=1 TO NTT:IF T(A)>0 THEN T(A)=0
700 NEXT:FOR A=1 TO 4:SH(A)=0:NEXT:IF NP+NTT>10 THEN XZ=0 ELSE XZ=1
710 CLS:GOSUB 2400:IF P1=>0 THEN 730 ELSE IF ABS(P1-INT(P1+1))<.1 THEN P1=INT(P1+1)
720 PRINT " POWER POINTS:  "P1:GOTO 750
730 IF P1-INT(P1)<.1 OR INT(P1)-P1<.1 THEN P1=INT(P1)
740 PRINT " POWER POINTS:  "STRING$(P1,219);P1;"     "
750 PM=3:LOCATE 3:PRINT " MOVEMENT:  "STRING$(MP,219);MP;TAB(46);"-";M$;" Mvt Pt Ratio"
760 SE=0:PRINT:FOR A=1 TO NP:PRINT " "P1$" BANK"STR$(A)":  ";:IF PH(A)=-1 THEN PRINT "DAMAGED":GOTO 780
770 PRINT STRING$(PB(A),219),PB(A)"units","  -"MFP"max/shot    Cht "FP1$
780 NEXT:PRINT:FOR A=1 TO NTT:PRINT " PHOTON TUBE"STR$(A)":  "STRING$((T(A)+1)*2,219)" "T$(T(A)+1),"    -"S$(NT(A))"-";TAB(45);"Cht "FT1$"  "ZX$"  To Arm"PP:NEXT
790 PRINT STRING$(XZ,13);:PRINT " SHIELDS":FOR A=1 TO 4:PRINT A" "S$(A),:IF S(A)=-1 THEN PRINT "   DAMAGED":GOTO 810
800 PRINT STRING$( H(A),219);TAB(17+MSP);SH(A);D$(SD(A));:IF SH(A)<0 THEN PRINT ELSE PRINT TAB(28+MSP);"-"MSP"max power  "SP$" Shd Pt Ratio"
810 NEXT:PRINT STRING$(XZ,13);:PRINT " FINISHED";
820 LOCATE LR,1:PRINT CHR$(26)
830 A$=INKEY$:IF A$="" THEN 830
840 IF LEN(A$)=1 THEN 860 ELSE GOSUB 4540:A$=RIGHT$(STR$(A),1)
850 IF A$="+" OR A$="-" THEN 900
860 IF A$="7" THEN LOCATE LR:PRINT " ":LR=3:LOCATE 3:PRINT CHR$(26):GOTO 830
870 IF A$="8" AND LR>3 THEN LOCATE LR:PRINT " ":LR=LR-1:LOCATE LR:PRINT CHR$(26):GOTO 830
880 IF A$="2" AND LR<12+NP+NTT+XZ THEN LOCATE LR:PRINT " ":LR=LR+1:LOCATE LR:PRINT CHR$(26):GOTO 830
890 IF A$="1" THEN LOCATE LR:PRINT " ":LR=11+NP+NTT+XZ*2:LOCATE LR:PRINT CHR$(26):GOTO 830
900 IF LR=3 AND A$="+" THEN P1=P1-MPR+MP1*MPR:MP=MP+1:LOCATE 3,13:PRINT STRING$(MP,219);MP:GOTO 1040
910 IF LR=3 AND A$="-" THEN IF MP=0 THEN 830 ELSE P1=P1+MPR+MP1*MPR:MP=MP-1:LOCATE 3,13:PRINT STRING$(MP,219);MP;" ":GOTO 1040
920 LL=LR-4:IF LR<=4 OR LR>4+NP OR A$<>"+" THEN 940 ELSE IF PH(LL)=-1 OR PB(LL)=3*MFP THEN 830 ELSE P1=P1-1:PB(LL)=PB(LL)+1:LOCATE LR,ZX:PRINT STRING$(PB(LL),219),PB(LL);"units";:IF PB(LL)>10 THEN PRINT STRING$(15,32) ELSE PRINT ,"  -"MFP"max/shot"
930 GOTO 1040
940 IF LR<=4 OR LR>4+NP OR A$<>"-" THEN 970 ELSE IF PH(LL)=-1 OR PB(LL)<1 THEN 830
950 P1=P1+1:PB(LL)=PB(LL)-1:LOCATE LR,ZX:PRINT STRING$(PB(LL),219),PB(LL);"units",:IF PB(LL)>10 THEN PRINT STRING$(15,32) ELSE PRINT "  -"MFP"max/shot"
960 GOTO 1040
970 LL=LR-NP-5:IF LR>5+NP AND LR<6+NP+NTT AND A$="+" THEN IF T(LL)<>0 THEN 830 ELSE P1=P1-PP:T(LL)=1:LOCATE LR,18:PRINT STRING$((T(LL)+1)*2,219)" "T$(T(LL)+1):GOTO 1040
980 IF LR>5+NP AND LR<6+NP+NTT AND A$="-" THEN IF T(LL)<>1 THEN 830 ELSE P1=P1+PP:T(LL)=0:LOCATE LR,18:PRINT STRING$((T(LL)+1)*2,219)" "T$(T(LL)+1):GOTO 1040
990 LL=LR-NP-6-NTT-XZ:IF LR>6+NP+NTT+XZ AND LR<11+NP+NTT+XZ AND A$="+" THEN IF S(LL)=-1 OR SH(LL)=MSP THEN 830 ELSE P1=P1-1/SPR:SH(LL)=SH(LL)+1:LOCATE LR,5:PRINT S$(LL),STRING$(SH(LL),219);TAB(17+MSP);SH(LL):GOTO 1040
1000 IF LR>6+NP+NTT+XZ AND LR<11+NP+NTT+XZ AND A$="-" THEN IF S(LL)=-1 OR SH(LL)=0 THEN 830 ELSE P1=P1+1/SPR:SH(LL)=SH(LL)-1:LOCATE LR,5:PRINT S$(LL),STRING$(SH(LL),219);" ";TAB(17+MSP);SH(LL);"  ":GOTO 1040
1010 IF LR>6+NP+NTT+XZ AND LR<11+NP+NTT+XZ AND A$="*" THEN IF S(LL)=-1 THEN 830 ELSE P1=P1-(MSP-SH(LL))/SPR:SH(LL)=MSP:LOCATE LR,5:PRINT S$(LL),STRING$(SH(LL),219);TAB(17+MSP);SH(LL);TAB(28+MSP);"-"MSP"max power":GOTO 1040
1020 IF LR=11+NP+NTT+XZ*2 THEN IF INT(P1+.3)<>0 OR A$<>CHR$(13) THEN 830 ELSE 1080
1030 GOTO 830
1040 LOCATE 1:IF P1=>0 THEN 1060 ELSE IF ABS(P1-INT(P1+1))<.1 THEN P1=INT(P1+1)
1050 PRINT " POWER POINTS:  "INT(P1*10)/10;" ";" ";"          ":GOTO 830
1060 IF P1-INT(P1)<.1 THEN P1=INT(P1) ELSE IF INT(P1+1)-P1<.1 THEN P1=INT(P1+1)
1070 PRINT " POWER POINTS:  "STRING$(P1,219);INT(P1*10)/10;" ";" ";"          ":GOTO 830
1080 CLS:PRINT "POWER:  "STRING$(P,219);P
1090 PRINT "MOVEMENT POINTS  ";:PRINT STRING$(XZ,13);STRING$(MP,219);MP;TAB(46);"-";M$;" Mvt Pt Ratio"
1100 PRINT:FOR A=1 TO NP:PRINT P1$" BANK";STR$(A);":  ";:IF PH(A)=-1 THEN PRINT "DAMAGED":GOTO 1120
1110 PRINT STRING$(ABS(PB(A)),219),PB(A);"units","  -"MFP"max/shot    Cht "FP1$
1120 NEXT:PRINT:FOR A=1 TO NTT:PRINT "PHOTON TUBE";STR$(A);":  ";STRING$((T(A)+1)*2,219);" ";T$(T(A)+1),"    -"S$(NT(A))"-";TAB(45);"Cht "FT1$"  "ZX$:NEXT
1130 PRINT:PRINT "SHIELDS":FOR A=1 TO 4:PRINT A;S$(A),:IF SH(A)=-1 THEN PRINT "DAMAGED":GOTO 1150
1140 PRINT STRING$(SH(A),219);TAB(17+MSP);SH(A);D$(SD(A));:IF SH(A)<0 THEN PRINT ELSE PRINT TAB(28+MSP);"-"MSP"max power  "SP$" Shd Pt Ratio"
1150 NEXT:PRINT STRING$(XZ,13);:PRINT "SUPERSTRUCTURE:  ";STRING$(SS,219);SS
1160 A$=INKEY$:IF A$="" THEN 1160 ELSE 80
1170 PC=0:IF D<=LEN(FP$) THEN PC=VAL(MID$(FP$,D,1))+1
1180 IF PC-INT(C/10)<=0 THEN PRINT TAB(10);"HELMSMAN:  "AD$"s are out of range, Captain.":GOTO 460
1190 IF PC-INT(C/10)>2 THEN 1220 ELSE PRINT TAB(10);"HELMSMAN:  The "AD$"s are at extreme range,":PRINT TAB(23);"Captain.  Shall I proceed?"
1200 A$=INKEY$:IF A$="" THEN 1200 ELSE LOCATE 21:PRINT STRING$(79,32):PRINT STRING$(79,32):LOCATE 21
1210 IF A$<>"Y" AND A$<>"y" THEN 460
1220 IF MID$(O$,A,PY)=PY$ THEN 3880 ELSE GOSUB 4700
1230 LOCATE 2+INT((8-NP)/2),57:PRINT P1$" BANKS:":FOR A=1 TO NP:LOCATE A+2+INT((8-NP)/2),55:PRINT A;" ";:IF PH(A)<0 THEN PRINT " DAMAGED":GOTO 1250
1240 PRINT STRING$(PB(A),219);:PRINT TAB(75);PB(A)
1250 NEXT
1260 DA=0:FOR A=1 TO NP:PPP(A)=0:NEXT:LOCATE 20,10:PRINT "Banks ";:FOR A=1 TO NP:IF PB(A)>0 THEN PRINT A;
1270 NEXT:PRINT " charged.":INPUT "         How many banks to fire";BK:BBK=1:LOCATE 20:PRINT STRING$(60,32):PRINT STRING$(60,32)
1280 IF BK<1 OR BK>NP THEN 4010 ELSE FOR A=1 TO BK:LOCATE 20,10:PRINT P$" bank   ";STRING$(3,29);:INPUT BB(A):IF BB(A)>NP THEN LOCATE 20,10:PRINT P$" bank   ";STRING$(3,29);:INPUT BB(A)
1290 IF PB(BB(A))<1 THEN PH=2:GOTO 4010
1300 IF PB(BB(A))<=0 THEN PRINT "Captain, "P$" bank";BB(A);"isn't armed!":KB=0:GOTO 1170
1310 LEG=0:LOCATE 21,10:PRINT STRING$(60,32):LOCATE 21,10:PRINT "Bank"BB(A)"has"PB(BB(A))"units.  How much to fire";:INPUT PPP(A):BBB=BB(A)
1320 IF PPP(A)<1 THEN 4010 ELSE IF PPP(A)>PB(BB(A)) OR PPP(A)>MFP AND PB(BB(A))<MFP THEN PPP(A)=PB(BB(A)):LEG=1:PRINT "         Bank"BB(A)"at maximum"
1330 IF PPP(A)>MFP OR PPP(A)>PB(BB(A)) AND MFP<=PB(BB(A)) THEN PPP(A)=MFP:PRINT "         Bank"BB(A)"at full power.":LEG=1:FOR APS=1 TO 30*TR:NEXT
1340 IF PPP(A)<=MFP AND PPP(A)<=PB(BB(A)) THEN LEG=1
1350 LOCATE BB(A)+2+INT((8-NP)/2),57:PRINT STRING$(12,32);STRING$(10,29);STRING$(PB(BB(A))-PPP(A),219);:PRINT TAB(75);PB(BB(A))-PPP(A)
1360 IF LEG=0 THEN 4010 ELSE PB(BB(A))=PB(BB(A))-PPP(A):NEXT
1370 PH=3:FOR A=20 TO 23:LOCATE A,10:PRINT STRING$(40,32):NEXT:GOSUB 4700:LOCATE 20
1380 A=BB(BBK):IF SD=1 THEN FOR B=1 TO 6:PLAY"L32 E-":NEXT
1390 GOSUB 1830:IF INT(RND*10)+1<=PC-INT(C/10) THEN 1420
1400 IF BK<5 THEN PRINT TAB(10);"HELMSMAN:  "P$" bank"A"missed." ELSE PRINT " MR. SULU:  "P$" bank"A"missed.",
1410 GOTO 1450
1420 FOR B6=1 TO 3:IF DM(B6)=>D THEN PB2=PPP(A)+B6
1430 NEXT:DA=DA+PB2:IF BK<5 THEN PRINT TAB(10);"HELMSMAN: "PB2"unit hit on "AD$" vessel." ELSE PRINT PB2"unit hit on "AD$" vessel.";STRING$(7-LEN(AD$),32),
1440 GOSUB 5230
1450 FOR A3=1 TO 4*(TR-15):NEXT:IF BBK=BK THEN 2040 ELSE BBK=BBK+1:GOTO 1380
1460 IF NTT=0 THEN LOCATE 21,10:PRINT "The "TS$" has no torpedoes, Captain!":GOTO 460
1470 PC=0:IF D<=LEN(FT$) THEN PC=VAL(MID$(FT$,D,1))+1
1480 IF PC-INT(C/10)<=0 THEN PRINT TAB(10);"HELMSMAN:  "AD$"s are out of torpedo range.":GOTO 460
1485 FOR A3=1 TO NTT:IF NT(A3)=D1 THEN 1490
1487 NEXT:PRINT TAB(10);"We have no "S$(D1)" tubes, Captain!":FOR A3=1 TO 40*TR:NEXT:GOTO 4180
1490 IF PC-INT(C/10)>2 THEN 1520 ELSE PRINT TAB(10);"HELMSMAN:  The "AD$"s are at extreme range,":PRINT TAB(23);"Captain.  Shall I proceed?"
1500 A$=INKEY$:IF A$="" THEN 1500 ELSE LOCATE 21:PRINT STRING$(79,32):PRINT STRING$(79,32):LOCATE 21
1510 IF A$<>"Y" AND A$<>"y" THEN 460
1520 IF MID$(O$,A,18)="FIRE ALL TORPEDOES" AND FPL<1 THEN 3940
1530 GOSUB 1840
1540 GOSUB 4700:FOR A=20 TO 22:LOCATE A:PRINT STRING$(69,32):NEXT
1550 LOCATE 2+INT((8-NTT)/2),57:PRINT "TORPEDO TUBES:":FOR A=1 TO NTT:LOCATE A+2+INT((8-NTT)/2),55:PRINT A;
1560 PRINT STRING$(T(A)+1,219)" "T$(T(A)+1);" -"S$(NT(A))"-"
1570 NEXT
1580 DA=0:LOCATE 20,10:PRINT "Tubes ";:FOR A=1 TO NTT:IF T(A)>0 THEN PRINT A;
1590 NEXT:PRINT " armed.":INPUT "         How many Torpedoes to fire";NT:LOCATE 20,10:PRINT STRING$(60,32):IF NT<1 OR NT>NTT THEN 4180
1600 LOCATE 21:PRINT STRING$(60,32):FOR A=1 TO NT:LOCATE 20,29:PRINT "   ":LOCATE 20,10:INPUT "Which Photon Tube";PT(A):IF PT(A)<1 OR PT(A)>NTT THEN 4180
1610 IF T(PT(A))<>1 THEN LOCATE 21,10:PRINT "Photon Tube";PT(A);"is ";T$(T(PT(A))+1);".":FOR AAA=1 TO 20*TR:NEXT:GOTO 4180
1620 LOCATE PT(A)+2+INT((8-NTT)/2),58:PRINT CHR$(219)" UNARMED -"S$(D1)"-"
1630 GOSUB 1840:IF NT(PT(A))=D1 THEN 1640 ELSE LOCATE 22,10:PRINT "HELMSMAN:  Tube"PT(A)"fires only "S$(NT(PT(A)))", Captain.":PH=2:FOR A=1 TO 25*TR:NEXT:GOTO 4180
1640 NEXT:GOSUB 4700
1650 NNN=1:FOR A=20 TO 23:LOCATE A,10:PRINT STRING$(40,32):NEXT
1660 FOR A=1 TO 12*TR:NEXT
1670 T(PT(NNN))=0
1680 GOSUB 1830:IF INT(RND*10)+1<=PC-INT(C/10) THEN 1700
1685 IF SD=1 THEN SOUND 2000,1:SOUND 40,.5:SOUND 2000,2
1690 LOCATE 21,10:PRINT STRING$(60,32):LOCATE 21,10:PRINT "HELMSMAN:  Photon Torpedo missed.":GOTO 1720
1700 IF FPL>0 THEN 5530 ELSE IF SD=1 THEN BEEP
1710 LOCATE 21,10:PRINT STRING$(60,32):LOCATE 21,10:PRINT "HELMSMAN: "TD"unit hit on "AD$" vessel.":PB2=TD:GOSUB 5230
1720 FOR A=1 TO 10*TR:NEXT:NNN=NNN+1:IF NNN<=NT THEN 1660 ELSE 2040
1730 IF KTD=0 OR PL<>0 THEN 2090 ELSE FOR B=1 TO 20*TR:NEXT:COLOR 6:GOSUB 4030:COLOR 7
1740 IF D>1 OR AV>1 THEN 1760 ELSE LOCATE 6,66:PRINT STRING$(2,219):FOR A=1 TO 52*TR:NEXT
1750 LOCATE 6,65:PRINT STRING$(4,219):LOCATE 6,65:PRINT "(";CHR$(174);CHR$(175);")"
1760 COLOR 5,0:LOCATE 7,67:PRINT "*"
1770 FOR A=1 TO 21*TR:NEXT:LOCATE 7,67:PRINT " ":LOCATE 8,68:PRINT CHR$(15)
1780 FOR A=1 TO 21*TR:NEXT:LOCATE 8,68:PRINT " ":LOCATE 9,68:PRINT STRING$(2,219)
1790 FOR A=1 TO 21*TR:NEXT:LOCATE 9,68:PRINT "  ":LOCATE 10,68:PRINT STRING$(3,219)
1800 FOR A=1 TO 21*TR:NEXT:LOCATE 10,66:PRINT STRING$(13,219):LOCATE 9,69:PRINT STRING$(10,219):LOCATE 8,72:PRINT STRING$(7,219)
1810 FOR A=1 TO 20*TR:NEXT:LOCATE 10,66:PRINT STRING$(13,32):LOCATE 9,69:PRINT "          ":LOCATE 8,72:PRINT "       ":COLOR 7,0:GOTO 2370
1820 D=SQR((L-K)^2+(L1-K1)^2):RETURN
1830 RANDOMIZE(VAL(RIGHT$(TIME$,2))):RETURN
1840 REM *** FIND DIRECTION TO ENEMY VESSEL ***
1850 IF L<K THEN D1=2:GOTO 1890
1860 IF L>K THEN D1=4:GOTO 1890
1870 IF L1<K1 THEN D1=3:GOTO 1890
1880 D1=1
1890 IF K1>L1 AND K1-L1>ABS(K-L) THEN D1=1
1900 IF L1>K1 AND L1-K1>ABS(L-K) THEN D1=3
1910 IF Q10=-1 THEN RETURN ELSE Q10=0:IF DF=1 THEN 1970 ELSE ON DF-1 GOTO 1920,1940,1960
1920 IF D1=1 THEN D1=4 ELSE IF D1=2 THEN D1=1 ELSE IF D1=3 THEN D1=2 ELSE IF D1=4 THEN D1=3
1930 RETURN
1940 IF D1=1 THEN D1=3 ELSE IF D1=2 THEN D1=4 ELSE IF D1=3 THEN D1=1 ELSE IF D1=4 THEN D1=2
1950 RETURN
1960 IF D1=1 THEN D1=2 ELSE IF D1=2 THEN D1=3 ELSE IF D1=3 THEN D1=4 ELSE IF D1=4 THEN D1=1
1970 RETURN
1980 IF B=2 THEN PB(Q)=-1:Q=Q+1
1990 IF B=3 THEN PT(Q1)=-1:Q1=Q1+1
2000 IF B=>4 AND B<=7 THEN SS=SS+A:C=C+(B-3)*A
2010 IF B=8 THEN SE=-1
2020 IF B>8 THEN PRINT "BRIDGE HIT"
2030 REM *** ADVERSARY'S TURN ***
2040 AFB=0:AFM=0:FIR=0:NTD=0:B5=1:B51=0:DA1=0:Q2=0
2043 IF DCLK>0 AND PLA>0 AND KM(1)>0 THEN 2310
2045 FOR A=1 TO KBW:K1(A)=0:NEXT
2050 IF SE1=-1 OR D<1 OR CLO=1 THEN 2785
2055 IF DCLK=1 THEN 6000
2060 FOR A=1 TO KBW:IF KB(A)>-99 THEN 2080
2065 IF KB(A)>-5 AND KB(A)<0 THEN KB(A)=ABS(KB(A))
2070 NEXT:IF PLA<1 AND KT<1 THEN 2530
2080 IF TS=2 THEN 2090 ELSE GOSUB 1830:IF RND*1<.5+PLL/4 THEN 2260
2090 IF PH=3 AND PH1=3 AND PLL=1 THEN KTD=1:GOTO 2260
2100 IF D>20 THEN 2530 ELSE FOR A=1 TO KBW:IF KB(A)>-9 THEN 2120
2110 NEXT:GOTO 2530
2120 HK=0:FOR A=1 TO KBW:K1(A)=0:IF KB(A)<=0 THEN 2150 ELSE IF PH1=3 THEN K1(A)=KB(A)
2130 IF PH1=2 THEN IF PLL+KT<=0 THEN K1(A)=INT(KB(A)/2) ELSE K1(A)=KB(A)
2140 IF PH1<>1 THEN 2145 ELSE IF PLL+KT<1 THEN K1(A)=INT(KB(A)/3) ELSE IF PLL=1 OR KT<=MTT THEN K1(A)=INT(KB(A)/2) ELSE K1(A)=KB(A)
2142 IF KB(A)>0 AND K1(A)=0 THEN K1(A)=INT(KB(A)/3+.7)
2145 IF KB(A)>0 AND K1(A)>KB(A) THEN K1(A)=KB(A)
2150 HK=HK+K1(A):NEXT:IF HK<1 THEN FIR=1:GOTO 2530
2160 IF B5>NKP THEN 2530 ELSE IF K1(B5)>0 AND B5<=KBW THEN 2170 ELSE B5=B5+1:GOTO 2160
2170 IF CLO=1 OR FIR>0 THEN 2530
2180 PC=0:IF D<=0 THEN 2530 ELSE PC=VAL(MID$(AP$,D,1))+1
2190 B51=B51+1:IF B51>KBW OR SE1=-1 THEN 2530 ELSE IF KB(B5)<1 THEN B5=B5+1:IF B5>KBW THEN 2530
2195 KB(B5)=KB(B5)-K1(B5):GOSUB 1830:IF INT(RND*10)+1<=PC-INT(C1/10) THEN 2220
2200 IF SD=1 THEN FOR B=1 TO 6:PLAY"L32 E-":NEXT
2210 LOCATE 20:PRINT"         ENGINEER:  "AD$" "D1$"s missed, Captain!";STRING$(25,32):AFB=1:FIR=1:B5=B5+1:Q2=1:IF B5>NKP THEN 2530 ELSE 2160
2220 FOR B6=1 TO 3:IF KM(B6)=>D THEN KB1=K1(B5)+B6
2230 NEXT:GOSUB 3540:DA1=DA1+KB1:GOSUB 1840:PRINT TAB(10);"ENGINEER: ";KB1;"unit hit to Shield";INT(D1);STRING$(34,32):AFB=1:FIR=1:Q2=1:SH(D1)=SH(D1)-KB1:IF SH(D1)<0 THEN NTD=NTD+1:ND(NTD)=-SH(D1):SH(D1)=0
2240 IF SH(D1)=0 THEN LOCATE 21:PRINT STRING$(79,32):COLOR 16,1:PRINT "         SHIELDS COLLAPSING, CAPTAIN!";STRING$(41,32):COLOR 7,0
2250 B5=B5+1:GOTO 2160
2260 DA1=0:IF KMW<1 OR D>16 OR KTD=0 THEN 2040
2270 IF KT>MNT THEN KT=MNT
2280 MMT=KT:IF MMT>MTT THEN MMT=MTT
2290 FOR A=1 TO KMW:IF KB1(A)=>0 THEN 2310
2300 NEXT:GOTO 2040
2310 KT=KT-1:MMT=MMT-1:PC=0:IF D>0 THEN PC=VAL(MID$(AT$,D,1))+1:IF MMT<0 THEN 2530
2320 FIR=1:IF MMM=>NKT THEN 2040
2330 IF SE1=-1 THEN 2530 ELSE MMM=MMM+1:GOSUB 1830:IF INT(RND*10)+1<=PC-INT(C1/10) THEN 2350
2340 LOCATE 20:PRINT"         ENGINEER:  "AD$" "PL$" missed, Captain!";STRING$(25,32):Q2=1:AFM=1:FIR=2:FOR A=1 TO 40*TR:NEXT:GOTO 2530
2350 IF PL<>0 AND PLL=0 THEN 2040
2360 IF PL=0 THEN 1730 ELSE PLL=0:GOTO 4190
2370 GOSUB 1840:DA1=KTD:LOCATE 20:PRINT STRING$(75,32);CHR$(30);TAB(10);"ENGINEER: "KTD"unit hit to Shield";INT(D1):AFM=1:Q2=1:FIR=2:IF PL>0 THEN 2530 ELSE SH(D1)=SH(D1)-KTD:IF SH(D1)<0 THEN NTD=NTD+1:ND(1)=-SH(D1):SH(D1)=0
2380 IF SH(D1)=0 THEN LOCATE 21:PRINT STRING$(79,32):COLOR 16,1:PRINT "         SHIELDS HAVE COLLAPSED, CAPTAIN!";STRING$(37,32):COLOR 7,0
2390 IF KT>0 THEN 2310 ELSE 2530
2400 CLO=0:MMM=0:MKP=KP:KT=NKT:MKP=MKP-KT*KPA:IF CLK=0 THEN 2410 ELSE GOSUB 1830:IF RND*1<.7 THEN MKP=MKP-CLK:CLO=1
2410 IF PL>0 AND KB1(1)=>0 THEN MKP=MKP-PLA:PLL=1
2420 B=0:FOR A=1 TO 4:KS(A)=0:IF KD(A)=0 THEN B=B+1
2430 NEXT:IF B>3 THEN B=3
2440 FOR A=1 TO B:GOSUB 1830:B1=RND*.6+.7:MZ=(MKP-NKP*MPP/3-D^.75/KPR)*B1
2450 GOSUB 1830:A1=INT(RND*4+1):IF KS(A1)>0 OR KD(A1)<0 THEN 2460 ELSE KS(A1)=INT(MZ*KSR+.5):IF KS(A1)<0 THEN KS(A1)=0 ELSE IF KS(A1)>MMP THEN KS(A1)=MMP
2460 MKP=MKP-KS(A1)/KSR:NEXT
2470 MM=0:FOR A=1 TO KBW:IF KB(A)<-9 THEN 2480 ELSE KB(A)=0:MM=MM+1
2480 NEXT:FOR A=1 TO KBW:IF KB(A)<-9 OR MM<1 THEN 2490 ELSE MN=INT(MKP/MM+.2):KB(A)=MN:MKP=MKP-MN:MM=MM-1
2490 NEXT
2500 IF MKP<1 THEN 2520 ELSE NNN=0:FOR A=1 TO NKT:IF KB(A)=0 THEN NNN=1
2510 NEXT:IF NNN=0 THEN 2520 ELSE GOSUB 1830:MN=INT(RND*KBW+1):IF KB(MN)<1 THEN 2500 ELSE KB(MN)=KB(MN)+1:MKP=MKP-1:GOTO 2500
2520 RETURN
2530 IF FIR=2 THEN 2535 ELSE 6200
2535 IF SE1=-1 THEN 2560 ELSE IF Q2>0 OR FIR>0 THEN 2550 ELSE FOR A=1 TO KBW:IF K1(A)>0 AND HH<9 THEN HH=HH+1:B5=0:GOTO 2160
2540 NEXT:IF KT>0 THEN 2310
2550 B5=1:FOR A=20 TO 23:LOCATE A:PRINT STRING$(79,32):NEXT:LOCATE 20:PRINT TAB(10);:IF DA1=0 THEN 2785
2560 HH=0:GOSUB 1840:IF S(D1)=-1 THEN A=DA1:GOTO 2590
2570 IF NTD<1 THEN CLS:LOCATE 8,14:PRINT "SHIELDS ABSORBED THE HIT":LOCATE 15:FOR A=1 TO 20*TR:NEXT:GOTO 2785
2580 A=ABS(SH(D1)):SH(D1)=0
2590 CLS:COLOR 15,0:PRINT:PRINT "DAMAGE REPORT:":COLOR 7,0:FOR MF=1 TO NTD
2600 GOSUB 1830:B=INT(RND*10)+1
2610 IF ND(MF)<1 THEN 2770
2620 IF D1=1 THEN ON B GOTO 2660,2670,2680,2690,2690,2690,2690,2700,2710,2710
2630 IF D1=2 THEN ON B GOTO 2660,2670,2720,2720,2720,2740,2690,2690,2700,2710
2640 IF D1=3 THEN ON B GOTO 2660,2690,2720,2720,2720,2720,2740,2690,2600,2600
2650 IF D1=4 THEN ON B GOTO 2660,2670,2720,2720,2720,2740,2690,2690,2700,2710
2660 IF B<3 THEN SH(D1)=-1:S(D1)=-1:C=C+ND(MF)/2:SS=SS-1:PRINT "SHIELD";D1;"HIT":PRINT TAB(11);STR$(ND(MF)/2);"% CASUALTIES":GOTO 2770
2670 GOSUB 1830:Q=INT(RND*NP)+1:PH(Q)=-1:SS=SS-1:PRINT P1$;Q;"HIT":C=C+ND(MF)/2:PRINT TAB(11);STR$(ND(MF)/2);"% CASUALTIES":GOTO 2770
2680 IF NT<1 THEN 2600 ELSE GOSUB 1830:Q1=INT(RND*NT)+1:T(Q1)=-1:SS=SS-1:PRINT "PHOTON TUBE";Q1;"HIT":C=C+ND(MF)/2:PRINT TAB(11);STR$(ND(MF)/2);"% CASUALTIES":GOTO 2770
2690 SS=SS-ND(MF):PRINT "SUPERSTRUCTURE HIT: ";ND(MF);"UNITS":PRINT " DOWN TO"SS"POINTS":GOSUB 1830:C4=INT(RND*3+1)*ND(MF):C=C+C4:PRINT TAB(11);STR$(C4);"% CASUALTIES":GOTO 2770
2700 SE=-1:SS=SS-1:PRINT "SENSORS HIT":GOTO 2770
2710 PRINT "BRIDGE HIT":C=C+ND(MF):PRINT TAB(11);STR$(ND(MF));"% CASUALTIES":GOTO 2770
2720 WE=WE-ND(MF):IF WE<0 THEN WE=0:P=IE:PRINT "WARP ENGINES OUT, CAPTAIN!":GOTO 2760
2730 PRINT "WARP ENGINE HIT: "ND(MF)"UNITS":P=P-ND(MF):GOTO 2760
2740 IE=IE-ND(MF):IF IE<0 THEN IE=0:P=WE:PRINT "IMPULSE ENGINE OUT, CAPTAIN!":GOTO 2760
2750 PRINT "IMPULSE ENGINE HIT: "ND(MF)"UNITS":P=P-ND(MF)
2760 PRINT TAB(12);"POWER DOWN TO";P;"POINTS"
2770 NEXT
2785 FOR A=1 TO 16:A$=INKEY$:NEXT
2787 LOCATE 22,1:PRINT "         ";:COLOR 0,7:PRINT "Press a key to continue, Captain.";:COLOR 7,0:PRINT STRING$(36,32)
2790 A$=INKEY$:IF A$="" THEN 2790
2800 IF DA<1 THEN 80
2810 IF KD(KSH)=-1 THEN A=DA:GOTO 2840
2820 IF NTK>0 THEN 2830 ELSE CLS:LOCATE 8,10:PRINT AV$"'S SHIELDS ABSORBED THE HIT":FOR A=1 TO 16:A$=INKEY$:NEXT
2825 A$=INKEY$:IF A$="" THEN 2825 ELSE 80
2830 A=ABS(KS(KSH)):KS(KSH)=0
2840 CLS:COLOR 15,0:PRINT "DAMAGE TO "AV$" VESSEL:":COLOR 7,0:FOR MF=1 TO NTK:GOSUB 1830:B=INT(RND*17)+1
2850 IF B<3 THEN KD(KSH)=-1:C1=C1+NK(MF)/2:SS1=SS1-1:PRINT "SHIELD HIT":PRINT TAB(11);STR$(NK(MF)/2);"% CASUALTIES"
2860 IF B=3 OR B=4 THEN GOSUB 1830:Q2=INT(RND*KBW+1):KB(Q2)=-99:NKP=NKP-1:SS1=SS1-1:PRINT D2$" HIT"
2870 IF B=5 THEN GOSUB 1830:Q3=INT(RND*KMW+1):KB1(Q3)=-1:NKT=NKT-1:PLL=-1:SS1=SS1-1:PRINT "MISSILE WEAPON HIT":C=C+NK(MF)/2:PRINT TAB(11);STR$(NK(MF)/2);"% CASUALTIES"
2880 IF B>5 AND B<10 THEN SS1=SS1-NK(MF):PRINT "SUPERSTRUCTURE HIT: ";NK(MF);"UNITS":PRINT " DOWN TO"SS1"POINTS":GOSUB 1830:C4=INT(RND*3+1)*NK(MF):C1=C1+C4:PRINT TAB(11);C4;"% CASUALTIES"
2890 IF B=10 THEN SE1=-1:SS1=SS1-1:PRINT "SENSORS OUT"
2900 IF B=11 OR B=12 THEN PRINT "BRIDGE HIT":C1=C1+NK(MF):PRINT TAB(11);STR$(NK(MF));"% CASUALTIES"
2910 IF B<13 OR B>16 THEN 2930 ELSE KW=KW-NK(MF):IF KW<0 THEN KW=0:PRINT "WARP ENGINES POWERLESS":KP=KI:GOTO 2950
2920 PRINT "WARP ENGINES HIT: "NK(MF)"UNITS":KP=KP-NK(MF):KW=KW-NK(MF)
2930 IF B>16 THEN KI=KI-NK(MF):KP=KP-NK(MF):IF KI=>0 THEN PRINT "IMPULSE ENGINE HIT: "NK(MF)"UNITS" ELSE KI=0:PRINT "IMPULSE ENGINE POWERLESS":KP=KW
2940 IF B>12 THEN PRINT TAB(12);"POWER DOWN TO";KP;"POINTS"
2950 NEXT
2960 A$=INKEY$:IF A$="" THEN 2960 ELSE PH=3:DA=0:GOTO 80
2970 MP1=MP/PM:PM=PM-1:FOR A=20 TO 23:LOCATE A,10:PRINT STRING$(60,32):NEXT
2980 REM *** MOVE FEDERATION VESSEL ***
2990 FOR A=3 TO 10:LOCATE A,55:PRINT STRING$(24,32):NEXT
3000 LOCATE 3,58:PRINT "DIRECTIONAL CODE:":LOCATE 5,66:PRINT "8":LOCATE 6,66:PRINT CHR$(179):LOCATE 7,62:PRINT "4"STRING$(2,196);"Ret";STRING$(2,196);"6":LOCATE 8,66:PRINT CHR$(179):LOCATE 9,66:PRINT "2"
3010 IF AA=1 THEN A$=INKEY$:IF A$="" THEN 3010 ELSE 3120
3020 LOCATE 22,10:PRINT "Press "CHR$(34)"t"CHR$(34)" for Tactical Display."
3030 M1=0:IF MP1=0 THEN 3230 ELSE M1=1:LOCATE 20,10:PRINT "Movement Points:";INT(MP1+.8):LOCATE 21,10:PRINT "HELMSMAN:  Please plot our course Captain."
3040 A$=INKEY$:IF A$="" THEN 3030 ELSE IF A$="T" OR A$="t" THEN PH=1:PM=PM+1:GOTO 1080
3050 IF A$=CHR$(13) THEN A=5:GOTO 3080
3060 IF LEN(A$)=2 THEN 3070 ELSE A=VAL(A$)
3070 GOSUB 4540:IF A<2 OR A=3 OR A=7 OR A>8 THEN 3090
3080 LOCATE L+1,L1*2:PRINT E$(DF):L=L+C(A,1):L1=L1+C(A,2):FD=FD+1:MP=MP-1:MP1=MP1-1:LOCATE 22,10:PRINT STRING$(69,32)
3090 IF L1<1 THEN L1=1
3100 COLOR 15:LOCATE L+1,L1*2:PRINT E$(DF):COLOR 7
3110 IF MP1>0 THEN 3030 ELSE 3120
3120 AA=0:FOR A=20 TO 21:LOCATE A,10:PRINT STRING$(60,32):NEXT:FOR A=1 TO 1000:NEXT:FOR A=1 TO 16:A$=INKEY$:NEXT
3130 IF M1<1 THEN 3330 ELSE LOCATE 22,10:PRINT "HELMSMAN:  Which direction shall we face?"
3140 COLOR 16,7:LOCATE 2,56:PRINT "Press "CHR$(34)"t"CHR$(34)" for Tactical":COLOR 7,0
3150 LOCATE 4,64:PRINT "Port":LOCATE 7,57:PRINT "Aft":LOCATE 7,73:PRINT "Fwd":LOCATE 10,64:PRINT "Strbd":LOCATE 7,65:PRINT E$(DF)" "
3160 A$=INKEY$:IF A$="" THEN 3160
3170 IF A$="T" OR A$="t" THEN AA=1:GOTO 1080
3180 IF A$=CHR$(13) OR A$=CHR$(32) THEN 3230
3190 IF LEN(A$)=2 THEN 3200 ELSE A=VAL(A$):IF A<2 OR A=3 OR A=5 OR A=7 OR A=9 THEN 3120 ELSE 3210
3200 GOSUB 4540
3210 IF A=6 THEN DF=1 ELSE IF A=2 THEN DF=2
3220 IF A=4 THEN DF=3 ELSE IF A=8 THEN DF=4
3230 GOSUB 1830:B=INT(RND*(7/KPR)):GOSUB 1820
3240 Q10=-1:GOSUB 1840
3250 IF D1=4 THEN D1=2 ELSE IF D1=2 THEN D1=8
3260 IF D1=1 THEN D1=4 ELSE IF D1=3 THEN D1=6
3300 LOCATE L+1,L1*2:PRINT E$(DF):LOCATE 7,65:PRINT E$(DF)
3310 ZZ=1:Q10=0:GOSUB 210:IF M1<1 THEN 3330 ELSE LOCATE 22,10:PRINT "Is this correct, Captain?";STRING$(30,32)
3320 A$=INKEY$:IF A$="" THEN 3320 ELSE IF A$<>"Y" AND A$<>"y" THEN ZZ=0:GOTO 3130
3330 IF KT>0 THEN 3350 ELSE FOR A=1 TO KBW:IF KB(A)>0 THEN 3350
3340 NEXT:Q2=-1
3350 ZZ=0:GOSUB 1820:IF D<2 THEN 5900 ELSE Q10=-1:GOSUB 1840:Q10=0:IF D1=1 THEN D1=6 ELSE IF D1=4 THEN D1=8 ELSE IF D1=3 THEN D1=4
3360 IF SE1=-1 OR Q2=-1 THEN 3380 ELSE IF D1=4 THEN D1=6 ELSE IF D1=6 THEN D1=4
3370 IF D1=8 THEN D1=2 ELSE IF D1=2 THEN D1=8
3380 K=K+C(D1,1)*B:K1=K1+C(D1,2)*B:FD=0
3390 GOSUB 1830:A=INT(RND*4+1):IF A=1 THEN A=6 ELSE IF A=4 THEN A=8 ELSE IF A=3 THEN A=4
3400 GOSUB 1830:B=INT(RND*2):K=K+C(A,1)*B:K1=K1+C(A,2)*B
3410 IF K1<1 THEN K1=1
3420 IF K<1 THEN K=1 ELSE IF K>16 THEN K=16 ELSE IF K1>25 THEN K1=25
3430 GOSUB 1840
3440 GOTO 80
3450 REM *** SELF-DESTRUCT ***
3460 GOSUB 1820:IF D>4 AND D<11 THEN DA=1
3470 IF D<5 THEN DA=P/8
3480 IF D<4 THEN DA=P/4
3490 IF D<3 THEN DA=P/2
3500 IF D<2 THEN DA=P
3510 IF D=0 THEN DA=P*2
3520 IF DA<1 THEN DA=1
3525 IF SD=1 THEN SOUND 100,1:SOUND 90,2:SOUND 50,8
3527 COLOR 0,15:CLS:FOR A=1 TO 6*TR:NEXT:COLOR 4,0:CLS:LOCATE 11
3530 PRINT TAB(10);DA"unit hit to "AD$" vessel.":FOR A=1 TO 80*TR:NEXT:P=0:SS=0:C=100:GOSUB 1830:KSH=INT(RND*4+1):KS(KSH)=KS(KSH)-DA:NTK=INT(ABS(KS(KSH))/5+.9):FOR A=1 TO NTK-1:NK(A)=5:NEXT:NK(NTK)=ABS(KS(KSH))-5*(NTK-1):IF KS(KSH)<1 THEN 2830 ELSE 2800
3540 FOR B=1 TO 20*TR:NEXT:COLOR 6:GOSUB 4030
3550 COLOR 7:IF D<=2 AND AV=1 THEN 3610
3560 IF F2=0 THEN FF$=CHR$(186) ELSE FF$=CHR$(179)
3565 IF F1<55 THEN F1=67
3570 COLOR 12,0:FOR B=7 TO 10:LOCATE B,F1:PRINT FF$:IF F2>0 THEN LOCATE B,F2:PRINT CHR$(179)
3580 IF SD=1 THEN PLAY "L64 E-":PLAY "L64 E-"
3590 NEXT:FOR B=7 TO 10:LOCATE B,F1:PRINT " ":IF F2>0 THEN LOCATE B,F2:PRINT " "
3600 NEXT:GOTO 3630
3610 COLOR 12,0:FOR B=8 TO 11:LOCATE B,39+B*2:PRINT "\":LOCATE B,94-B*2:PRINT "/":IF SD=1 THEN PLAY "L64 E-":PLAY "L64 E-"
3620 NEXT:LOCATE 10,66:PRINT "  "
3630 LOCATE 11,57:PRINT STRING$(20,223):FOR B=1 TO 4*TR:NEXT
3640 LOCATE 10,58:PRINT CHR$(220);STRING$(16,219);CHR$(220):LOCATE 9,60:PRINT STRING$(2,220);STRING$(10,219);STRING$(2,220):FOR B=1 TO 2.5*TR:NEXT
3650 COLOR 7,0:FOR B=9 TO 10:LOCATE B,55:PRINT STRING$(24,32):NEXT:LOCATE 11,56:PRINT STRING$(23,196):LOCATE 20:RETURN
3660 FOR A=1 TO 25*TR:NEXT:GOTO 710
3670 REM *** ENTERPRISE DEFEATED ***
3680 IF C>100 THEN C=100
3690 CLS:LOCATE 4,22:PRINT "Casualties:";STR$(C);"%"
3700 LOCATE 6,24:PRINT "Power at";P
3710 LOCATE 8,20:PRINT "Superstructure at";SS
3720 FOR A=10 TO 12:LOCATE A,20:PRINT "*******************":NEXT
3730 LOCATE 14,23-LEN(TS$)/2:PRINT TS$" defenseless"
3740 LOCATE 16,27:PRINT "Sorry"
3750 IF AV$="FEDERATION" OR AV$="ORION" OR AV$="GORN" THEN 3840 ELSE LOCATE 18,15:PRINT AD$"s don't take prisoners":GOTO 3840
3760 IF C1>100 THEN C1=100
3770 CLS:LOCATE 4,22:PRINT "Casualties:";STR$(C1);"%"
3780 LOCATE 6,24:PRINT "Power at";KP
3790 LOCATE 8,20:PRINT "Superstructure at";SS1
3800 FOR A=10 TO 12:LOCATE A,20:PRINT "*******************":NEXT
3810 LOCATE 14,19+(7-LEN(AD$))/2:PRINT AD$" ship disabled"
3820 LOCATE 16,22:PRINT "Congratulations"
3830 LOCATE 19,13:PRINT "You have been put up for promotion":IF TK=0 OR TT=0 THEN 3850
3840 A=INT(100*(.5+(P/P2)*100+100-((KP/KP1)*100)+(SS/SO)*100+100-(SS1/SO1)*100+C+100-C1)/6)/100:A=A*(TK/TT)^.5:LOCATE 20,14:PRINT "Your performance rating is";STR$(INT(A*100)/100);"%"
3850 PRINT:PRINT TAB(11);"Would you like to try again, Captain?"
3860 A$=INKEY$:IF A$="" THEN 3860
3870 IF A$<>"Y" AND A$<>"y" THEN END ELSE CLS:LOCATE 12,10:PRINT "NAVIGATOR:  Aye aye, Captain.  Returning to Starbase.":COMMON SD,TR:CHAIN "TREK",20
3880 REM *** FIRE ALL PHASERS ***
3890 LOCATE 21,1:PRINT "         HELMSMAN:  Aye, Captain.";STRING$(45,32)
3900 A1=1:BK=0:FOR A=1 TO NP:IF PB(A)<=0 THEN 3930 ELSE BB(A1)=A:PPP(A1)=PB(A)
3910 IF PPP(A1)>MFP THEN PPP(A1)=MFP
3920 PB(A1)=PB(A1)-PPP(A1):A1=A1+1:BK=BK+1:BBK=1
3930 NEXT:IF BK>0 THEN 1370 ELSE PRINT TAB(10);P$" banks are not powered, Captain!":FOR A=1 TO 40*TR:NEXT:GOTO 4180
3940 REM *** FIRE ALL TORPEDOES ***
3950 LOCATE 21,10:PRINT "HELMSMAN:  Aye, Captain.";STRING$(45,32)
3960 A1=1:NT=0:FOR A=1 TO NTT:IF T(A)<>1 THEN 4000
3970 GOSUB 1840:IF NT(A)<>D1 THEN 4000
3980 NT=NT+1:PT(A1)=A
3990 T(PT(A))=0:A1=A1+1
4000 NEXT:IF NT>0 THEN 1650 ELSE PRINT TAB(10);"No "S$(D1)" photon tubes are armed, Captain.":FOR A=1 TO 40*TR:NEXT:GOTO 4180
4010 GOSUB 4700:FOR A=1 TO NP:IF A<>BBB THEN PB(A)=PB(A)+PPP(A)
4015 NEXT:LOCATE 20,10:GOTO 460
4020 REM *** SHOW KLINGON SHIP ON S-R SCAN ***
4030 IF AV=2 THEN GOSUB 4710:RETURN
4040 IF AV=3 THEN GOSUB 5270:RETURN
4050 IF AV=4 THEN GOSUB 5400:RETURN
4060 F1=0:F2=0:IF CLO=1 THEN RETURN ELSE GOSUB 1820:IF D>11 THEN LOCATE 6,67:PRINT CHR$(250):F1=67:RETURN
4070 IF D>18 THEN LOCATE 6,67:PRINT CHR$(249):F1=67:RETURN
4080 IF D>16 THEN LOCATE 6,67:PRINT "-":F1=67:RETURN
4090 IF D>14 THEN LOCATE 6,66:PRINT CHR$(169);CHR$(170):F1=66:F2=67:RETURN
4100 IF D>12 THEN LOCATE 6,66:PRINT CHR$(169);CHR$(205);CHR$(170):F1=66:F2=68:RETURN
4110 IF D>10 THEN LOCATE 6,65:PRINT CHR$(169);STRING$(2,205);CHR$(170):F1=65:F2=68:RETURN
4120 IF D>8 THEN LOCATE 6,64:PRINT CHR$(169);CHR$(205);CHR$(240);CHR$(205);CHR$(170):F1=64:F2=68:RETURN
4130 IF D>6 THEN LOCATE 6,62:PRINT CHR$(218);STRING$(3,205);CHR$(240);STRING$(3,205);CHR$(191):F1=62:F2=70:RETURN
4140 IF D>4 THEN LOCATE 6,61:PRINT "(          )":LOCATE 5,62:PRINT STRING$(4,220);CHR$(254);CHR$(254);STRING$(4,220):F1=61:F2=72:RETURN
4150 IF D>2 THEN LOCATE 6,59:PRINT "(             )":LOCATE 5,60:PRINT STRING$(5,220);CHR$(254);CHR$(240);CHR$(254);STRING$(5,220):F1=59:F2=73:RETURN
4160 LOCATE 4,66:PRINT "__":LOCATE 5,62:PRINT "__======__ ":LOCATE 6,56:PRINT STRING$(9,220);"(";CHR$(174);CHR$(175);")";STRING$(9,220)
4170 LOCATE 7,55:PRINT "/                      \":LOCATE 8,55:PRINT "\\                    //":RETURN
4180 GOSUB 4700:LOCATE 20,10:GOTO 440
4190 REM *** PLASMA WEAPONS ***
4200 COLOR 6:GOSUB 4030:COLOR 7
4210 FOR A=1 TO 40*TR:NEXT:COLOR 15,0:LOCATE 6,66:PRINT CHR$(223)
4220 FOR A=1 TO 8*TR:NEXT:LOCATE 5,66:PRINT CHR$(220):LOCATE 6,65:PRINT CHR$(223);CHR$(219);CHR$(223)
4230 FOR A=1 TO 8*TR:NEXT:LOCATE 5,65:PRINT STRING$(3,220):LOCATE 6,65:PRINT STRING$(3,219)
4240 FOR A=1 TO 8*TR:NEXT:LOCATE 5,66:PRINT CHR$(219):LOCATE 6,64:PRINT CHR$(223):LOCATE 6,68:PRINT CHR$(223):LOCATE 7,66:PRINT CHR$(223)
4250 FOR A=1 TO 8*TR:NEXT:LOCATE 5,64:PRINT STRING$(5,219):LOCATE 6,64:PRINT STRING$(5,219):LOCATE 7,64:PRINT STRING$(5,223)
4260 FOR A=1 TO 8*TR:NEXT:LOCATE 4,65:PRINT STRING$(3,220):LOCATE 5,63:PRINT CHR$(220);STRING$(5,219);CHR$(220):LOCATE 6,63:PRINT STRING$(7,219):LOCATE 7,65:PRINT STRING$(3,219)
4270 FOR A=1 TO 8*TR:NEXT:LOCATE 3,65:PRINT STRING$(3,219):LOCATE 4,63:PRINT STRING$(7,219):LOCATE 5,61:PRINT STRING$(11,219):LOCATE 6,61:PRINT STRING$(11,219):LOCATE 7,63:PRINT STRING$(7,219)
4280 LOCATE 8,65:PRINT STRING$(3,219)
4290 FOR A=1 TO 5*TR:NEXT:LOCATE 3,63:PRINT STRING$(7,219):LOCATE 4,61:PRINT STRING$(11,219):LOCATE 5,59:PRINT STRING$(15,219):LOCATE 6,59:PRINT STRING$(15,219):LOCATE 7,61:PRINT STRING$(11,219):LOCATE 8,63:PRINT STRING$(7,219)
4300 FOR A=1 TO 8*TR:NEXT:LOCATE 2,64:PRINT STRING$(5,219):LOCATE 3,62:PRINT STRING$(9,219):LOCATE 4,60:PRINT STRING$(13,219):LOCATE 5,58:PRINT STRING$(17,219):LOCATE 6,58:PRINT STRING$(17,219):LOCATE 7,60:PRINT STRING$(13,219)
4310 LOCATE 8,62:PRINT STRING$(9,219):LOCATE 9,64:PRINT STRING$(5,219)
4320 FOR A=1 TO 8*TR:NEXT:LOCATE 2,63:PRINT STRING$(7,219):LOCATE 3,61:PRINT STRING$(11,219):LOCATE 4,59:PRINT STRING$(15,219):LOCATE 5,57:PRINT STRING$(19,219):LOCATE 6,57:PRINT STRING$(19,219):LOCATE 7,59:PRINT STRING$(15,219)
4330 LOCATE 8,61:PRINT STRING$(11,219):LOCATE 9,63:PRINT STRING$(7,219):COLOR 7,0
4340 GOSUB 1820:ON PL GOTO 4350,4410,4490
4350 DA1=4:IF D<8 THEN DA1=8
4360 IF D<7 THEN DA1=12
4370 IF D<6 THEN DA1=16
4380 IF D<4 THEN DA1=20
4390 IF D<2 THEN DA1=24
4400 GOTO 4460
4410 DA1=8:IF D<13 THEN DA1=12
4420 IF D<11 THEN DA1=16
4430 IF D<9 THEN DA1=20
4440 IF D<7 THEN DA1=24
4450 IF D<4 THEN DA1=32
4460 NTD=0:DA2=DA1:SH(D1)=SH(D1)-DA1:IF SH(D1)<0 THEN KTD=-SH(D1):DA1=KTD:NTD=DA1/2
4470 IF SD=1 THEN BEEP
4480 FOR A=1 TO NTD:ND(A)=2:NEXT:LOCATE 21:PRINT STRING$(79,32):COLOR 0,15:LOCATE 21,10:PRINT DA2"unit hit to our "S$(D1)" Shields, Captain! ":COLOR 7,0:FOR A=1 TO 93*TR:NEXT:GOTO 2530
4490 DA1=12:IF D<14 THEN DA1=16
4500 IF D<12 THEN DA1=20
4510 IF D<9 THEN DA1=24
4520 IF D<5 THEN DA1=28
4530 GOTO 4460
4540 REM *** DETERMINE DIRECTION FROM KEYPAD ***
4550 A$=RIGHT$(A$,1):IF A$="M" THEN A=6 ELSE IF A$="K" THEN A=4
4560 IF A$="H" THEN A=8 ELSE IF A$="P" THEN A=2
4570 IF A$="G" THEN A=7 ELSE IF A$="O" THEN A=1
4580 RETURN
4590 REM *** SCAN FOR CLOAKED VESSEL ***
4600 LOCATE 21,10:PRINT "Which direction shall I scan, sir?";STRING$(30,32)
4610 A$=INKEY$:IF A$="" THEN 4610 ELSE A=0:GOSUB 4540:IF A<>6 AND A<>2 AND A<>4 AND A<>8 THEN 4610
4620 LOCATE 22,10:PRINT "SCIENCE OFFICER:  Scanning";:FOR A=1 TO 3:FOR B=1 TO 40*TR:NEXT:PRINT " .";:NEXT
4630 GOSUB 4540:IF A=8 THEN A=4
4640 IF A=4 THEN A=3 ELSE IF A=6 THEN A=1
4650 A1=A
4660 Q10=-1:GOSUB 1840:IF A1<>D1 THEN 4690
4670 GOSUB 1830:IF RND*1>.5 THEN 4690
4680 LOCATE 22,28:PRINT "I have him, Captain:"INT(D)"spaces distance.":FOR B=1 TO 13:FOR A=1 TO 150:NEXT:LOCATE K+1,K1*2:PRINT "::":FOR A=1 TO 4*TR:NEXT:LOCATE K+1,K1*2:PRINT "  ":NEXT:GOTO 440
4690 LOCATE 22,10:PRINT AD$"s not located, Captain.";STRING$(38,32):FOR A=1 TO 90*TR:NEXT:GOTO 440
4700 FOR AG=2 TO 10:LOCATE AG,55:PRINT STRING$(24,32):NEXT:RETURN
4710 REM *** SHOW ROMULAN SHIP ON S-R SCAN ***
4720 F1=0:F2=0:IF CLO=1 THEN RETURN ELSE GOSUB 1820:IF D>11 THEN LOCATE 6,67:PRINT CHR$(250):F1=67:RETURN
4730 IF D>18 THEN LOCATE 6,67:PRINT CHR$(249):F1=67:RETURN
4740 IF D>32 THEN LOCATE 6,67:PRINT "-":F1=67:RETURN
4750 IF D>14 THEN LOCATE 6,66:PRINT STRING$(2,196):F1=66:F2=67:RETURN
4760 IF D>12 THEN LOCATE 6,66:PRINT CHR$(196);CHR$(205);CHR$(196):F1=67:RETURN
4770 IF D>10 THEN LOCATE 6,65:PRINT CHR$(196);STRING$(2,205);CHR$(196):F1=66:F2=67:RETURN
4780 IF D>8 THEN LOCATE 6,64:PRINT CHR$(196);CHR$(205);"=";CHR$(205);CHR$(196) F1=66:RETURN
4790 IF D>6 THEN LOCATE 6,62:PRINT "\";STRING$(3,205);"=";STRING$(3,205);"/":F1=66:RETURN
4800 IF D>4 THEN LOCATE 6,61:PRINT CHR$(248);"\";CHR$(196);STRING$(2,205);"==";STRING$(2,205);CHR$(196);"/";CHR$(248):F1=66:F2=67:RETURN
4810 IF D>2 THEN LOCATE 6,59:PRINT CHR$(248);"\";STRING$(2,196);STRING$(2,205);"=";CHR$(240);"=";STRING$(2,205);STRING$(2,196);"/";CHR$(248):F1=66:RETURN
4820 LOCATE 5,55:PRINT "o__      ";:COLOR 1,0:PRINT "     ";:COLOR 4,0:PRINT "      __o":LOCATE 6,58:PRINT"^^\"STRING$(5,223);CHR$(248);STRING$(5,223);"/^^":F1=66:RETURN
4830 REM *** SCAN PHASE [NON-CLOAKED SHIPS] ***
4840 LOCATE 22,10:IF SE=-1 THEN PRINT "Sensors are out, Captain.";STRING$(44,32):FOR A=1 TO 40*TR:NEXT:GOTO 440
4850 LOCATE 22,10:PRINT "SCIENCE OFFICER:  Scanning";:FOR A=1 TO 3:FOR B=1 TO 40*TR:NEXT:PRINT " .";:NEXT:LOCATE 22,10
4860 GOSUB 1830:IF RND*1<.5 THEN PRINT "I cannot obtain a sensors lock, Captain.":GOTO 440
4870 PRINT "Sensors lock obtained on "AD$" vessel, Captain.":PRINT TAB(10);"Vessel checks out as a "VN$"."
4880 GOSUB 4700:LOCATE 2,55:PRINT "Information desire  on:"
4890 LOCATE 4,56:PRINT "1. Engines & Power":LOCATE 5,56:PRINT "2. "D1$" Status":LOCATE 6,56:PRINT "3. Torpedo Status":LOCATE 7,56:PRINT "4. Shield Status":LOCATE 8,56:PRINT "5. Superstructure":LOCATE 9,56:PRINT "6. Casualties"
4900 A$=INKEY$:IF A$="" THEN 4900 ELSE A=VAL(A$)
4910 IF A<1 OR A>6 THEN 4900 ELSE GOSUB 4700
4920 LOCATE 3,56:ON A GOTO 4930,4950,4980,5030,5050,5070
4930 PRINT AD$" ship still":LOCATE 4,56:PRINT "has"KP"power points,":LOCATE 5,56:IF KP<KP1 THEN PRINT "down"STR$(100-INT(100*K /KP1))"% from"KP1"units." ELSE PRINT "and is at maximum."
4940 LOCATE 7,56:PRINT "Warp Engines at"KW:LOCATE 8,56:PRINT "Impulse Engine at"KI:GOTO 5080
4950 BB=3:IF KBW>7 THEN BB=2:PRINT CHR$(30);
4955 PRINT AD$" "D1$"s:":FOR A=1 TO KBW
4960 LOCATE A+BB,56:PRINT A;:IF KB(A)<0 THEN PRINT "DAMAGED" ELSE PRINT STRING$(KB(A),219);KB(A)
4970 NEXT:GOTO 5080
4980 IF PL=0 THEN 5000 ELSE PRINT AD$" Plasma Weapon":LOCATE 5,57
4990 IF KB1(1)=-1 THEN PRINT "is damaged" ELSE IF PLL=1 THEN PRINT "is armed" ELSE IF PLL=0 THEN PRINT "is unarmed"
5000 IF PL>0 THEN 5080 ELSE IF KMW<7 THEN BB=4 ELSE BB=3:IF KMW>7 THEN BB=2:PRINT CHR$(30);
5005 PRINT AD$" Torpedoes:":FOR A=1 TO KMW:LOCATE A+BB,55:PRINT A;:IF KB1(A)=-1 THEN PRINT "  DAMAGED" ELSE IF A>KT THEN PRINT "  UNARMED" ELSE PRINT "   ARMED"
5010 NEXT:IF KMW<1 THEN LOCATE 6,56:PRINT "This vessel has no":LOCATE 7,56:PRINT "torpedoes, Captain.":FOR A=1 TO 100*TR:NEXT:A$=INKEY$:GOTO 4890
5020 GOTO 5080
5030 PRINT AD$" deflectors:":FOR A=1 TO 4:LOCATE 4+A,56:PRINT A;:IF KD(A)<0 THEN PRINT " DAMAGED" ELSE PRINT STRING$(KS(A),219);KS(A)
5040 NEXT:GOTO 5080
5050 PRINT AD$" Superstructure":LOCATE 5,56:PRINT "is now at"SS1"points,":LOCATE 7,56:IF SS1<SO1 THEN PRINT "down from"SO1"points" ELSE PRINT "and is at maximum"
5060 GOTO 5080
5070 PRINT AD$" Casualties":LOCATE 5,60:PRINT "are at"STR$(C1)"%"
5080 A$=INKEY$:IF A$="" THEN 5080
5090 GOSUB 4700:GOTO 440
5100 REM *** STATUS REPORT ***
5110 GOSUB 4700
5120 LOCATE 3,56:PRINT "SCIENCE OFFICER:":LOCATE 5,56:PRINT "Power     is at"STR$(INT(100*P/P2))"%"
5130 LOCATE 6,56:PRINT "Accuracy  is at"STR$(INT(100-INT(C/10)*10))"%"
5140 FPR=0:FPP=0:FOR A=1 TO NP:IF DM(3)>0 THEN KK=MFP+3 ELSE KK=MFP+2
5150 FPP=FPP+KK:IF PH(A)=>0 THEN FPR=FPR+KK
5160 NEXT:FOR A=1 TO NTT:FPP=FPP+TD:IF T(A)=>0 THEN FPR=FPR+TD
5170 NEXT:LOCATE 7,56:PRINT "Firepower is at"STR$(INT(100*FPR/FPP))"%"
5180 SHD=0:FOR A=1 TO 4:IF SH(A)=>0 THEN SHD=SHD+25
5190 NEXT:LOCATE 8,56:PRINT "Shielding is at"STR$(SHD)"%"
5200 LOCATE 9,56:PRINT "Superstructure is at":LOCATE 10,71:PRINT STR$(INT(100*SS/SO))"%"
5210 A$=INKEY$:IF A$="" THEN 5210 ELSE GOSUB 4700:GOTO 440
5220 REM *** ENEMY TAKES A HIT ON SHIELD "KSH" ***
5230 IF KSH>0 THEN 5250 ELSE IF KS(1)<1 AND KS(2)<1 AND KS(3)<1 AND KS(4)<1 THEN  GOSUB 1830:KSH=INT(RND*4+1):GOTO 5250
5240 GOSUB 1830:KSH=INT(RND*4+1):IF KS(KSH)<1 THEN 5240
5250 KS(KSH)=KS(KSH)-PB2:IF KS(KSH)<0 THEN NTK=NTK+1:NK(NTK)=-KS(KSH):DA=DA-KS(KSH):KS(KSH)=0
5260 RETURN
5270 REM *** SHOW GORN ON S-R SCAN ***
5280 F1=0:F2=0:GOSUB 1820:IF D>11 THEN LOCATE 6,67:PRINT CHR$(250):F1=67:RETURN
5290 IF D>18 THEN LOCATE 6,67:PRINT CHR$(249):F1=67:RETURN
5300 IF D>16 THEN LOCATE 6,67:PRINT "^":F1=67:RETURN
5310 IF D>14 THEN LOCATE 6,67:PRINT CHR$(127):F1=67:RETURN
5320 IF D>12 THEN LOCATE 6,66:PRINT "/"CHR$(167);"\":F1=67:RETURN
5330 IF D>10 THEN LOCATE 6,65:PRINT CHR$(22);STRING$(2,223);CHR$(22):F1=66:F2=67:RETURN
5340 IF D>8 THEN LOCATE 6,66:PRINT CHR$(220);CHR$(219);CHR$(220):LOCATE 7,65:PRINT CHR$(223);"   ";CHR$(223):F1=66:F2=68:RETURN
5350 IF D>6 THEN LOCATE 5,67:PRINT CHR$(220):LOCATE 6,65:PRINT CHR$(220);STRING$(3,219);CHR$(220):LOCATE 7,64:PRINT CHR$(223);"     ";CHR$(223):F1=66:F2=68:RETURN
5360 IF D>4 THEN LOCATE 5,66:PRINT STRING$(3,220):LOCATE 6,64:PRINT CHR$(220);STRING$(5,219);CHR$(220):LOCATE 7,63:PRINT CHR$(223);"       ";CHR$(223):F1=65:F2=69:RETURN
5370 IF D>2 THEN LOCATE 4,65:PRINT STRING$(4,220):LOCATE 5,63:PRINT CHR$(220);STRING$(6,219);CHR$(220):LOCATE 6,61:PRINT CHR$(220);STRING$(10,219);CHR$(220):LOCATE 7,61:PRINT CHR$(223);"          ";CHR$(223):F1=64:F2=70:RETURN
5380 LOCATE 3,65:PRINT STRING$(4,220):LOCATE 4,62:PRINT CHR$(220);STRING$(8,219);CHR$(220):LOCATE 5,60:PRINT CHR$(220);STRING$(12,219);CHR$(220)
5390 LOCATE 6,60:PRINT CHR$(219);STRING$(12,223);CHR$(219):LOCATE 7,59:PRINT STRING$(3,223);"          ";STRING$(3,223):F1=63:F2=70:RETURN
5400 REM *** SHOW ORION ON S-R SCAN ***
5410 F1=0:F2=0:GOSUB 1820:IF D>11 THEN LOCATE  ,67:PRINT CHR$(250):F1=67:RETURN
5420 IF D>18 THEN LOCATE 6,67:PRINT CHR$(249):F1=67:RETURN
5430 IF D>16 THEN LOCATE 6,67:PRINT "-":F1=67:RETURN
5440 IF D>14 THEN LOCATE 6,67:PRINT "v":F1=67:RETURN
5450 IF D>12 THEN LOCATE 6,66:PRINT "\=/":F1=67:RETURN
5460 IF D>10 THEN LOCATE 6,66:PRINT "\==/":F1=67:F2=68:RETURN
5470 IF D>8 THEN LOCATE 6,65:PRINT CHR$(248);"\==/";CHR$(248):F1=67:F2=68:RETURN
5480 IF D>6 THEN LOCATE 5,64:PRINT CHR$(248)"\"CHR$(220)"  "CHR$(220)"/"CHR$(248):LOCATE 6,67:PRINT STRING$(2,223):F1=67:F2=68:RETURN
5490 IF D>4 THEN LOCATE 5,62:PRINT CHR$(248);CHR$(220)"      "CHR$(220);CHR$(248):LOCATE 6,64:PRINT CHR$(223);STRING$(4,219);CHR$(223):F1=65:F2=68:RETURN
5500 IF D>2 THEN LOCATE 4,61:PRINT "o          o":LOCATE 5,62:PRINT CHR$(223);CHR$(220)"      "CHR$(220);CHR$(223):LOCATE 6,64:PRINT CHR$(223);STRING$(4,219);CHR$(223):F1=65:F2=68:RETURN
5510 LOCATE 4,59:PRINT "o              o":LOCATE 5,60:PRINT CHR$(223);STRING$(2,220);"        ";STRING$(2,220);CHR$(223)
5520 LOCATE 6,62:PRINT CHR$(223);STRING$(2,219);STRING$(4,220);STRING$(2,219);CHR$(223):LOCATE 7,64:PRINT STRING$(6,223):F1=65:F2=68:RETURN
5530 REM *** FEDERATION PLASMA WEAPON DAMAGE ***
5540 GOSUB 1820:ON FPL GOTO 5550,5590,5630
5550 TD=4:IF D<8 THEN TD=8
5560 IF D<7 THEN TD=12 ELSE IF D<6 THEN TD=16
5570 IF D<4 THEN TD=20 ELSE IF D<2 THEN TD=24
5580 GOTO 5650
5590 TD=8:IF D<13 THEN TD=12
5600 IF D<11 THEN TD=16 ELSE IF D<9 THEN TD=20
5610 IF D<7 THEN TD=24 ELSE IF D<4 THEN TD=32
5620 GOTO 5650
5630 TD=12:IF D<14 THEN TD=16 ELSE IF D<12 THEN TD=20
5640 IF D<9 THEN TD=24 ELSE IF D<5 THEN TD=28
5650 IF SD=1 THEN FOR A=220 TO 400 STEP 3:SOUND A,.05*TR/80:NEXT
5651 COLOR 6:LOCATE K+1,K1*2:PRINT STRING$(2,219);:FOR A=1 TO 70*TR:NEXT:COLOR 14:PRINT STRING$(2,29);STRING$(2,219)
5652 FOR A=1 TO 50*TR:NEXT:COLOR 0,15:LOCATE K+1,K1*2:PRINT K$;:FOR A=1 TO 50*TR:NEXT:COLOR 7,0:LOCATE K+1,K1*2:PRINT K$
5654 DA=TD:LOCATE 21,10:COLOR 0,12:PRINT TD;"unit hit on "AD$" vessel, Captain.":COLOR 7,0:PRINT TAB(22);"(Press a key)"
5655 A$=INKEY$:IF A$="" THEN 5655
5660 FOR A=1 TO TD/2:IF KSH>0 THEN 5680 ELSE IF KS(1)<1 AND KS(2)<1 AND KS(3)<1 AND KS(4)<1 THEN GOSUB 1830:KSH=INT(RND*4+1):GOTO 5680
5670 GOSUB 1830:KSH=INT(RND*4+1):IF KS(KSH)<1 THEN 5670
5680 KS(KSH)=KS(KSH)-2:IF KS(KSH)<0 THEN NTK=NTK+1:NK(NTK)=-KS(KSH):KS(KSH)=0
5690 NEXT:GOTO 2040
5700 GOSUB 4700
5710 COLOR 1,0:LOCATE 2,56:PRINT "COMMANDS:":COLOR 7,0:LOCATE 3,56:PRINT "FIRE (ALL) "P1$"S"
5720 LOCATE 4,56:PRINT "FIRE (ALL) TORPEDOES":LOCATE 5,56:PRINT "TACTICAL":LOCATE 6,56:PRINT "SCAN":LOCATE 7,56:PRINT "STATUS":LOCATE 8,56:PRINT "FIRING / COMPUTE"
5730 LOCATE 9,56:PRINT "SOUND ON/OFF":LOCATE 10,56:PRINT "SELF-DESTRUCT"
5740 A$=INKEY$:IF A$="" THEN 5740 ELSE GOSUB 4700:GOTO 440
5750 GOSUB 4700
5760 LOCATE 3,56:PRINT "SCIENCE OFFICER:":LOCATE 5,56:IF CLO>0 THEN PRINT "The "AD$"s are":LOCATE 6,56:PRINT "cloaked, Captain!":GOTO 440
5770 IF D<1 THEN LOCATE 3,56:PRINT "SCIENCE OFFICER:":LOCATE 5,56:PRINT "We are too close":LOCATE 6,56:PRINT "to fire, Captain.":GOTO 440
5775 PRINT "Our chances of hitting":LOCATE 6,56:PRINT "the "AD$" ship":LOCATE 7,56:PRINT "from this distance with"
5780 LOCATE 8,56:PRINT P$"s are";:IF D+INT(C/10)>LEN(FP$) THEN PRINT " 0%." ELSE PRINT STR$(10*VAL(MID$(FP$,INT(D),1))+10-10*INT(C/10))"%."
5790 IF NTT<1 THEN 5800 ELSE LOCATE 9,56:PRINT "With torpedoes, they":LOCATE 10,56:PRINT "are";:IF MID$(FP$,INT(D),1)=MID$(FT$,INT(D),1) THEN PRINT " also";
5795 IF D+INT(C/10)>LEN(FT$) THEN PRINT " 0%." ELSE PRINT STR$(10*VAL(MID$(FT$,INT(D),1))+10-10*INT(C/10))"%."
5800 A$=INKEY$:IF A$="" THEN 5800 ELSE GOSUB 4700:GOTO 440
5900 TW=0:FOR A=1 TO KBW:IF K1(A)>0 THEN TW=TW+1
5910 NEXT:FOR A=1 TO KMW:IF KM(A)>0 THEN TW=TW+1
5920 NEXT:IF D>0 THEN 5950 ELSE IF SE1<0 OR TW<1 THEN 3430
5930 GOSUB 1830:K=K+INT(RND*3)-1:K1=K1+INT(RND*3)-1:GOTO 3430
5950 GOSUB 1830:IF RND*1<.2 THEN 5930 ELSE IF SE1<0 OR TW<1 THEN K=L:K1=L1:GOTO 3430
5960 GOSUB 1830:K=K+INT(RND*5)-2:K1=K1+INT(RND*5)-2:IF K=L AND K1=L1 THEN 5960 ELSE 3430
6000 REM *** ENEMY HAS DECLOAKED AND FIRES ***
6010 TM=0:TBW=0:IF PL<1 THEN 6020 ELSE IF KM(1)<1 THEN 6100 ELSE 2310
6020 FOR A=1 TO KMW:IF KM(A)>0 THEN TM=TM+KTD
6030 NEXT:FOR A=1 TO KBW:IF KB(A)>0 THEN TBW=TBW+KB(A)
6040 NEXT:IF TBW>TM THEN 6100 ELSE FOR A=1 TO KBW:K1(A)=0:IF KB(A)>0 THEN K1(A)=KB(A)
6050 NEXT:GOTO 2180
6100 B51=1:GOTO 6245
6200 IF DCLK<1 AND D>LEN(AP$) AND D>LEN(AT$) THEN 2535 ELSE IF AFB>0 THEN 6240 ELSE IF AFM>0 THEN 6210
6205 GOSUB 6260:IF TM<TBW OR B51>0 THEN 6240 ELSE IF MMM=>MNT THEN 2535
6210 FOR A=1 TO KMW:IF KM(A)>0 THEN KM(A)=0:GOTO 2330
6220 NEXT:GOTO 2535
6240 IF B51=>KBW THEN 2535
6245 IF B51<2 THEN B51=2
6247 FOR A=1 TO KBW:IF KB(A)>0 THEN B5=B51-1:K1(B5)=KB(B5):GOTO 2190
6250 NEXT:GOTO 2535
6260 TM=0:TBW=0:FOR A=1 TO KBW:IF KB(A)>0 THEN TM=TM+KB(A)
6270 NEXT:FOR A=1 TO KMW:IF KM(A)>0 THEN TBW=TBW+KTD
6280 NEXT:RETURN

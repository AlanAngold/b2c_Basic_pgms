# NOTE: Type forced to be:
# trs2 - TRS-80 Level II BASIC tokenized file
  100 REM 
  110 REM BY * ANTHONY WOOD *
  120 REM 
  130 CLS : PRINT "                      * SPACE TAXI *"
  140 PRINT : PRINT "THE OBJECT OF THIS GAME IS TO GO OVER THE MIDDLE MT."
  150 PRINT "AND LAND ON THE OTHER SIDE. ENTER THE HORIZ. & VERT."
  160 PRINT "THRUST. IF YOU HIT THE RADIATION DOME YOU WILL CRASH."
  170 PRINT : PRINT "HIT ANY KEY TO START."
  180 IF INKEY$ = ""THEN 180 
  190 CLS : FOR T = 0 TO 127 : SET (T,0) : NEXT 
  200 PRINT @23," RADIATION DOME "
  210 READ X1,Y1,X2,Y2 : GOSUB 290 : FOR Z% = 1 TO 5 : X1 = X2 : Y1 = Y2 : READ X2,Y2
  220 GOSUB 290 : NEXT : X = 22 : Y = 36 : XX = X : YY = Y
  230 GOSUB 460 : PRINT @664,"VERT. SPEED =";V; : PRINT @728,"HORZ. SPEED =";H;
  240 PRINT @539,"FUEL =";45 - F;
  250 PRINT @834,""; : INPUT "VERT. FUEL";V1 : PRINT @845,"     ";
  260 PRINT @898,"HORZ. FUEL"; : INPUT H1 : PRINT @909,"     ";
  270 MM = 1 : F = F + ABS (V1) + ABS (H1)
  280 X1 = X : Y1 = Y : H = H + H1 : V = V + V1 - 2 : X2 = X + H : Y2 = Y - V : IF F > 30 THEN 530 : ELSE GOSUB 290 : GOTO 230 
  290 DY = Y1 - Y2 : DX = X1 - X2
  300 IF DX = 0 THEN 390 
  310 M = DY / DX
  320 IF ABS (M) > 1 THEN 390 
  330 I = - SGN (DX)
  340 FOR X = X1 TO X2 STEP I
  350 Y = M * (X - X1) + Y1 + .5
  360 IF MM = 1 THEN GOSUB 460 : ELSE SET (X,Y)
  370 NEXT 
  380 RETURN 
  390 IF DY = 0 THEN RETURN : ELSE M = DX / DY
  400 I = - SGN (DY)
  410 FOR Y = Y1 TO Y2 STEP I
  420 X = M * (Y - Y1) + X1 + .5
  430 IF MM = 1 THEN GOSUB 460 : ELSE SET (X,Y)
  440 NEXT : RETURN 
  450 DATA 0,18,20,37,37,37,62,6,89,39,102,39,127,16
  460 REM -SUBROUTINE TO PRINT SHIP
  470 RESET (XX,YY) : RESET (XX + 1,YY - 1) : RESET (XX + 2,YY) : XX = X : YY = Y
  480 IF Y > = 38 AND V > - 3 AND H < 3 AND X > 70 THEN 560 
  490 IF POINT (X,Y) = - 1 OR POINT (X + 1,Y - 1) = - 1 OR POINT (X + 2,Y) = - 1 THEN 510 
  500 SET (X,Y) : SET (X + 1,Y - 1) : SET (X + 2,Y) : RETURN 
  510 PRINT @960,"BOOM!!! YOU CRASHED ";
  520 GOTO 520 
  530 IF F = > 45 THEN 550 : ELSE PRINT @960,"     ** FUEL LOW **";
  540 GOSUB 290 : GOTO 230 
  550 IF (45 - F) < 0 THEN F = F - ( ABS (V1) + ABS (H1)) : V = V - V1 + 2 : H = H - H1 : GOTO 230
  555 V1 = 0 : H1 = 0 : PRINT @960,"   ** OUT OF FUEL **       "; : GOSUB 290 : GOTO 280
  560 GOSUB 500 : PRINT @960,"     ** YOU LANDED **"; : GOTO 520

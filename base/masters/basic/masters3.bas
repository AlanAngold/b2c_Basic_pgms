    1 REM NOTE: Basic language type forced to be:
    2 REM       trs2 - TRS-80 Level II BASIC tokenized file
   10 CLEAR200
   15 DEFINTX,Y
   20 CLS:GOSUB875
   25 CLS:PRINTCHR$(23):PRINT"T H E   M A S T E R S":PRINT
   30 INPUT"=ENTER= THE NUMBER OF PLAYERS";PL
   35 IFPL>4GOTO25
   40 PRINT:FORI=1TOPL
   45 PRINT"NAME OF PLAYER # ";I;:INPUTP$(I)
   50 NEXT
   55 FORR=1TO9:CLS:GOSUB725:GOSUB840:P(R)=P
   60 FORRR=1TOPL:S=0
   65 GOSUB770:GOSUB855
   70 M=1:N=RND(7)+21:SET(M,N):SET(M+1,N)
   75 GOSUB475
   80 GOSUB270
   85 PRINT@256,"            ";
   90 A(RR,R)=S
   95 FORJ=1TO3
  100 PRINT@PT,P$(RR);"  IS  ON  THE  GREEN !";
  105 FORI=1TO300:NEXT:PRINT@PT,CHR$(30):FORI=1TO200:NEXT:NEXT
  110 NEXTRR
  115 FORRR=1TOPL:M=M(RR):S=0
  120 GOSUB275:A(RR,R)=A(RR,R)+S
  125 FORI=1TO999:NEXT
  130 NEXTRR
  135 GOSUB150
  140 NEXTR
  145 GOTO145
  150 CLS:FORX=0TO256+PL*128STEP128:PRINT@X,STRING$(63,140);:NEXT
  155 FORI=0TOR:FORJ=0TOPL+1:PRINT@I*4+J*128+86,CHR$(170);
  160 NEXT:NEXT
  165 FORX=1TO9:PRINT@X*4+19+64,X;:NEXT
  170 PRINT@80,"HOLE";:PRINT@208,"PAR";:PRINT@68,"T H E";
  175 PRINT@128,"M A S T E R S   ";:PRINT@187," TOT";
  180 FORI=1TOPL:PRINT@I*128+197,P$(I);:NEXT
  185 FORI=1TOR:PRINT@I*4+211,P(I);
  190 FORJ=1TOPL:PRINT@128*J+211+I*4,A(J,I);
  195 NEXT:NEXT
  200 X=0:FORI=1TOR:X=P(I)+X:NEXT:PRINT@251,X;
  205 FORI=1TOPL
  210 X=0:FORJ=1TOR:X=X+A(I,J):NEXT:PRINT@I*128+251,X;:NEXT
  215 IFR<>9THEN235
  220 PRINT@960,"ANOTHER ROUND";
  225 INPUTA$
  230 IFLEFT$(A$,1)="Y"ORLEFT$(A$,1)="y"THENRUN25ELSE1005
  235 PRINT@960,"";:INPUT"PRESS =ENTER= TO CONTINUE ";A$
  240 RETURN
  245 GOSUB270
  250 PRINT@PT,"* OUT OF BOUNDS PENALTY *  STROKE AND DISTANCE!";
  255 M=M1:N=N1:FORJ=1TO1000:NEXTJ:S=S+1
  260 SET(M,N):SET(M+1,N):GOSUB270
  265 PRINT@960,STRING$(61,192);:RETURN
  270 FORI=PTTOPT+128STEP64:PRINT@I,CHR$(30);:NEXT:RETURN
  275 CLS:ST=(-1)^(RND(2)):T=RND(90)+20:Y=33:IFABS(T-90)<7GOTO275
  280 IF(M<10)*(RND(10)<3)THENM=0
  285 PRINT@960,P$(RR);"'S  PUTT.";
  290 PRINT" ENTER TWO DIGITS OR ONE DIGIT & <ENTER>";
  295 FORX=0TO125STEP3:SET(X,Y):SET(X+1,Y):SET(X+2,Y)
  300 SET(X,Y+1):SET(X+1,Y+1):SET(X+2,Y+1)
  305 IFABS(90-X)<10GOTO320
  310 IF(RND(5)=1)*(X>T)THENY=Y+ST:IFY>44THENY=Y-ST
  315 IF(RND(5)=1)*(X<T)THENY=Y-ST
  320 IFX=90THENH=Y
  325 NEXT:RESET(89,H+1):RESET(90,H+1):RESET(91,H+1)
  330 SET(89,H+2):SET(90,H+2):SET(91,H+2)
  335 PRINT@110,"*******";:PRINT@238,"*******";:PRINT@179," ***";
  340 PRINT@176,R;
  345 X=90-M:FORY=9TO45:IFPOINT(X,Y)THENY=Y-2:GOTO355
  350 NEXT
  355 FORN=2TOH-4:SET(90,N):NEXT:GOTO455
  360 SET(X,Y):SET(X+1,Y)
  365 PRINT@64,"                                 ";
  370 PRINT@0,"ENTER LENGTH OF PUTT ( IN FEET )";
  375 PRINT@64,"?";
  380 S=S+1
  385 A$=INKEY$:IFA$=""THEN385ELSEA=VAL(A$)
  390 B$=INKEY$:IFB$=""THEN390
  395 IFB$=CHR$(13)THENA=0:B$=A$
  400 B=VAL(B$)
  405 PX=A*10+B
  410 PRINT@64,"PUTT ATTEMPT IS ";PX;" FEET.  ";
  415 A=-1:Q=1:IFX>90THENQ=-1
  420 A=A+1:IFA>=PXTHEN455
  425 RESET(X,Y):RESET(X+1,Y):X=X+Q
  430 IFX>121THENX=117+RND(3):GOTO450
  435 IFX<1THENX=2+RND(2):GOTO450
  440 IFPOINT(X,Y+1)THENY=Y-1:A=A+RND(2)+2:GOTO450
  445 IFPOINT(X,Y+2)=0THENY=Y+1:A=A-RND(2)-2
  450 SET(X,Y):SET(X+1,Y):GOTO420
  455 IF(X<>89)*(X<>90)GOTO360
  460 RESET(89,Y):RESET(90,Y):RESET(91,Y):RESET(X,Y):RESET(X+1,Y)
  465 SET(X,Y+3):SET(X+1,Y+3)
  470 RETURN
  475 IFSG>0THENPT=0ELSEPT=768
  480 IFSG>0THENPP=832ELSEPP=0
  485 PRINT@PP,"HOLE # ";R;
  490 PRINT@PP+64,"PAR ";P;:PRINT@PP+128,"YARDS ";Y;
  495 PRINT@PT,P$(RR);"'S ";
  500 PRINT"CLUB CHOICE :  W=WOOD  I=IRON  S=SAND WEDGE";
  505 S=S+1
  510 C$=INKEY$:IFC$=""THEN510
  515 PRINT@PT,CHR$(30)
  520 IFC$<>"W"THEN540ELSEPRINT@PT,"ENTER WOOD # (1-4) ?";
  525 D$=INKEY$:IFD$=""THEN525ELSEPRINTD$;
  530 W=VAL(D$)
  535 IFW>4THEN515ELSEY1=180+10*(5-W)+RND((6-W)*10):GOTO565
  540 IFC$<>"I"THEN560ELSEPRINT@PT,"ENTER IRON # (1-9) ?";
  545 D$=INKEY$:IFD$=""THEN545ELSEPRINTD$;
  550 W=VAL(D$)
  555 IFW>9THEN515ELSEY1=80+10*(10-W)+RND(18+2*(10-W)):GOTO565
  560 IFC$<>"S"THEN500ELSEY1=30+RND(20)
  565 PRINT@PT,CHR$(30)
  570 IFM=1THENJ=5:GOTO600
  575 PRINT@PT+4,"1 2 3 4";:PRINT@PT+132,"9 8 7 6";
  580 PRINT@PT+64,"DIR   ";CHR$(140);"   5";
  585 PRINT@PT+15,"ENTER DIRECTION  ?";
  590 B$=INKEY$:IFB$=""THEN590ELSEPRINTB$;
  595 J=VAL(B$)
  600 SS=RND(3)-2:RESET(M,N):RESET(M+1,N)
  605 GOSUB675:T=INT(Y1/W)
  610 M1=M:N1=N
  615 FORI=1TOT:RESET(M,N):RESET(M+1,N):GOSUB675
  620 IF(N<1)+(N>46)+(M<1)+(M>126)GOSUB245:GOTO495
  625 IFRND(9)=1THENN=N+SS:IFJ<>5THENN=N-SS
  630 SET(M,N):SET(M+1,N):NEXT
  635 G1=G-64:G2=(INT((N+3)/3)-1)*64+INT(M/2)
  640 FORJ=G1TOG1+128STEP64:FORI=JTOJ+7:IFI<>G2GOTO660
  645 M2=(G+4-INT((G+3)/64)*64)*2:N2=INT((G+3)/64)*3+1
  650 M=ABS(M-M2)*10+ABS(N-N2)*10-RND(9):IFM>89THENM=80+RND(9)
  655 M(RR)=M:RETURN
  660 NEXT:NEXT
  665 I=PT:FORK=1TO3:PRINT@I,CHR$(30);:I=I+64:NEXT
  670 GOTO495
  675 ONJGOTO680,685,690,695,700,705,710,715,720
  680 M=M-2:N=N-1:W=18:RETURN
  685 N=N-1:W=10:RETURN
  690 M=M+2:N=N-1:W=18:RETURN
  695 M=M+4:N=N-1:W=20:RETURN
  700 M=M+2:W=10:RETURN
  705 M=M+4:N=N+1:W=20:RETURN
  710 M=M+2:N=N+1:W=18:RETURN
  715 N=N+1:W=10:RETURN
  720 M=M-2:N=N+1:W=18:RETURN
  725 PRINT@384,"TEE";:PRINT@640,"TEE";
  730 SG=(-1)^(RND(2)):Y=RND(18)
  735 IFY>15THENX2=SG*(4+RND(2)):Y=RND(68)+500:P=5:GOTO760
  740 IF(Y>3)*(Y<16)THENY=RND(180)+300:P=4:GOTO750
  745 Y=RND(70)+150:P=3
  750 X2=RND(9)-5:IFY<300THENX2=0
  755 IFY>420THENX2=SG*(3+RND(2))
  760 Y1=Y-ABS(X2)*18:X1=INT(Y1/8)-8:IFX1>60THENX1=60
  765 X1=INT((X1-ABS(X2)*4)/4)-1
  770 C=0
  775 FORI=520TO572STEP4:C=C+1
  780 IFC>X1+1GOTO795
  785 PRINT@I+128,"F";:PRINT@I-128,"F";
  790 GOTO830
  795 PC=I+SG*(C-X1)*64
  800 IFSG>0ANDPC>256PRINT@PC-256,"F";
  805 IFSG>0ANDPC+128<1020PRINT@PC+128,"F";
  810 IFSG>0GOTO825
  815 IFPC+256<1020PRINT@PC+256,"F";
  820 IFPC>128PRINT@PC-128,"F";
  825 IFC>X1+ABS(X2)GOTO835
  830 NEXTI
  835 RETURN
  840 G=PC+64*(RND(3)-2)
  845 IFG<64THENG=G+64:GOTO845
  850 IFG+64>1020THENG=G-64:GOTO850
  855 PRINT@G,"G   +   G";
  860 PRINT@G-61,"G  G";
  865 PRINT@G+67,"G  G";
  870 RETURN
  875 PRINTTAB(10),"T H E   M A S T E R S":PRINT
  880 PRINT"     TO PLAY, SIMPLY ENTER THE CLUB TYPE (WOOD OR IRON OR SAND"
  885 PRINT"WEDGE) AND THE CLUB NUMBER.  ON ALL FAIRWAY SHOTS, YOU ALSO"
  890 PRINT"HAVE THE OPTION OF DIRECTION (1-9) AS WILL BE INDICATED BY"
  895 PRINT"A CHART LIKE THE ONE BELOW.
  900 PRINT@448,"1 2 3 4":PRINT@514,CHR$(140);"   5"
  905 PRINT@576,"9 8 7 6"
  910 PRINT@468,"SO IF YOU WISH TO HIT UP AND TO THE RIGHT,";
  915 PRINT@532,"YOU SHOULD ENTER A 3 OR A 4 FOR DIRECTION.";
  920 PRINT@704,"     FROM ONE TO FOUR MAY PLAY EACH ROUND.  FIRST, EACH"
  925 PRINT"PLAYER WILL SHOOT UNTIL HE REACHES THE GREEN.  WHEN ALL PLAYERS"
  930 PRINT"HAVE REACHED THE GREEN, THE PUTTING SURFACE WILL BE DISPLAYED."
  935 PRINT:INPUT"PRESS =ENTER= TO CONTINUE ";A$:CLS
  940 PRINTTAB(10),"T H E   M A S T E R S":PRINT
  945 PRINT"     EACH PLAYER WILL TAKE HIS TURN PUTTING UNTIL HE HAS MADE"
  950 PRINT"THE PUTT.  SINCE TWO PLAYERS SELDOM PUTT FROM THE SAME SPOT,"
  955 PRINT"EACH PLAYER WILL HAVE A DIFFERENT PUTTING SURFACE TO CONTEND"
  960 PRINT"WITH.  AFTER ALL PLAYERS HAVE HOLED OUT, A CURRENT SCORE CARD"
  965 PRINT"WILL BE DISPLAYED -- THEN IT IS ON TO THE NEXT HOLE."
  970 PRINT:PRINT"     SINCE THERE ARE ADVANTAGES TO HITTING THIRD OR FOURTH"
  975 PRINT"WHEN MORE THAN ONE GOLFERS ARE PARTICIPATING, THE MORE EXPER-"
  980 PRINT"IENCED SHOOTERS SHOULD ENTER THEIR NAMES AS PLAYER #1, "
  985 PRINT"PLAYER #2, AND SO ON.  THIS WAY, THE BETTER PLAYERS WILL HAVE"
  990 PRINT"TO HIT FIRST, AND THE OTHERS CAN LEARN FROM THEIR SHOTS."
  995 PRINT
 1000 INPUT"PRESS  =ENTER= TO BEGIN ";A$:RETURN
 1005 CLS:PRINT:PRINT"THANKS FOR PLAYING"
 1010 PRINT:PRINT"TO PLAY AGAIN, TYPE 'RUN'."
 1015 END

    1 REM NOTE: Basic language type forced to be:
    2 REM       trs2 - TRS-80 Level II BASIC tokenized file
    1 GOSUB 7000
    2 CLEAR 200
    3 DEFINT X,Y
    5 CLS:GOSUB1000
   10 CLS:PRINTCHR$(23):PRINT"T H E   M A S T E R S":PRINT
   20 INPUT"=ENTER= THE NUMBER OF PLAYERS";PL
   22 IFPL>4GOTO10
   25 PRINT:FORI=1TOPL
   30 PRINT"NAME OF PLAYER # ";I;:INPUTP$(I)
   35 NEXT
  100 FOR R=1 TO 9:CLS:GOSUB900:GOSUB955:P(R)=P
  110 FOR RR=1TOPL:S=0
  112 GOSUB 926:GOSUB 960
  115 M=1:N=RND(7)+21:SET(M,N):SET(M+1,N)
  120 GOSUB800
  125 GOSUB630
  130 PRINT@256,"            ";
  132 A(RR,R)=S
  134 FORJ=1TO3
  135 PRINT@PT,P$(RR);"  IS  ON  THE  GREEN !";
  136 FORI=1TO300:NEXT:PRINT@PT,CHR$(30):FORI=1TO200:NEXT:NEXT
  140 NEXTRR
  150 FOR RR=1TOPL:M=M(RR):S=0
  160 GOSUB700:A(RR,R)=A(RR,R)+S
  165 FORI=1TO999:NEXT
  170 NEXTRR
  180 GOSUB500
  189 NEXTR
  199 GOTO199
  500 CLS:FORX=0TO256+PL*128STEP128:PRINT@X,STRING$(63,140);:NEXT
  505 FORI=0TOR:FORJ=0TOPL+1:PRINT@I*4+J*128+86,CHR$(170);
  506 NEXT:NEXT
  510 FORX=1TO9:PRINT@X*4+19+64,X;:NEXT
  515 PRINT@80,"HOLE";:PRINT@208,"PAR";:PRINT@68,"T H E";
  520 PRINT@128,"M A S T E R S   ";:PRINT@187," TOT";
  525 FORI=1TOPL:PRINT@I*128+197,P$(I);:NEXT
  535 FORI=1TOR:PRINT@I*4+211,P(I);
  540 FORJ=1TOPL:PRINT@128*J+211+I*4,A(J,I);
  545 NEXT:NEXT
  550 X=0:FORI=1TOR:X=P(I)+X:NEXT:PRINT@251,X;
  555 FORI=1TOPL
  560 X=0:FORJ=1TOR:X=X+A(I,J):NEXT:PRINT@I*128+251,X;:NEXT
  585 IF R<>9 THEN 595
  590 PRINT@960,"ANOTHER ROUND";
  591 INPUTA$
  593 IF LEFT$(A$,1)="Y"THEN RUN10ELSE9000
  595 PRINT@960,"";:INPUT"PRESS =ENTER= TO CONTINUE ";A$
  599 RETURN
  600 GOSUB630
  605 PRINT@PT,"* OUT OF BOUNDS PENALTY *  STROKE AND DISTANCE!";
  610 M=M1:N=N1:FORJ=1TO1000:NEXTJ:S=S+1
  615 SET(M,N):SET(M+1,N):GOSUB630
  620 PRINT@960,STRING$(61,192);:RETURN
  630 FORI=PTTOPT+128STEP64:PRINT@I,CHR$(30);:NEXT:RETURN
  700 CLS:ST=(-1)^(RND(2)):T=RND(90)+20:Y=33:IFABS(T-90)<7GOTO700
  701 IF(M<10)*(RND(10)<3)THENM=0
  702 PRINT@960,P$(RR);"'S  PUTT.";
  703 PRINT" ENTER TWO DIGITS OR ONE DIGIT & <ENTER>";
  705 FORX=0TO125STEP3:SET(X,Y):SET(X+1,Y):SET(X+2,Y)
  706 SET(X,Y+1):SET(X+1,Y+1):SET(X+2,Y+1)
  707 IFABS(90-X)<10GOTO715
  710 IF(RND(5)=1)*(X>T)THENY=Y+ST:IFY>44THENY=Y-ST
  712 IF(RND(5)=1)*(X<T)THENY=Y-ST
  715 IFX=90THENH=Y
  718 NEXT:RESET(89,H+1):RESET(90,H+1):RESET(91,H+1)
  719 SET(89,H+2):SET(90,H+2):SET(91,H+2)
  720 PRINT@110,"*******";:PRINT@238,"*******";:PRINT@179," ***";
  721 PRINT@176,R;
  723 X=90-M:FORY=9TO45:IFPOINT(X,Y)THENY=Y-2:GOTO726
  724 NEXT
  726 FORN=2TOH-4:SET(90,N):NEXT:GOTO785
  727 SET(X,Y):SET(X+1,Y)
  729 PRINT@64,"                                 ";
  730 PRINT@0,"ENTER LENGTH OF PUTT ( IN FEET )";
  737 PRINT@64,"?";
  738 S=S+1
  740 A$=INKEY$:IFA$=""THEN740ELSEA=VAL(A$)
  745 B$=INKEY$:IFB$=""THEN745
  746 IF B$=CHR$(13)THEN A=0:B$=A$
  747 B=VAL(B$)
  749 PX=A*10+B
  750 PRINT@64,"PUTT ATTEMPT IS ";PX;" FEET.  ";
  760 A=-1:Q=1:IFX>90THENQ=-1
  765 A=A+1:IF A>=PXTHEN 785
  770 RESET(X,Y):RESET(X+1,Y):X=X+Q
  772 IFX>121THENX=117+RND(3):GOTO780
  773 IFX<1THENX=2+RND(2):GOTO780
  774 IFPOINT(X,Y+1)THENY=Y-1:A=A+RND(2)+2:GOTO780
  776 IFPOINT(X,Y+2)=0THENY=Y+1:A=A-RND(2)-2
  780 SET(X,Y):SET(X+1,Y):GOTO765
  785 IF(X<>89)*(X<>90)GOTO727
  790 RESET(89,Y):RESET(90,Y):RESET(91,Y):RESET(X,Y):RESET(X+1,Y)
  792 SET(X,Y+3):SET(X+1,Y+3)
  799 RETURN
  800 IFSG>0THENPT=0ELSEPT=768
  801 IFSG>0THENPP=832ELSEPP=0
  802 PRINT@PP,"HOLE # ";R;
  803 PRINT@PP+64,"PAR ";P;:PRINT@PP+128,"YARDS ";Y;
  804 PRINT@PT,P$(RR);"'S ";
  805 PRINT"CLUB CHOICE :  W=WOOD  I=IRON  S=SAND WEDGE";
  807 S=S+1
  810 C$=INKEY$:IFC$=""THEN810
  812 PRINT@PT,CHR$(30)
  815 IFC$<>"W"THEN820ELSEPRINT@PT,"ENTER WOOD # (1-4) ?";
  816 D$=INKEY$:IFD$=""THEN816ELSEPRINTD$;
  817 W=VAL(D$)
  819 IFW>4THEN812ELSEY1=180+10*(5-W)+RND((6-W)*10):GOTO830
  820 IFC$<>"I"THEN825ELSEPRINT@PT,"ENTER IRON # (1-9) ?";
  821 D$=INKEY$:IFD$=""THEN821ELSEPRINTD$;
  822 W=VAL(D$)
  824 IFW>9THEN812ELSEY1=80+10*(10-W)+RND(18+2*(10-W)):GOTO830
  825 IFC$<>"S"THEN805ELSEY1=30+RND(20)
  830 PRINT@PT,CHR$(30)
  832 IFM=1THENJ=5:GOTO855
  835 PRINT@PT+4,"1 2 3 4";:PRINT@PT+132,"9 8 7 6";
  840 PRINT@PT+64,"DIR   ";CHR$(140);"   5";
  845 PRINT@PT+15,"ENTER DIRECTION  ?";
  850 B$=INKEY$:IFB$=""THEN850ELSEPRINTB$;
  852 J=VAL(B$)
  855 SS=RND(3)-2:RESET(M,N):RESET(M+1,N)
  856 GOSUB890:T=INT(Y1/W)
  857 M1=M:N1=N
  860 FORI=1TOT:RESET(M,N):RESET(M+1,N):GOSUB890
  863 IF(N<1)+(N>46)+(M<1)+(M>126)GOSUB600:GOTO804
  865 IFRND(9)=1THENN=N+SS:IFJ<>5THENN=N-SS
  870 SET(M,N):SET(M+1,N):NEXT
  875 G1=G-64:G2=(INT((N+3)/3)-1)*64+INT(M/2)
  877 FORJ=G1TOG1+128STEP64:FORI=JTOJ+7:IFI<>G2GOTO885
  880 M2=(G+4-INT((G+3)/64)*64)*2:N2=INT((G+3)/64)*3+1
  882 M=ABS(M-M2)*10+ABS(N-N2)*10-RND(9):IFM>89THENM=80+RND(9)
  883 M(RR)=M:RETURN
  885 NEXT:NEXT
  886 I=PT:FORK=1TO3:PRINT@I,CHR$(30);:I=I+64:NEXT
  889 GOTO804
  890 ONJGOTO891,892,893,894,895,896,897,898,899
  891 M=M-2:N=N-1:W=18:RETURN
  892 N=N-1:W=10:RETURN
  893 M=M+2:N=N-1:W=18:RETURN
  894 M=M+4:N=N-1:W=20:RETURN
  895 M=M+2:W=10:RETURN
  896 M=M+4:N=N+1:W=20:RETURN
  897 M=M+2:N=N+1:W=18:RETURN
  898 N=N+1:W=10:RETURN
  899 M=M-2:N=N+1:W=18:RETURN
  900 PRINT@384,"TEE";:PRINT@640,"TEE";
  912 SG=(-1)^(RND(2)):Y=RND(18)
  914 IFY>15THENX2=SG*(4+RND(2)):Y=RND(68)+500:P=5:GOTO922
  916 IF(Y>3)*(Y<16)THENY=RND(180)+300:P=4:GOTO920
  918 Y=RND(70)+150:P=3
  920 X2=RND(9)-5:IFY<300THENX2=0
  921 IFY>420THENX2=SG*(3+RND(2))
  922 Y1=Y-ABS(X2)*18:X1=INT(Y1/8)-8:IFX1>60THENX1=60
  924 X1=INT((X1-ABS(X2)*4)/4)-1
  926 C=0
  930 FORI=520TO572STEP4:C=C+1
  932 IFC>X1+1GOTO940
  934 PRINT@I+128,"F";:PRINT@I-128,"F";
  939 GOTO950
  940 PC=I+SG*(C-X1)*64
  941 IFSG>0ANDPC>256PRINT@PC-256,"F";
  942 IFSG>0ANDPC+128<1020PRINT@PC+128,"F";
  943 IFSG>0GOTO949
  945 IFPC+256<1020PRINT@PC+256,"F";
  946 IFPC>128PRINT@PC-128,"F";
  949 IFC>X1+ABS(X2)GOTO953
  950 NEXTI
  953 RETURN
  955 G=PC+64*(RND(3)-2)
  957 IFG<64THENG=G+64:GOTO957
  958 IFG+64>1020THENG=G-64:GOTO958
  960 PRINT@G,"G   +   G";
  961 PRINT@G-61,"G  G";
  962 PRINT@G+67,"G  G";
  965 RETURN
 1000 PRINTTAB(10),"T H E   M A S T E R S":PRINT
 1010 PRINT"     TO PLAY, SIMPLY ENTER THE CLUB TYPE (WOOD OR IRON OR SAND"
 1020 PRINT"WEDGE) AND THE CLUB NUMBER.  ON ALL FAIRWAY SHOTS, YOU ALSO"
 1030 PRINT"HAVE THE OPTION OF DIRECTION (1-9) AS WILL BE INDICATED BY"
 1040 PRINT"A CHART LIKE THE ONE BELOW.
 1050 PRINT@448,"1 2 3 4":PRINT@514,CHR$(140);"   5"
 1060 PRINT@576,"9 8 7 6"
 1070 PRINT@468,"SO IF YOU WISH TO HIT UP AND TO THE RIGHT,";
 1080 PRINT@532,"YOU SHOULD ENTER A 3 OR A 4 FOR DIRECTION.";
 1090 PRINT@704,"     FROM ONE TO FOUR MAY PLAY EACH ROUND.  FIRST, EACH"
 1100 PRINT"PLAYER WILL SHOOT UNTIL HE REACHES THE GREEN.  WHEN ALL PLAYERS"
 1105 PRINT"HAVE REACHED THE GREEN, THE PUTTING SURFACE WILL BE DISPLAYED."
 1110 PRINT:INPUT"PRESS =ENTER= TO CONTINUE ";A$:CLS
 1112 PRINTTAB(10),"T H E   M A S T E R S":PRINT
 1115 PRINT"     EACH PLAYER WILL TAKE HIS TURN PUTTING UNTIL HE HAS MADE"
 1120 PRINT"THE PUTT.  SINCE TWO PLAYERS SELDOM PUTT FROM THE SAME SPOT,"
 1125 PRINT"EACH PLAYER WILL HAVE A DIFFERENT PUTTING SURFACE TO CONTEND"
 1130 PRINT"WITH.  AFTER ALL PLAYERS HAVE HOLED OUT, A CURRENT SCORE CARD"
 1135 PRINT"WILL BE DISPLAYED -- THEN IT IS ON TO THE NEXT HOLE."
 1140 PRINT:PRINT"     SINCE THERE ARE ADVANTAGES TO HITTING THIRD OR FOURTH"
 1145 PRINT"WHEN MORE THAN ONE GOLFERS ARE PARTICIPATING, THE MORE EXPER-"
 1150 PRINT"IENCED SHOOTERS SHOULD ENTER THEIR NAMES AS PLAYER #1, "
 1160 PRINT"PLAYER #2, AND SO ON.  THIS WAY, THE BETTER PLAYERS WILL HAVE"
 1165 PRINT"TO HIT FIRST, AND THE OTHERS CAN LEARN FROM THEIR SHOTS."
 1190 PRINT
 1199 INPUT"PRESS  =ENTER= TO BEGIN ";A$:RETURN
 7000 CLS:PRINT@281,"THE  MASTERS"
 7010 PRINT@409,"COPYRIGHT BY"
 7020 PRINT@470,"CREATIVE COMPUTING"
 7030 PRINT@536,"P.O. BOX 789-M"
 7040 PRINT@593,"MORRISTOWN, NEW JERSEY 07960"
 7050 FORI=1TO2000:NEXTI:RETURN
 9000 CLS:INPUT "DO YOU WANT TO PLAY AGAIN";A$
 9010 IF LEFT$(A$,1)="Y" THEN 2
 9020 PRINT : PRINT : PRINT "THANKS FOR PLAYING!"
 9030 END

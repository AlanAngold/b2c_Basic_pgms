1000	PRINT "              THE GAME OF ICBM"
1010	PRINT
1020	PRINT"     THE OBJECT OF THE GAME IS TO SHOOT DOWN THE INCOMING"
1030	PRINT"ICBM WITH YOUR ABM (ANTI-BALISTIC MISSILE) BEFORE IT"
1040	PRINT"IMPACTS ON YOUR CONTROL HEADQUARTERS(AT 0,0).  THE"
1050	PRINT"THE STANDARD X,Y COORDINATE SYSTEM CORRESPONDS TO "
1060	PRINT"YOUR EAST/NORTH SYSTEM.  SO IF YOU HAVE A HEADING OF"
1070	PRINT"ZERO DEGREES YOUR GOING EAST, AND 90 TAKES YOU NORTH."
1080	PRINT"TYPE 'QUIT' TO END THE GAME"
1090	PRINT
1100	PRINT"    GOOD LUCK  (REMEMBER THE WORLD IS COUNTING ON -YOU-)"
1110	RANDOMIZE
1120	LET X2=0
1130	LET Y2=0
1140	LET W = INT (RND*10 +1)
1150	LET X=(INT(RND*800)+200)*SGN(RND(0)-0.5)
1160	IF X=0 THEN 1150
1170	LET Y=(INT(RND*800)+200)*SGN(RND(0)-0.5)
1180	IF Y=0 THEN 1170
1190	LET S=INT(RND*20+50)
1200	LET S1=INT(RND*20+50)
1210	LET D2=1000
1220	PRINT
1230	PRINT
1240	D=INT(SQR((X2-X)^2+(Y2-Y)^2))
1250	PRINT "------MISSLE------ SEPARATION --------SAM-------"
1260	PRINT "MILES        MILES            MILES        MILES       NEW  "
1270	PRINT "NORTH        EAST             NORTH        EAST      HEADING"
1280	PRINT "------------------------------------------------------------"
1290	      :####         ####    #####    ####         ####       
1300	FOR N=1 TO 50
1310	    PRINT USING 1290,Y,X,D,Y2,X2 ;
1320	    IF D2<=W THEN 1660
1330	    GOSUB 1800
1340	    LET T1=T1/57.296
1350	    LET H=INT(RND*200+1)
1360	    IF H>4 THEN 1380
1370	    ON H GOTO 1580,1600,1620,1640
1380	    LET T=ATN(Y/X)
1390	    LET A=S*COS(T)/100
1400	    LET B=S*SIN(T)/100
1410	    LET A1=S1*COS(T1)/100
1420	    LET B1=S1*SIN(T1)/100
1430	    FOR J9=1 TO 100
1440		X2=X2+A1
1450		Y2=Y2+B1
1460		X =X -A
1470		Y =Y -B
1480		D=INT(SQR((X2-X)^2+(Y2-Y)^2))
1490		IF D<=W THEN 1550
1500	    D2=INT(SQR(X^2+Y^2))
1510	    IF D2>W THEN 1530
1520	    GOTO 1540
1530	    NEXT J9
1540	NEXT N
1550	PRINT "CONGRATULATIONS!  YOUR SAM CAME WITHIN";D;"MILES OF"
1560	PRINT "THE ICBM AND DESTROYED IT."
1570	GOTO 1680
1580	PRINT "TOO BAD.  YOUR SAM FELL TO THE GROUND"
1590	GOTO 1680
1600	PRINT "YOUR SAM EXPLODED IN MIDAIR"
1610	GOTO 1680
1620	PRINT "GOOD LUCK-THE ICBM EXPLODED HARMLESSLY IN MID-AIR"
1630	GOTO1680
1640	PRINT "GOOD LUCK-THE ICBM TURNED OUT TO BE A FRIENDLY AIRCRAFT"
1650	GOTO 1680
1660	PRINT
1670	PRINT "TOO BAD!  THE ICBM JUST HIT YOUR LOCATION"
1680	PRINT "DO YOU WANT TO PLAY MORE (Y OR N)";
1690	INPUT A$
1700	IF A$="Y" THEN 1150
1710	STOP
1720	REM
1730	REM 		THIS SUBROUTINE IS USED TO LET THE USER INPUT NUMBERS
1740	REM		TO THE PROGRAM IT IS BEING RUN IN WITHOUT THE WORRY
1750	REM 		OF THE ERROR STATEMENT OF THIS HOSY BASIC THAT IS
1760	REM		OUTPUT WHEN THE USER TRIES TO INPUT A CHARACTER TO
1770	REM		A NUMERIC INPUT FIELD.
1780	REM
1790	PRINT" PLEASE INPUT THAT NUMBER AGAIN ";
1800	INPUT A7$
1810	DIM A(15)
1820	LET A(1)=A(2)=A(3)=A(4)=A(5)=T1=0
1830	CHANGE A7$ TO A
1840	FOR I = 1 TO 15
1850	    IF A(I) = 0 THEN 1920
1860	    IF A(I) =113 THEN 1960
1870	    IF A(I) = 81 THEN 1960
1880	    IF A(I)-48 < 0 THEN 1790
1890	    IF A(I)-48 > 9 THEN 1790
1900	NEXT I
1910	GO TO 1790
1920	FOR J = 1 TO I-1
1930	    T1=T1+(A(I-J)-48)*10^(J-1)
1940	NEXT J
1950	RETURN
1960	STOP
1970	REM
1980	REM		THIS IS THE END OF THE NUMERIC INPUT SUBROUTINE
1990	REM		NOTE: A "Q" OR "q" TO THE INPUT WILL STOP THE PROGRAM
2000	REM
2010	END

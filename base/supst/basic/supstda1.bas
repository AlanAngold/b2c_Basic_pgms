    1 REM NOTE: Basic language type forced to be:
    2 REM       trs2 - TRS-80 Level II BASIC tokenized file
    1 '"     ********************************************         "
    2 '"     * EDITED & TYPED FROM THE BOOK             *         "
    3 '"     * <BASIC COMPUTER GAMES> ED. BY DAVID AHL  *         "
    4 '"     ********************************************         "
    5 '"     * 72716,356 >>> ***Arvendui*** >>> 6/84    *         "
    6 '"     ********************************************         "
    7 '"     *      Super      Star       Trek          *         "
    8 '"     *     IMPROVED GRAPHIC ENTERPRISE   7/84   *         "
    9 '"     ********************************************         "
   10 CLEAR50
   20 GOTO50
   30 PRINT@960,"ENTER to continue . . .";
   40 X$=INKEY$:IFX$=""THEN40:ELSERETURN
   50 FORI=1TO12:PRINT:NEXTI
   60 PRINTTAB(10);"*******************************************"
   70 PRINTTAB(10);"*                                         *"
   80 PRINTTAB(10);"*                                         *"
   90 PRINTTAB(10);"*         * * SUPER STAR TREK * *         *"
  100 PRINTTAB(10);"*                                         *"
  110 PRINTTAB(10);"*                                         *"
  120 PRINTTAB(10);"*******************************************"
  130 FORI=1TO8:PRINT:NEXTI
  140 FORI=1TO1000:NEXTI
  150 PRINT@960,"DO YOU NEED INSTRUCTIONS (Y/N)?";
  160 X$=INKEY$:IFX$<>"N"ANDX$<>"n"ANDX$<>"Y"ANDX$<>"y"THEN160
  170 IFX$="N"ORX$="n"THEN1270
  180 CLS
  190 PRINT"INSTRUCTIONS FOR 'SUPER STAR TREK'"
  200 PRINT
  210 PRINT"1. When you see /COMMAND?/ printed, ENTER one of the legal"
  220 PRINT"   commands :  NAV, SRS, LRS, PHA, TOR, SHE, DAM, COM or XXX."
  230 PRINT
  240 PRINT"2. If you should type in an illegal command, you'll get"
  250 PRINT"   a short list of the legal commands printed out."
  260 PRINT
  270 PRINT"3. Some commands require you to enter data (for example,"
  280 PRINT"   the 'NAV' command comes back with 'COURSE (1-9) ?')."
  290 PRINT"   If you type in illegal data (like negative numbers) the"
  300 PRINT"   command will be aborted."
  310 GOSUB30
  320 CLS:PRINT"  The galaxy is divided into an 8 x 8 quadrant grid, and each"
  330 PRINT"quadrant is further divided into an 8 x 8 sector grid."
  340 PRINT:PRINT"  You will be assigned a starting point somewhere in the"
  350 PRINT"galaxy to begin a tour of duty as Captain of the starship"
  360 PRINT"<ENTERPRISE>; your mission:  To seek and destroy the fleet of"
  370 PRINT"Klingon warships which are menacing the United Federation of"
  380 PRINT"Planets."
  390 PRINT:PRINT"  You have the following commands available to you as Captain"
  400 PRINT"of the starship <ENTERPRISE>:"
  410 GOSUB30
  420 CLS:PRINT"/NAV/ COMMAND = WARP ENGINE CONTROL"
  430 PRINT:PRINT"Course is a circular numerical";TAB(44)"  4  3  2"
  440 PRINT"vector arrangement as shown.";TAB(44)"   . . ."
  450 PRINT"Integer and real values may be";TAB(44)"    ..."
  460 PRINT"used (thus course 1.5 is halfway";TAB(44)"5 ---*--- 1"
  470 PRINT"between 1 and 2)";TAB(44)"    ..."
  480 PRINTTAB(44)"   . . ."
  490 PRINT"Values may approach 9.0, which";TAB(44)"  6  7  8"
  500 PRINT"itself is equivalent to 1.0."
  510 PRINTTAB(44)"  COURSE"
  520 PRINT"One warp factor is the size of"
  530 PRINT"one quadrant. Therefore, to get from quadrant 6,5 to 5,5,"
  540 PRINT"use course 3, warp factor 1."
  550 GOSUB30
  560 CLS:PRINT"/SRS/ COMMAND = SHORT RANGE SENSOR SCAN"
  570 PRINT:PRINT"Shows you a scan of your current quadrant."
  580 PRINT:PRINT"Symbology on your sensor scan is as follows:"
  590 PRINT"   <*> = The <ENTERPRISE>"
  600 PRINT"   +K+ = Klingon Battle Cruiser"
  610 PRINT"   >!< = Federation Starbase (refeul, repair, re-arm here!)"
  620 PRINT"    *  = Star"
  630 PRINT:PRINT"A condensed 'status report' will also be presented."
  640 GOSUB30
  650 CLS:PRINT"/LRS/ COMMAND = LONG RANGE SENSOR SCAN"
  660 PRINT:PRINT"Shows conditions in space for each quadrant directly adjacent"
  670 PRINT"to (and including) the quadrant that contains the <ENTERPRISE>"
  680 PRINT"(which is in the middle of the scan)."
  690 PRINT:PRINT"The scan is coded in the form: /###/, where the ones digit"
  700 PRINT"is the number of stars, the tens digit is the number of"
  710 PRINT"Starbases, and the hundreds digit is the number of Klingons."
  720 PRINT:PRINT"EXAMPLE: /207/ = 2 Klingons, no Starbases and 7 stars."
  730 GOSUB30
  740 CLS:PRINT"/PHA/ COMMAND = PHASER CONTROL"
  750 PRINT:PRINT"Allows you to destroy the Klingon Battle Cruisers by"
  760 PRINT"zapping them with suitably large bursts of energy to deplete"
  770 PRINT"thier shield power."
  780 PRINT:PRINT"CAUTION: Klingons have phasers too!!!"
  790 GOSUB30
  800 CLS:PRINT"/TOR/ COMMAND = PHOTON TORPEDO CONTROL"
  810 PRINT:PRINT"Torpedo course is the same as used in the /NAV/ COMMAND"
  820 PRINT"(WARP ENGINE CONTROL)."
  830 PRINT:PRINT"If you hit a Klingon Cruiser with a torpedo it will be"
  840 PRINT"instantly destroyed!  If you miss you are then subject to the"
  850 PRINT"Cruiser's phasers.  In either case, you are also subject to the"
  860 PRINT"phaser fire of all other Klingons in the quadrant."
  870 PRINT:PRINT"The LIBRARY COMPUTER (/COM/ COMMAND) has an option to"
  880 PRINT"compute torpedo trajectory for you (Option 2)."
  890 GOSUB30
  900 CLS:PRINT"/SHE/ COMMAND = SHIELD CONTROL"
  910 PRINT:PRINT"Defines the number of energy units to be assigned to the"
  920 PRINT"shields.  The energy is taken from the total ships energy."
  930 PRINT"Note that the status display 'TOTAL ENERGY' includes the shield"
  940 PRINT"energy."
  950 GOSUB30
  960 CLS:PRINT"/DAM/ COMMAND = DAMAGE CONTROL REPORT"
  970 PRINT:PRINT"Gives the state of repair of all devices.  Where a negative"
  980 PRINT"'state of repair' shows that the devise is too damaged to be"
  990 PRINT"operational."
 1000 GOSUB30
 1010 GOTO1030
 1020 CLS:PRINT"/COM/ COMMAND = LIBRARY COMPUTER":PRINT:RETURN
 1030 GOSUB1020:PRINT"Option 0 = CUMULATIVE GALACTIC RECORD"
 1040 PRINT:PRINT"This option shows the computer memory of the results of all"
 1050 PRINT"previous short and long range sensor scans."
 1060 GOSUB30
 1070 GOSUB1020:PRINT"Option 1 = STATUS REPORT"
 1080 PRINT:PRINT"This option shows the number of Klingons, Stardates and"
 1090 PRINT"Starbases remaining in the game."
 1100 GOSUB30
 1110 GOSUB1020:PRINT"Option 2 = PHOTON TORPEDO DATA"
 1120 PRINT:PRINT"Which gives directions and distances from the <ENTERPRISE>"
 1130 PRINT"to all Klingons in your quadrant."
 1140 GOSUB30
 1150 GOSUB1020:PRINT"Option 3 = STARBASE NAVIGATION DATA"
 1160 PRINT:PRINT"This option gives the direction and distance to any Starbase"
 1170 PRINT"within your quadrant."
 1180 GOSUB30
 1190 GOSUB1020:PRINT"Option 4 = DIRECTION/DISTANCE CALCULATOR"
 1200 PRINT:PRINT"This option allows you to enter coordinates for"
 1210 PRINT"direction/distance calculations."
 1220 GOSUB30
 1230 GOSUB1020:PRINT"Option 5 = GALACTIC MAP"
 1240 PRINT:PRINT"This option prints the names of the sixteen major galactic"
 1250 PRINT"regions referred to in the game."
 1260 GOSUB30
 1270 RANDOM:CLEAR1000:GOTO1310
 1280 FORYY=1TO3:VD=15360:VD=VD+RND(1024)-1
 1290 IFPEEK(VD)<>32THENNEXTYY:RETURN
 1300 IFRND(10)=1THENPOKEVD,42:NEXTYY:RETURNELSEPOKEVD,46:NEXTYY:RETURN
 1310 FORZZ=1TO16:PRINT:NEXTZZ
 1320 FORNT=1TO6:GX$(NT)=""
 1330 READGX:FORVA=1TOGX:READNB
 1340 GX$(NT)=GX$(NT)+CHR$(NB)
 1350 PRINT:GOSUB1280
 1360 NEXTVA:READNB(NT):NEXTNTNT
 1370 FORNT=1TO6:PRINTTAB(NB(NT));GX$(NT)
 1380 GOSUB1280:NEXTNT
 1390 FORZZ=1TO4:PRINT:GOSUB1280:NEXTZZ
 1400 FORZZ=1TO5:GOSUB1280:NEXTZZ
 1410 Z$="                         "
 1420 DIMG(8,8),C(9,2),K(3,3),N(3),Z(8,8),D(8)
 1430 T=INT(RND(0)*20+20)*100:T0=T:T9=25+INT(RND(0)*10):D0=0:E=3000:E0=E
 1440 P=10:P0=P:S9=200:S=0:B9=0:K9=0:X$="":X0$=" is "
 1450 DEFFND(D)=SQR((K(I,1)-S1)^2+(K(I,2)-S2)^2)
 1460 DEFFNR(R)=INT(RND(R)*7.98+1.01)
 1470 Q1=FNR(0):Q2=FNR(0):S1=FNR(0):S2=FNR(0)
 1480 FORI=1TO9:C(I,1)=0:C(I,2)=0:NEXTI
 1490 C(3,1)=-1:C(2,1)=-1:C(4,1)=-1:C(4,2)=-1:C(5,2)=-1:C(6,2)=-1
 1500 C(1,2)=1:C(2,2)=1:C(6,1)=1:C(7,1)=1:C(8,1)=1:C(8,2)=1:C(9,2)=1
 1510 FORI=1TO8:D(I)=0:NEXTI
 1520 A1$="NAVSRSLRSPHATORSHEDAMCOMXXX"
 1530 FORI=1TO8:FORJ=1TO8:K3=0:Z(I,J)=0:R1=RND(0)
 1540 IFR1>.98THENK3=3:K9=K9+3:GOTO1570
 1550 IFR1>.95THENK3=2:K9=K9+2:GOTO1570
 1560 IFR1>.80THENK3=1:K9=K9+1
 1570 B3=0:IFRND(0)>.96THENB3=1:B9=B9+1
 1580 G(I,J)=K3*100+B3*10+FNR(0):NEXTJ,I:IFK9>T9THENT9=K9+1
 1590 IFB9<>0THEN1620
 1600 IFG(Q1,Q2)<200THENG(Q1,Q2)=G(Q1,Q2)+100:K9=K9+1
 1610 B9=1:G(Q1,Q2)=G(Q1,Q2)+10:Q1=FNR(0):Q2=FNR(0)
 1620 K7=K9:IFB9<>1THENX$="s":X0$=" are "
 1630 PRINT"Your orders are as follows:"
 1640 PRINT"     Destroy the";K9;"Klingon warships which have invaded"
 1650 PRINT"   the galaxy, before they can attack Federation Headquarters"
 1660 PRINT"   on Stardate";STR$(T0+T9);"; this gives you";T9;"days.  There";X0$
 1670 PRINT"  ";B9;"Starbase";X$;" in the galaxy for resupplying your ship."
 1680 PRINT:PRINT"Hit any key when ready to accept command"
 1690 ZZ$=INKEY$:IFZZ$=""THEN1690
 1700 Z4=Q1:Z5=Q2:K3=0:B3=0:S3=0:G5=0:D4=.5*RND(0):Z(Q1,Q2)=G(Q1,Q2)
 1710 IFQ1<1ORQ1>8ORQ2<1ORQ2>8THEN1810
 1720 GOSUB4790:PRINT:IFT0<>TTHEN1750
 1730 PRINT"Your mission begins with your starship located"
 1740 PRINT"in the galactic quadrant, '";G2$;"'.":GOTO1760
 1750 PRINT"Now entering ";G2$;" quadrant . . ."
 1760 PRINT:K3=INT(G(Q1,Q2)*.01):B3=INT(G(Q1,Q2)*.1)-10*K3
 1770 S3=G(Q1,Q2)-100*K3-10*B3:IFK3=0THEN1800
 1780 PRINT"COMBAT AREA      CONDITION RED":IFS>200THEN1800
 1790 PRINT"   SHIELDS DANGEROUSLY LOW"
 1800 FORI=1TO3:K(I,1)=0:K(I,2)=0:NEXTI
 1810 FORI=1TO3:K(I,3)=0:NEXTI:Q$=Z$+Z$+Z$+Z$+Z$+Z$+Z$+LEFT$(Z$,17)
 1820 A$="<*>":Z1=S1:Z2=S2:GOSUB4620:IFK3<1THEN1850
 1830 FORI=1TOK3:GOSUB4600:A$="+K+":Z1=R1:Z2=R2
 1840 GOSUB4620:K(I,1)=R1:K(I,2)=R2:K(I,3)=S9*(.5+RND(0)):NEXTI
 1850 IFB3<1THEN1870
 1860 GOSUB4600:A$=">!<":Z1=R1:B4=R1:Z2=R2:B5=R2:GOSUB4620
 1870 FORI=1TOS3:GOSUB4600:A$=" * ":Z1=R1:Z2=R2:GOSUB4620:NEXTI
 1880 GOSUB3770
 1890 IFS+E>10THENIFE>10ORD(7)=0THEN1940
 1900 PRINT:PRINT"** FATAL ERROR **":PRINT"YOU HAVE JUST STRANDED YOUR SHIP IN SPACE."
 1910 PRINT"YOU HAVE INSUFFICIENT MANUEVERING ENERGY AND SHIELD CONTROL"
 1920 PRINT"IS PRESENTLY INCAPABLE OF CROSS-CIRCUITING TO ENGINE ROOM!!"
 1930 GOTO3640
 1940 INPUT"COMMAND";A$
 1950 FORI=1TO9:IFLEFT$(A$,3)<>MID$(A1$,3*I-2,3)THEN1970
 1960 ONIGOTO2070,1880,2720,2800,2990,3310,3380,3990,3670
 1970 NEXTI:PRINT"<ENTER> one of the following:"
 1980 PRINT"   NAV . . .To set course"
 1990 PRINT"   SRS . . .For short range sensor scan"
 2000 PRINT"   LRS . . .For long range sensor scan"
 2010 PRINT"   PHA . . .To fire phasers"
 2020 PRINT"   TOR . . .To fire photon torpedoes"
 2030 PRINT"   SHE . . .To raise or lower shields"
 2040 PRINT"   DAM . . .For damage control reports"
 2050 PRINT"   COM . . .To call on library-computer"
 2060 PRINT"   XXX . . .To resign your command":PRINT:GOTO1890
 2070 INPUT"COURSE (0-9)";C1:IFC1=9THENC1=1
 2080 IFC1>=1ANDC1<9THEN2100
 2090 PRINT"   LT. SULU REPORTS: 'Incorrect course data, Sir!'"
 2100 X$="8":IFD(1)<0THENX$="0.2"
 2110 PRINT"WARP FACTOR (0-";X$;")";:INPUTW1:IFD(1)<0ANDW1>.2THEN2160
 2120 IFW1>0ANDW1<=8THEN2170
 2130 IFW1=0THEN1890
 2140 PRINT"   CHIEF ENGINEER SCOTT REPORTS: 'The engines just won't take"
 2150 PRINT"      warp";STR$(W1);", Sir!'":GOTO1890
 2160 PRINT"WARP ENGINES ARE DAMAGED.  MAXIMUM SPEED = WARP 0.2":GOTO1890
 2170 N=INT(W1*8+.5):IFE-N>=0THEN2240
 2180 PRINT"   CHIEF ENGINEER SCOTT REPORTS: 'Sir, we just don't have"
 2190 PRINT"      enough energy to manuever at warp";STR$(W1);"!'"
 2200 IFS<N-EORD(7)<0THEN1890
 2210 PRINT"   DEFLECTOR CONTROL ROOM ACKNOWLEGES";S;"UNITS OF ENERGY"
 2220 PRINT"      PRESENTLY DEPLOYED TO SHIELDS."
 2230 GOTO1890
 2240 FORI=1TOK3:IFK(I,3)=0THEN2270
 2250 A$="   ":Z1=K(I,1):Z2=K(I,2):GOSUB4620:GOSUB4600
 2260 K(I,1)=Z1:K(I,2)=Z2:A$="+K+":GOSUB4620
 2270 NEXTI:GOSUB3530:D1=0:D6=W1:IFW1>=1THEND6=1
 2280 FORI=1TO8:IFD(I)>=0THEN2330
 2290 D(I)=D(I)+D6:IFD(I)>-.1ANDD(I)<0THEND(I)=-.1:GOTO2330
 2300 IFD(I)<0THEN2330
 2310 IFD1<>1THEND1=1:PRINT"DAMAGE CONTROL REPORT:"
 2320 PRINTTAB(3);:R1=I:GOSUB4670:PRINTG2$;" REPAIR COMPLETED."
 2330 NEXTI:IFRND(0)>.2THEN2390
 2340 R1=FNR(0):IFRND(0)>=.6THEN2370
 2350 D(R1)=D(R1)-(RND(0)*5+1):PRINT"DAMAGE CONTROL REPORT:"
 2360 GOSUB4670:PRINTTAB(3);G2$;" DAMAGED.":PRINT:GOTO2390
 2370 D(R1)=D(R1)+RND(0)*3+1:PRINT"DAMAGE CONTROL REPORT:"
 2380 GOSUB4670:PRINTTAB(3);G2$;" STATE OF REPAIR IMPROVED.":PRINT
 2390 A$="   ":Z1=INT(S1):Z2=INT(S2):GOSUB4620
 2400 X1=C(C1,1)+(C(C1+1,1)-C(C1,1))*(C1-INT(C1)):X=S1:Y=S2
 2410 X2=C(C1,2)+(C(C1+1,2)-C(C1,2))*(C1-INT(C1)):Q4=Q1:Q5=Q2
 2420 FORI=1TON:S1=S1+X1:S2=S2+X2:IFS1<1ORS1>=9ORS2<1ORS2>=9THEN2510
 2430 S8=INT(S1)*24+INT(S2)*3-26:IFMID$(Q$,S8,2)="  "THEN2460
 2440 S1=INT(S1-X1):S2=INT(S2-X2):PRINT"WARP ENGINES SHUT DOWN AT SECTOR";
 2450 PRINTSTR$(S1);",";RIGHT$(STR$(S2),1);" DUE TO BAD NAVIGATION.":GOTO2470
 2460 NEXTI:S1=INT(S1):S2=INT(S2)
 2470 A$="<*>":Z1=INT(S1):Z2=INT(S2):GOSUB4620:GOSUB2680:T8=1
 2480 IFW1<1THENT8=.1*INT(10*W1)
 2490 T=T+T8:IFT>T0+T9THEN3640
 2500 GOTO1880
 2510 X=8*Q1+X+N*X1:Y=8*Q2+Y+N*X2:Q1=INT(X/8):Q2=INT(Y/8):S1=INT(X-Q1*8)
 2520 S2=INT(Y-Q2*8):IFS1=0THENQ1=Q1-1:S1=8
 2530 IFS2=0THENQ2=Q2-1:S2=8
 2540 X5=0:IFQ1<1THENX5=1:Q1=1:S1=1
 2550 IFQ1>8THENX5=1:Q1=8:S1=8
 2560 IFQ2<1THENX5=1:Q2=1:S2=1
 2570 IFQ2>8THENX5=1:Q2=8:S2=8
 2580 IFX5=0THEN2660
 2590 PRINT"   LT. UHURA REPORTS: 'Sir! We have just recieved a message"
 2600 PRINT"      from Starfleet Command: Permission to attempt crossing of"
 2610 PRINT"      Galactic Perimeter is hereby *DENIED*!'"
 2620 PRINT"      SHUT DOWN YOUR ENGINES!":FORZZ=1TO500:NEXTZZ
 2630 PRINT"   CHIEF ENGINEER SCOTT REPORTS: 'Warp engines shut down at"
 2640 PRINT"      sector";STR$(S1);",";RIGHT$(STR$(S2),1);" of quadrant";STR$(Q1);",";RIGHT$(STR$(Q2),1);".'"
 2650 IFT>T0+T9THEN3640
 2660 IF8*Q1+Q2=8*Q4+Q5THEN2470
 2670 T=T+1:GOSUB2680:GOTO1700
 2680 E=E-N-10:IFE>=0THENRETURN
 2690 PRINT"SHIELD CONTROL SUPPLIES THE ENERGY TO COMPLETE THE MANUEVER."
 2700 S=S+E:E=0:IFS<=0THENS=0
 2710 RETURN
 2720 IFD(3)<0THENPRINT"LONG RANGE SENSORS ARE INOPERABLE.":GOTO1890
 2730 PRINT"LONG RANGE SCAN FOR QUADRANT";STR$(Q1);",";RIGHT$(STR$(Q2),1);"."
 2740 O1$="-------------------":PRINTO1$
 2750 FORI=Q1-1TOQ1+1:N(1)=-1:N(2)=-2:N(3)=-3:FORJ=Q2-1TOQ2+1
 2760 IFI>0ANDI<9ANDJ>0ANDJ<9THENN(J-Q2+2)=G(I,J):Z(I,J)=G(I,J)
 2770 NEXTJ:FORL=1TO3:PRINT": ";:IFN(L)<0THENPRINT"*** ";:GOTO2790
 2780 PRINTRIGHT$(STR$(N(L)+1000),3);" ";
 2790 NEXTL:PRINT":":PRINTO1$:NEXTI:GOTO1890
 2800 IFD(4)<0THENPRINT"PHASERS ARE INOPERABLE.":GOTO1890
 2810 IFK3>0THEN2840
 2820 PRINT"   SCIENCE OFFICER SPOCK REPORTS: 'Captain, sensors indicate"
 2830 PRINT"      that there are no Klingon vessels in this quadrant.'":GOTO1890
 2840 IFD(8)<0THENPRINT"COMPUTER FAILURE HAMPERS ACCURACY."
 2850 PRINT"PHASERS LOCKED ON TARGET."
 2860 PRINT"ENERGY AVAILABLE =";E;"UNITS."
 2870 INPUT"NUMBER OF UNITS TO FIRE";X:IFX<=0THEN1890
 2880 IFE-X<0THEN2860
 2890 E=E-X:IFD(7)<0THENX=X*RND(0)
 2900 H1=INT(X/K3):FORI=1TO3:IFK(I,3)<=0THEN2980
 2910 H=INT((H1/FND(0))*(RND(0)+2)):IFH>.15*K(I,3)THEN2930
 2920 PRINT"SENSORS INDICATE NO DAMAGE TO KLINGON VESSEL AT SECTOR";STR$(K(I,1));",";RIGHT$(STR$(K(I,2)),1);".":GOTO2980
 2930 K(I,3)=K(I,3)-H:PRINTRIGHT$(STR$(H),LEN(STR$(H))-1);" UNIT HIT ON KLINGON VESSEL AT SECTOR";STR$(K(I,1));",";RIGHT$(STR$(K(I,2)),1);"."
 2940 IFK(I,3)<=0THENPRINT"***KLINGON VESSEL DESTROYED***":GOTO2960
 2950 PRINT"SENSORS INDICATE THE KLINGON VESSEL HAS";K(I,3):PRINT"   UNITS OF ENERGY REMAINING.":GOTO2980
 2960 K3=K3-1:K9=K9-1:Z1=K(I,1):Z2=K(I,2):A$="   ":GOSUB4620
 2970 K(I,3)=0:G(Q1,Q2)=G(Q1,Q2)-100:Z(Q1,Q2)=G(Q1,Q2):IFK9<=0THEN3740
 2980 NEXTI:GOSUB3530:GOTO1890
 2990 IFP<=0THENPRINT"PHOTON TORPEDOES EXPENDED.":GOTO1890
 3000 IFD(5)<0THENPRINT"PHOTON TUBES ARE NOT OPERATIONAL.":GOTO1890
 3010 INPUT"PHOTON TORPEDOE COURSE (1-9)";C1:IFC1=9THENC1=1
 3020 IFC1>=1ANDC1<9THEN3050
 3030 PRINT"   ENSIGN CHEKOV REPORTS: 'Incorrect course data, Sir!'"
 3040 GOTO1890
 3050 X1=C(C1,1)+(C(C1+1,1)-C(C1,1))*(C1-INT(C1)):E=E-2:P=P-1
 3060 X2=C(C1,2)+(C(C1+1,2)-C(C1,2))*(C1-INT(C1)):X=S1:Y=S2
 3070 PRINT"TORPEDOE TRACK:"
 3080 X=X+X1:Y=Y+X2:X3=INT(X+.5):Y3=INT(Y+.5)
 3090 IFX3<1ORX3>8ORY3<1ORY3>8THEN3300
 3100 PRINT"               ";STR$(X3);",";RIGHT$(STR$(Y3),1):A$="   ":Z1=X:Z2=Y:GOSUB4760
 3110 IFZ3<>0THEN3080
 3120 A$="+K+":Z1=X:Z2=Y:GOSUB4760:IFZ3=0THEN3170
 3130 PRINT"***KLINGON VESSEL DESTROYED***":K3=K3-1:K9=K9-1:IFK9<=0THEN3740
 3140 FORI=1TO3:IFX3=K(I,1)ANDY3=K(I,2)THEN3160
 3150 NEXTI:I=3
 3160 K(I,3)=0:GOTO3280
 3170 A$=" * ":Z1=X:Z2=Y:GOSUB4760:IFZ3=0THEN3190
 3180 PRINT"STAR AT SECTOR";STR$(X3);",";RIGHT$(STR$(Y3),1);" ABSORBED TORPEDOE ENERGY.":GOSUB3530:GOTO1890
 3190 A$=">!<":Z1=X:Z2=Y:GOSUB4760:IFZ3=0THEN3010
 3200 PRINT"***FEDERATION STARBASE DESTROYED***":B3=B3-1:B9=B9-1
 3210 IFB9>0ORK9>T-T0-T9THEN3260
 3220 PRINT"   LT. UHURA REPORTS: 'Sir, I just recieved a message from"
 3230 PRINT"      Starfleet.  You are hereby relieved of command and"
 3240 PRINT"      sentenced to 99 stardates hard labor on Cygnus 12.":PRINT"      I,m sorry Captain.'"
 3250 GOTO3670
 3260 PRINT"   LT. UHURA REPORTS: 'Captain, Starfleet has just informed me"
 3270 PRINT"      that they are reviewing your record to consider court":PRINT"      martial!'":D0=0
 3280 Z1=X:Z2=Y:A$="   ":GOSUB4620
 3290 G(Q1,Q2)=K3*100+B3*10+S3:Z(Q1,Q2)=G(Q1,Q2):GOSUB3530:GOTO1890
 3300 PRINT"TORPEDOE MISSED TARGET VESSEL.":GOSUB3530:GOTO1890
 3310 IFD(7)<0THENPRINT"SHIELD CONTROL INOPERABLE.":GOTO1890
 3320 PRINT"ENERGY AVAILABLE =";STR$(E+S);" UNITS.":INPUT"NUMBER OF UNITS TO SHIELDS";X
 3330 IFX<0ORS=XTHENPRINT"<SHIELDS UNCHANGED>":GOTO1890
 3340 IFX<=E+STHEN3370
 3350 PRINT"   SHIELD CONTROL ROOM REPORTS INCORRECT ENERGY DEPLOYMENT DATA.";
 3360 PRINT"<SHIELDS UNCHANGED>":GOTO1890
 3370 E=E+S-X:S=X:PRINT"   DEFLECTOR CONTROL ROOM REPORTS SHIELDS NOW AT";INT(S);"UNITS":PRINT"      OF ENERGY, BY YOUR COMMAND.":GOTO1890
 3380 IFD(6)>=0THEN3490
 3390 PRINT"DAMAGE CONTROL REPORT NOT AVAILABLE.":IFD0=0THEN1890
 3400 D3=0:FORI=1TO8:IFD(I)<0THEND3=D3+.1
 3410 NEXTI:IFD3=0THEN1890
 3420 PRINT:D3=D3+D4:IFD3>=1THEND3=.9
 3430 PRINT"TECHNITIANS STANDING BY TO EFFECT REPAIRS TO YOUR SHIP."
 3440 PRINT"ESTIMATED TIME OF REPAIR";.01*INT(100*D3);"STARDATES."
 3450 INPUT"WILL YOU AUTHORIZE THE REPAIR (Y/N)";A$
 3460 IFA$<>"Y"ANDA$<>"y"THEN1890
 3470 FORI=1TO8:IFD(I)<0THEND(I)=0
 3480 NEXTI:T=T+D3+.1
 3490 PRINT:PRINT"DEVICE             STATE OF REPAIR":FORR1=1TO8
 3500 GOSUB4670:PRINTG2$;LEFT$(Z$,25-LEN(G2$));INT(D(R1)*100)*.01
 3510 NEXTR1:PRINT:IFD0<>0THEN3400
 3520 GOTO1890
 3530 IFK3<=0THENRETURN
 3540 IFD0<>0THENPRINT"STARBASE SHIELDS PROTECT THE ENTERPRISE.":RETURN
 3550 FORI=1TO3:IFK(I,3)<=0THEN3630
 3560 ZZ=RND(0):H=INT((K(I,3)/FND(1))*(2+ZZ)):S=S-H:K(I,3)=K(I,3)/(3+ZZ)
 3570 PRINTRIGHT$(STR$(H),LEN(STR$(H))-1);" UNIT HIT ON ENTERPRISE FROM SECTOR";STR$(K(I,1));",";RIGHT$(STR$(K(I,2)),1);"."
 3580 IFS<=0THEN3650
 3590 PRINT"<SHIELDS DOWN TO";S;"UNITS>":IFH<20THEN3630
 3600 IFRND(0)>.6ORH/S<=.02THEN3630
 3610 R1=FNR(0):D(R1)=D(R1)-H/S-.5*RND(0):GOSUB4670
 3620 PRINT"   DAMAGE CONTROL REPORTS ";G2$;" DAMAGED":PRINT"      BY THE HIT."
 3630 NEXTI:RETURN
 3640 PRINT"It is Stardate";STR$(T);".":GOTO3670
 3650 PRINT:PRINT"The Enterprise has been destroyed."
 3660 PRINT"The Federation will be conquered":GOTO3640
 3670 ZZ$=" was":YZ$=" ":IFK9>1THENZZ$=" were":YZ$="s "
 3680 PRINT"There";ZZ$;K9;"Klingon battle cruiser";YZ$;"left at the end":PRINT"   of your mission."
 3690 PRINT:PRINT:IFB9=0THEN3730
 3700 PRINT"The Federation is in need of a new Starship Commander"
 3710 PRINT"   for a similar mission.  If there is a volunteer,"
 3720 INPUT"   let him/her step forward and <ENTER> 'AYE'";A$:IFA$="AYE"THEN1270
 3730 END
 3740 PRINT"Congratulations Captain! the last Klingon battle cruiser"
 3750 PRINT"   menacing the Federation has been destroyed!":PRINT
 3760 PRINT"YOUR EFFICIENCY RATING IS";STR$(1000*(K7/(T-T0))^2);".":GOTO3690
 3770 FORI=S1-1TOS1+1:FORJ=S2-1TOS2+1
 3780 IFINT(I+.5)<1ORINT(I+.5)>8ORINT(J+.5)<1ORINT(J+.5)>8THEN3800
 3790 A$=">!<":Z1=I:Z2=J:GOSUB4760:IFZ3=1THEN3810
 3800 NEXTJ,I:D0=0:GOTO3830
 3810 D0=1:C$="DOCKED":E=E0:P=P0
 3820 PRINT"SHIELDS DISABLED FOR DOCKING PURPOSES.":S=0:GOTO3850
 3830 IFK3>0THENC$="* RED *":GOTO3850
 3840 C$="GREEN":IFE<E0*.1THENC$="YELLOW"
 3850 IFD(2)>=0THEN3870
 3860 PRINT:PRINT"SHORT RANGE SENSORS ARE INOPERABLE.":PRINT:RETURN
 3870 O1$=STRING$(33,"-"):PRINTO1$:FORI=1TO8
 3880 FORJ=(I-1)*24+1TO(I-1)*24+22STEP3:PRINT" ";MID$(Q$,J,3);:NEXTJ
 3890 ONIGOTO3900,3910,3920,3930,3940,3950,3960,3970
 3900 PRINT"     STARDATE          ";INT(T*10)*.1:GOTO3980
 3910 PRINT"     CONDITION          ";C$:GOTO3980
 3920 PRINT"     QUADRANT          ";STR$(Q1);",";RIGHT$(STR$(Q2),1):GOTO3980
 3930 PRINT"     SECTOR            ";STR$(S1);",";RIGHT$(STR$(S2),1):GOTO3980
 3940 PRINT"     PHOTON TORPEDOES  ";INT(P):GOTO3980
 3950 PRINT"     TOTAL ENERGY      ";INT(E+S):GOTO3980
 3960 PRINT"     SHIELDS           ";INT(S):GOTO3980
 3970 PRINT"     KLINGONS          ";INT(K9)
 3980 NEXTI:PRINTO1$:RETURN
 3990 IFD(8)<0THENPRINT"LIBRARY-COMPUTER DISABLED.":GOTO1890
 4000 INPUT"COMPUTER ACTIVE AND AWAITING COMMAND";A:IFA<0THEN1890
 4010 PRINT:H8=1:ONA+1GOTO4100,4220,4300,4570,4360,4090
 4020 PRINT"FUNCTIONS AVAILABLE FROM LIBRARY-COMPUTER:"
 4030 PRINT"   0 = CUMULATIVE GALACTIC RECORD"
 4040 PRINT"   1 = STATUS REPORT"
 4050 PRINT"   2 = PHOTON TORPEDOE DATA"
 4060 PRINT"   3 = STARBASE NAV. DATA"
 4070 PRINT"   4 = DIRECTION/DISTANCE CALCULATOR"
 4080 PRINT"   5 = GALAXY 'REGION NAME' MAP":PRINT:GOTO4000
 4090 H8=0:G5=1:PRINT"                          THE GALAXY":GOTO4120
 4100 PRINT:PRINT"CUMULATIVE GALACTIC RECORD FOR QUADRANT";STR$(Q1);",";RIGHT$(STR$(Q2),1);"."
 4110 PRINT
 4120 PRINT"        1     2     3     4     5     6     7     8"
 4130 O1$="      ----- ----- ----- ----- ----- ----- ----- -----":PRINTO1$
 4140 FORI=1TO8:PRINTI;:IFH8=0THEN4190
 4150 FORJ=1TO8:IFJ=1THENPRINT" ";
 4160 PRINT"   ";:IFZ(I,J)=0THENPRINT"***";:GOTO4180
 4170 PRINTRIGHT$(STR$(Z(I,J)+1000),3);
 4180 NEXTJ:GOTO4210
 4190 Z4=I:Z5=1:GOSUB4790:J0=INT(15-.5*LEN(G2$)):PRINTTAB(J0);G2$;
 4200 Z5=5:GOSUB4790:J0=INT(39-.5*LEN(G2$)):PRINTTAB(J0);G2$;
 4210 PRINT:NEXTI:PRINT:GOTO1890
 4220 PRINT"   STATUS REPORT:":X$="":IFK9>1THENX$="S"
 4230 PRINT"      KLINGON";X$;" REMAINING:";INT(K9)
 4240 PRINT"      MISSION MUST BE COMPLETED IN";.1*INT((T0+T9-T)*10);"STARDATES."
 4250 X$="S":IFB9<2THENX$="":IFB9<1THEN4280
 4260 PRINT"      THE FEDERATION IS MAINTAINING";INT(B9);"STARBASE";X$;" IN THE GALAXY."
 4270 GOTO3380
 4280 PRINT"      NO FEDERATION STARBASES REMAINING!"
 4290 GOTO3380
 4300 IFK3<=0THEN2820
 4310 X$="":IFK3>1THENX$="S"
 4320 PRINT"FROM ENTERPRISE TO KLINGON BATTLE CRUISER";X$;"."
 4330 H8=0:FORI=1TO3:IFK(I,3)<=0THEN4560
 4340 W1=K(I,1):X=K(I,2)
 4350 QW=1:C1=S1:A=S2:GOTO4400
 4360 PRINT"DIRECTION/DISTANCE CALCULATOR:"
 4370 PRINT"   YOU ARE AT QUADRANT";STR$(Q1);",";RIGHT$(STR$(Q2),1);" SECTOR";STR$(S1);",";RIGHT$(STR$(S2),1);"."
 4380 PRINT"PLEASE <ENTER>:":INPUT"   INITIAL COORDINATES (X,Y)";C1,A
 4390 INPUT"   DESTINATION COORDINATES (X,Y)";W1,X
 4400 X=X-A:A=C1-W1:IFX<0THEN4480
 4410 IFA<0THEN4500
 4420 IFX>0THEN4440
 4430 IFA=0THENC1=5:GOTO4450
 4440 C1=1
 4450 IFABS(A)<=ABS(X)THEN4470
 4460 PRINT"   DIRECTION =";C1+(((ABS(A)-ABS(X))+ABS(A))/ABS(A)):GOTO4540
 4470 PRINT"   DIRECTION =";C1+(ABS(A)/ABS(X)):GOTO4540
 4480 IFA>0THENC1=3:GOTO4510
 4490 IFX<>0THENC1=5:GOTO4450
 4500 C1=7
 4510 IFABS(A)>=ABS(X)THEN4530
 4520 PRINT"   DIRECTION =";C1+(((ABS(X)-ABS(A))+ABS(X))/ABS(X)):GOTO4540
 4530 PRINT"   DIRECTION =";C1+(ABS(X)/ABS(A))
 4540 PRINT"   DISTANCE =";:IFQW=1THENPRINTSQR(X^2+A^2)/10:QW=0ELSEPRINTSQR(X^2+A^2)
 4550 IFH8=1THEN1890
 4560 NEXTI:GOTO1890
 4570 IFB3<>0THENPRINT"FROM ENTERPRISE TO STARBASE:":W1=B4:X=B5:QW=1:GOTO4350
 4580 PRINT"   SCIENCE OFFICER SPOCK REPORTS: 'Captain, sensors indicate"
 4590 PRINT"      that there are no Starbases in this quadrant.'":GOTO1890
 4600 R1=FNR(0):R2=FNR(0):A$="   ":Z1=R1:Z2=R2:GOSUB4760:IFZ3=0THEN4600
 4610 RETURN
 4620 S8=INT(Z2-.5)*3+INT(Z1-.5)*24+1
 4630 IFLEN(A$)<>3THENPRINT"ERROR":STOP
 4640 IFS8=1THENQ$=A$+RIGHT$(Q$,189):RETURN
 4650 IFS8=190THENQ$=LEFT$(Q$,189)+A$:RETURN
 4660 Q$=LEFT$(Q$,S8-1)+A$+RIGHT$(Q$,190-S8):RETURN
 4670 ONR1GOTO4680,4690,4700,4710,4720,4730,4740,4750
 4680 G2$="WARP ENGINES":RETURN
 4690 G2$="SHORT RANGE SENSORS":RETURN
 4700 G2$="LONG RANGE SENSORS":RETURN
 4710 G2$="PHASER CONTROL":RETURN
 4720 G2$="PHOTON TUBES":RETURN
 4730 G2$="DAMAGE CONTROL":RETURN
 4740 G2$="SHIELD CONTROL":RETURN
 4750 G2$="LIBRARY-COMPUTER":RETURN
 4760 Z1=INT(Z1+.5):Z2=INT(Z2+.5):S8=(Z2-1)*3+(Z1-1)*24+1:Z3=0
 4770 IFMID$(Q$,S8,3)<>A$THENRETURN
 4780 Z3=1:RETURN
 4790 IFZ5<=4THENONZ4GOTO4810,4820,4830,4840,4850,4860,4870,4880
 4800 GOTO4890
 4810 G2$="ANTARES":GOTO4980
 4820 G2$="RIGEL":GOTO4980
 4830 G2$="PROCYON":GOTO4980
 4840 G2$="VEGA":GOTO4980
 4850 G2$="CANOPUS":GOTO4980
 4860 G2$="ALTAIR":GOTO4980
 4870 G2$="SAGITTARIUS":GOTO4980
 4880 G2$="POLLUX":GOTO4980
 4890 ONZ4GOTO4900,4910,4920,4930,4940,4950,4960,4970
 4900 G2$="SIRIUS":GOTO4980
 4910 G2$="DENEB":GOTO4980
 4920 G2$="CAPELLA":GOTO4980
 4930 G2$="BETELGEUSE":GOTO4980
 4940 G2$="ALDEBARAN":GOTO4980
 4950 G2$="REGULUS":GOTO4980
 4960 G2$="ARCTURUS":GOTO4980
 4970 G2$="SPICA"
 4980 IFG5<>1THENONZ5GOTO5000,5010,5020,5030,5000,5010,5020,5030
 4990 RETURN
 5000 G2$=G2$+" I":RETURN
 5010 G2$=G2$+" II":RETURN
 5020 G2$=G2$+" III":RETURN
 5030 G2$=G2$+" IV":RETURN
 5040 DATA19,160,176,176,176,176,176,176,176,176,176,176,176,176,176,176,176,176,176,144,6
 5050 DATA19,130,136,172,188,188,188,188,188,188,188,188,188,188,188,188,188,188,188,148,7
 5060 DATA35,130,164,139,180,128,128,128,128,128,128,128,128,128,128,128,128,128,128,160,176,176,188,188,188,188,188,190,188,188,188,188,188,180,176,144,19
 5070 DATA34,137,146,173,144,128,128,128,128,128,128,128,128,128,128,128,131,131,139,143,143,143,143,143,191,191,143,143,143,143,143,143,131,131,131,21
 5080 DATA29,160,176,176,176,176,178,180,187,180,176,176,176,176,176,176,176,176,176,176,176,176,176,176,176,176,176,190,159,129,17
 5090 DATA26,130,131,131,139,143,143,143,143,143,143,143,143,143,143,143,143,143,143,143,143,143,143,143,143,143,135,18

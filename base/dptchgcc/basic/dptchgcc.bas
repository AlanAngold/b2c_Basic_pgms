    1 REM NOTE: Basic language type forced to be:
    2 REM       trs2 - TRS-80 Level II BASIC tokenized file
   10 CLS:PRINT@281,"DEPTH CHARGE":PRINT@409,"COPYRIGHT BY"
   20 PRINT@470,"CREATIVE COMPUTING":PRINT@536,"P.O. BOX 789-M"
   30 PRINT@593,"MORRISTOWN, NEW JERSEY 07960"
   40 FOR I=1TO2000:NEXT I:CLS
   90 INPUT "DO YOU NEED INSTRUCTIONS (Y OR N)";A$
  100 IF A$="N" GOTO 210
  110 CLS : PRINT "WELCOME TO DEPTH CHARGE"
  140 PRINT "   YOU ARE THE COMANDER OF A DISTROYER YOUR MISSION---"
  150 PRINT "TO SINK AS MANY SUBS AS POSSIBLE IN THE TIME YOU HAVE"
  160 PRINT "TO MOVE YOUR SHIP LEFT PUSH (,) TO MOVE RIGHT PUSH (.)"
  170 PRINT "TO STOP PUSH ANY OTHER BUTTON"
  180 PRINT "YOU MAY FIRE A CHARGE OFF OF ETHER SIDE OF YOUR SHIP"
  190 PRINT "OFF THE LEFT PUSH Z OFF THE RIGHT PUSH X" : PRINT "YOU CAN ONLY HAVE 4 CHARGES OUT AT A TIME" : PRINT
  200 INPUT "PUSH ENTER TO START";A$
  205 DIM P(3),A(4),C(4)
  210 CLS : T=120 : PRINT@ 0,"TIME REMAINING";T; : A(1)=0 : A(2)=0 : A(3)=0 : A(4)=0 : B=0 : C(1)=0 : C(2)=0 : C(3)=0 : C(4)=0
  215 P(0)=25 : P(1)=25 : P(2)=25 : P(3)=25
  220 POKE 15450,174 : POKE 15457,157 : M1=15450
  230 FOR A=15451 TO 15456 : POKE A,188 : NEXT
  240 FORA=15488TO15551:POKEA,131:NEXT
  250 A$=INKEY$ : IF A$<>"" THEN M$=A$
  260 IF M$="," THEN 500
  270 IF M$="." THEN 540
  280 S=0 : GOSUB 600
  290 S=1 : GOSUB 670
  300 FOR A=1 TO 2 : S=2 : GOSUB 600 : NEXT
  310 FOR A=1 TO 2 : S=3 : GOSUB 670 : NEXT
  320 IF A$="Z" GOTO 750
  330 IF A$="X" GOTO 790
  340 FOR A=1 TO 4
  350 IF C(A)<>0 GOTO 830
  360 NEXT
  370 FOR S=0 TO 3 : FOR A=1 TO 4
  380 IF A(A)>P(S)-3 AND A(A)<P(S)+3 GOTO 880
  390 NEXT A,S
  400 T=T-1
  410 PRINT@ 14,T;
  420 IF T=0 GOTO 930
  490 GOTO 240
  500 IF M1=15425 GOTO 280
  510 POKE M1-1,174 : POKE M1,188 : POKE M1+6,157 : POKE M1+7,32
  520 M1=M1-1 : GOTO 280
  540 IF M1=15479 GOTO 280
  550 POKE M1,32 : POKE M1+1,174 : POKE M1+7,188 : POKE M1+8,157
  560 M1=M1+1 : GOTO 280
  600 IF P(S)=25 THEN P(S)=RND(12)*64+15552 : D(S)=P(S)
  610 P(S)=P(S)+1
  620 IF P(S)<D(S)+64 THEN POKE P(S),141 : POKE P(S)-1,140
  630 IF P(S)-2>D(S) THEN POKE P(S)-3,32
  640 IF P(S)=D(S)+66 THEN P(S)=25
  650 RETURN
  670 IF P(S)=25 THEN P(S)=RND(12)*64+15615 : D(S)=P(S)
  680 P(S)=P(S)-1
  690 IF P(S)>D(S)-64 THEN POKE P(S),142 : POKE P(S)+1,140
  700 IF P(S)+2<D(S) THEN POKE P(S)+3,32
  710 IF P(S)=D(S)-66 THEN P(S)=25
  720 RETURN
  750 FOR A=1 TO 4
  760 IF C(A)=0 THEN C(A)=2 : A(A)=M1-1 : GOTO 340
  770 NEXT : GOTO 340
  790 FOR A=1 TO 4
  800 IF C(A)=0 THEN C(A)=2 : A(A)=M1+8 : GOTO 340
  810 NEXT : GOTO 340
  830 ON C(A) GOTO 840,850,860
  840 POKE A(A),129:POKE A(A)-64,32:C(A)=C(A)+1:GOTO 360
  850 POKE A(A),136 : C(A)=C(A)+1 : GOTO 360
  860 POKE A(A),144 : IF A(A)>16319 THEN C(A)=0 : POKE A(A),32 : GOTO 360
  870 C(A)=1 : A(A)=A(A)+64 : GOTO 360
  880 IF S>2 THEN B=B+10
  890 B=B+10
  900 PRINT@ 51, "SCORE ";B;
  910 IF P(S)-15363>0 AND P(S)-15363<16383 THEN PRINT@ P(S)-15363, "******"; : FOR C=1 TO 50 : NEXT : PRINT@ P(S)-15363,"      "; : POKE A(A),32 : POKE A(A)-64,32
  920 P(S)=25 : C(A)=0 : GOTO 390
  930 CLS : PRINT@ 411,"GAME OVER"
  940 PRINT : PRINT : PRINT"YOUR SCORE",,B
  950 IF B>HS THEN HS=B
  960 PRINT "HIGH SCORE THIS RUN",HS
  970 PRINT : INPUT "ANOTHER GAME (Y OR N)";A$
  980 IF LEFT$(A$,1)="Y" THEN 210
  990 PRINT "SO LONG THEN COMMANDER"
  995 FOR I=1TO2000:NEXTI
 1000 CLS:PRINT "THANKS FOR PLAYING!":PRINT:PRINT
 1005 PRINT "TO PLAY AGAIN, TYPE 'RUN'.":PRINT:PRINT
 1010 PRINT "TO LOAD ANOTHER PROGRAM, TYPE 'CLOAD'.":END

    1 REM NOTE: Basic language type forced to be:
    2 REM       trs2 - TRS-80 Level II BASIC tokenized file
   10 ' TAKEN FROM CREATIVE COMPUTING  -  101 COMPUTER GAMES
   20 ' CONVERTED FOR TRS-80 BY:R.FULLWOOD
   30 PRINT "THE COMPUTER WILL EXPLAIN THE GAME AS YOU PLAY -"
   40 PRINT "ENJOY YOUR GAME - SEE YOU AT THE 19TH HOLE  . . ."
   50 DIM L(10):G1=18:G2=0:G3=0:A=0:N=.8:S2=0:F=1
   60 PRINT@576,;:INPUT "WHAT IS YOUR HANDICAP - - -";H
   70   IF H>30 OR H<0 THEN 60   
   80 CLS:PRINT"DIFFICULTIES AT GOLF INCLUDE:":PRINTTAB(15)"0 = HOOK":PRINTTAB(15)"1 = SLICE":PRINTTAB(15)"2 = POOR DISTANCE":PRINTTAB(15)"3 = TRAP SHOTS":PRINTTAB(15)"4 = PUTTING":PRINT"WHICH ONE AND ONLY ONE IS YOUR WORST":INPUTT
   90 CLS:IF T>5 THEN 80   ELSE S1=0
  100 L(0) = 0:J=0:Q=0:S2=S2+1:K=0:IF F=1 THEN 240  
  110  PRINT@576,"YOUR SCORE ON HOLE";F-1;"WAS";S1;:GOSUB1470 :PRINTCHR$(31)
  120   GOTO 1350 
  130  IF S1>P+2 THEN 140  ELSE IF S1=P THEN 160  ELSE IF S1=P-1 THEN 180  ELSE IF S1=P-2 THEN 200  ELSE GOTO 240  
  140  PRINT@512,"KEEP YOUR HEAD DOWN ***";:GOSUB1460 :PRINTCHR$(31)
  150   GOTO 240  
  160  PRINT@512, "A PAR":GOSUB1460 
  170   GOTO 240  
  180  PRINT@512,"A BIRDIE.":GOSUB1460 
  190  GOTO 240  
  200   IF P =3 THEN 230  
  210  PRINT @512," YIPEE, A GREAT BIG EAGLE":GOSUB1470 
  220  GOTO 240  
  230  PRINT@512," Y A H O O O O, THE MAN GETS A HOLE IN ONE TIE....":GOSUB1470 
  240  IF F =19 THEN 1340 ELSE S1=0
  250 PRINT
  260  IF S1 =0 THEN 1200 ELSE IF L(0)<1 THEN 910  ELSE X=0
  270  IF L(0)>5 THEN 930  
  280 PRINT@512,;:PRINTCHR$(31): PRINT @512,"SHOT WENT  ";D1;"  YARDS. IT'S  ";D2;"  YARDS FROM THE CUP."
  290 O=(RND(0)/.8)*(2*H+16)*ABS(TAN(D1*.0035)):PRINT "BALL IS";INT(O);"YARDS OFF LINE IN ...";
  300  GOSUB 320  
  310  GOTO 400  
  320  IF L(X)=1 THEN 340  ELSE IF L(X)=2 THEN 350  ELSE IF L(X)=3 THEN 360  ELSE IF L(X)=4 THEN 370  ELSE IF L(X)=5 THEN 380  ELSE IF L(X)=6 THEN 390  
  330  PRINT "OUT OF BOUNDS":GOTO1280 
  340  PRINT"FAIRWAY":GOTO1280 
  350  PRINT"ROUGH":GOTO1280 
  360  PRINT "TREES":GOTO1280 
  370  PRINT "ADJACENT FAIRWAY":GOTO1280 
  380  PRINT "TRAP":GOTO1280 
  390  PRINT "WATER":GOTO1280 
  400  IF A=1 THEN 470  
  410  PRINT@10, "SELECTION OF CLUBS":PRINT"YARDAGE DESIRED";TAB(20);"SUGGESTED CLUBS"
  420  PRINT"200 TO 280 YARDS";TAB(25);"1 TO 4":PRINT"100 TO 200 YARDS";TAB(24);"19 TO 13"
  430  PRINT"  0 TO 100 YARDS";TAB(24);"29 TO 23":GOSUB 1490 
  440  A = 1
  450 PRINT@320,;:FOR KY=0TO 127:SET(KY,16):NEXTKY
  460 FOR Y=1 TO 16:SET(76,Y):NEXTY:PRINT@448,;
  470 PRINT@704,;:INPUT"WHAT CLUB DO YOU CHOOSE";C:PRINT@512,;:PRINTCHR$(31)
  480  IF C < 1 THEN 510  ELSE IF C>29 THEN 510  ELSE IF C>4 THEN 520  
  490  IF L(0)<= 5 THEN 540  ELSE IF C=14 THEN 540  ELSE IF C=23 THEN 540  ELSE GOTO 510  
  500  S = S1-1
  510  PRINT@512, "THAT CLUB IS NOT IN THE BAG - DUMMY! ! !":PRINTCHR$(31):GOTO 400  
  520  IFC<12 THEN 510  ELSE C=C-6
  530  GOTO 490  
  540  S1 = S1 +1:W=1
  550  IF C > 13 THEN 730  ELSE IF INT(F/3)=F/3 THEN 710  
  560  IF C<4 THEN 570  ELSE GOTO 580  
  570  IF L(0) =2 THEN 630  
  580  IF S1>7 THEN 650  
  590 D1 =INT(((30-H)*2.5+187-((30-H)*.25+15)*C/2)+25*RND(1))
  600  D1 =INT(D1*W):IF T=2 THEN 920  
  610 O=(RND(0)/.8)*(2*H+16)*ABS(TAN(DI*.0035))
  620  D2=INT(SQR(O^2+ABS(D-D1)^2)):IF D-D1<0 THEN 660  ELSE GOTO 680  
  630  PRINT"YOU TOPPED IT.":GOSUB1460 
  640   D1 =35:GOTO 610  
  650   IF D <200 THEN 1000 ELSE GOTO 590  
  660  IF D2<20 THEN 680  
  670  PRINT@512, "TOO MUCH CLUB. YOU ARE PAST THE HOLE.";:GOSUB1460 :PRINTCHR$(31)
  680  B=D:D=D2
  690  IFD2>27 THEN 800  ELSE IF D2>20 THEN 890  ELSE IF D2>.5 THEN 900  ELSE L(0)=9
  700  GOTO 1130 
  710  IF S2+Q+(10*(F-1)/18)<(F-1)*(72+((H+1)/.85))/18 THEN 720  ELSE GOTO 560  
  720  Q=Q+1:IF S1/2<>INT(S1/2) THEN 780  ELSE GOTO 630  
  730  PRINT@768,"YOU MAY NOW GAUGE YOUR DISTANCE BY PERCENT (1 TO 100)"
  740  PRINT"PERCENT FULL SWING";
  750  INPUT W : W=W/100
  760  IF W >1 THEN 500  ELSE IF L(0)=5 THEN 990  ELSE IF C=14 THEN 580  ELSE C=C-10
  770  GOTO 580  
  780  IF D<95 THEN 630  
  790  PRINT"BALL HIT TREE -BOUNCED INTO ROUGH";D-75;"YARDS FROM HOLE":D=D-75:GOTO 400  
  800  IF O<30 THEN 910  ELSE IFJ>0 THEN 910  ELSE IF T>0 THEN 850  ELSE S9=(S2+1)/15
  810 IF INT(S9)=S9 THEN 860  
  820 PRINT"YOU HOOKED- ";
  830 L(0)=L(2)
  840 IFO>45 THEN 880  ELSE GOTO 260  
  850 S9=(S2+1)/15:IF INT(S9)=S9 THEN 820  
  860 PRINT"YOU SLICED--";
  870 L(0)=L(1):GOTO 840  
  880 PRINT "BADLY.":GOTO 260  
  890 L(0)=5:GOTO 260  
  900 L(0)=8:D2=INT(D2*3):GOTO 1050 
  910 L(0)=1:GOTO 260  
  920 D1=INT(.85*D1):GOTO 610  
  930 IF L(0)>6THEN 980  
  940 PRINT"YOUR SHOT WENT INTO WATER."
  950 S1=S1+1
  960 PRINT"PENALTY STROKE ASSSESSED - HIT FROM PREVIOUS LOCATION"
  970 J=J+1:L(0)=1:D=B:GOTO 400  
  980 PRINT"YOUR SHOT WENT OUT OF BOUNDS.":GOTO 950  
  990 IFT=3 THEN 1010 
 1000 D2=1+(3*INT((80/(40-H))*RND(1))):GOTO 1050 
 1010 IF RND(1)>N THEN 1040 
 1020 N=N*.2
 1030 PRINT"SHOT FLUFFED, STILL IN TRAP.":GGOTO 400  
 1040 N=.8:GOTO 1000 
 1050 PRINT@512,"ON GREEN";D2;"FEET FROM THE PIN";:PRINTCHR$(31)
 1060 PRINT" CHOOSE YOUR PUTT DISTANCE POTENCY NUMBBER 1 - 13"
 1070 INPUT "PUTT POTENCY NUMBER";I
 1080 S1=S1+1
 1090 IF S1+1-P>(H*.072)+2 THEN 1130 ELSE IF K<2 THEN 1130 ELSE K=K+1
 1100 IF T=3 THEN 1170 
 1110 D2=D2-I*(4+2*RND(1))+1.5
 1120 IFD2<-2 THEN 1180 ELSE IF D2>2 THEN 1150 
 1130 PRINT@512, "YOU HOLED IT.";:PRINTCHR$(31)
 1140 F=F+1:GOTO 100  
 1150 PRINT@704,"PUTT SHORT.":GOSUB1460 
 1160 D2=INT(D2):GOTO 1050 
 1170 D2=D2-I*(4+1*RND(1))+1:GOTO 1120 
 1180 PRINT@704,"PASSED BY CUP.":GOSUB1460 
 1190 D2 = -D2:GOTO 1160 
 1200 READ D,P,L(1),L(2)
 1210 PRINT
 1220 PRINT@512,"YOU ARE AT TEE OF HOLE  ";F;"  DISTANCE  ";D;"  YARDS, PAR  ";P:GOSUB1490 
 1230 G3=G3+P:PRINT"ON YOUR RIGHT IS ";
 1240 X=1:GOSUB 320  
 1250 PRINT"ON YOUR LEFT IS ";
 1260 X=2:GOSUB 320  
 1270 GOTO 400  
 1280 RETURN
 1290 DATA361,4,4,2,389,4,3,3,206,3,4,2,500,5,7,2
 1300 DATA 408,4,2,4,359,4,6,4,424,4,4,2,388,4,4,4
 1310 DATA 196,3,7,2,400,4,7,2,560,5,7,2,132,3,2,2
 1320 DATA 357,4,4,4,294,4,2,4,475,5,2,3,375,4,4,2
 1330 DATA 180,3,6,2,550,5,6,6
 1340 PRINT
 1350 G2=G2+S1
 1360 PRINT@384,;:PRINTCHR$(31):PRINT@384,"TOTAL PAR FOR  ";F-1;"  HOLES IS  ";G3; "  ****   YOUR TOTAL IS  ";G2
 1370 IF G1=F-1 THEN 1400 
 1380 FOR KY=0TO127:SET(KY,21):NEXTKY
 1390 GOTO 130  
 1400 GOSUB1470 :CLS:PRINTCHR$(23)
 1410 IFG2=72 :PRINT@448,"WOW - THAT WAS A GOOD PAR ROUND EH!!":GOTO1440 
 1420 IFG2>=73PRINT"iT MIGHT BE WISE TO SEE THE CLUB PROFESSIONAL FOR SOME LESSONS":GOTO1440 
 1430 IFG2<=71 PRINT"GREAT STUFF - DID YOU GET LESSONS FROM BILLY DUNK OR WAS IT JACK NICKLAUS":PRINT"YOU SLY OLD DOG"
 1440 INPUT"DO YOU WISH TO PLAY AGAIN       (YES/NO)";A$:CLS:IFA$="YES"GOTO 30
 1450 END
 1460 FOR X=1TO 2000:NEXT:RETURN
 1470 FOR X=1TO 3000:NEXT:RETURN
 1480 END
 1490 PRINT@109,;:PRINTCHR$(30);:PRINT"TEE NO   ";F;
 1500 PRINT@173,;:PRINTCHR$(30);:PRINT"DISTANCE ";D;
 1510 PRINT@237,;:PRINTCHR$(30);:PRINT"PAR      ";P;
 1520 PRINT@576,;
 1530 RETURN

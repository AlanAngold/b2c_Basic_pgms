    1 REM NOTE: Basic language type forced to be:
    2 REM       trs2 - TRS-80 Level II BASIC tokenized file
   10 CLS
   20 PRINT@285,"LEM"
   30 PRINT@ 409,"cOPYRIGHT BY"
   40 PRINT@ 534,"CREATIVE COMPUTING"
   50 PRINT@ 657,"MORRISTOWN, NEW JERSEY 07960"
   60 FOR D=1 TO 1500:NEXT
   70 CLS
  110 Z$="GO"
  120 B1=1
  130 M=17.95
  140 F1=5.25
  150 N=7.5
  160 R0=926
  170 V0=1.29
  180 T=0
  190 H0=60
  200 R=R0+H0
  210 A=-3.425
  220 R1=0
  230 A1=8.84361E-04
  240 R3=0
  250 A3=0
  260 M1=7.45
  270 M0=M1
  280 B=750
  290 T1=0
  300 F=0
  310 P=0
  320 N=1
  330 M2=0
  340 S=0
  350 C=0
  360 IF Z$="YES" THEN 2510
  370 PRINT
  380 PRINT"LUNAR LANDING SIMULATION"
  390 PRINT
  400 PRINT"HAVE YOU FLOWN AN APOLLO/LEM MISSION BEFORE";
  410 PRINT" (YES OR NO)";
  420 INPUT Q$
  430 IF Q$="YES" THEN 470
  440 IF Q$="NO" THEN 500
  450 PRINT"JUST ANSWER THE QUESTION PLEASE, ";
  460 GOTO 410
  470 PRINT
  480 PRINT"INPUT MEASUREMENT OPTION NUMBER";
  490 GOTO 540
  500 PRINT
  510 PRINT"WHICH SYSTEM OF MEASUREMENT DO YOU PREFER?"
  520 PRINT" 1=METRIC   0=ENGLISH "
  530 PRINT"ENTER THE APPROPRIATE NUMBER";
  540 INPUT K
  550 CLS
  560 PRINT
  570 IF K=0 THEN 660
  580 IF K=1 THEN 600
  590 GOTO 530
  600 Z=1852.8
  610 M$="METERS"
  620 G3=3.6
  630 N$="KILOMETERS"
  640 G5=1000
  650 GOTO 710
  660 Z=6080
  670 M$="FEET"
  680 G3=.592
  690 N$="N.MILES"
  700 G5=Z
  710 IF B1=3 THEN 1540
  720 IF Q$="YES" THEN 1130
  730 PRINT
  740 PRINT"YOU ARE ON A LUNAR LANDING MISSION. AS THE PILOT OF"
  750 PRINT"THE LUNAR EXCURSION MODULE, YOU WILL BE EXPECTED TO"
  760 PRINT"GIVE CERTAIN COMMANDS TO THE MODULE NAVIGATION SYSTEM."
  770 PRINT"THE ON-BOARD COMPUTER WILL GIVE A RUNNING ACCOUNT"
  780 PRINT"OF THE INFORMATION NEEDED TO NAVIGATE THE SHIP."
  790 GOSUB 2660
  800 PRINT
  810 PRINT
  820 PRINT"THE ATTITUDE ANGLE CALLED FOR IS DESCRIBED AS FOLLOWS."
  830 PRINT"+ OR -180 DEGREES IS DIRECTLY AWAY FROM THE MOON"
  840 PRINT"-90 DEGREEES IS ON A TANGENT IN THE DIRECTION OF ORBIT"
  850 PRINT"+90 DEGREES IS ON A TANGENT FROM THE DIRECTION OF ORBIT"
  860 PRINT"0 (ZERO) DEGREES IS DIRECTLY TOWARD THE MOON"
  870 GOSUB 2660
  880 PRINT:PRINT:PRINTTAB(26);"* COURSE *"
  890 PRINT
  900 PRINT TAB(27);"-180,180"
  910 PRINTTAB(31);"["
  920 PRINTTAB(24);" -90 <-+-> 90"
  930 PRINTTAB(31);"!"
  940 PRINTTAB(31);"0"
  950 GOSUB 2660
  960 PRINTTAB(20);"<< DIRECTION OF ORBIT <<"
  970 PRINT
  980 PRINTTAB(24);"SURFACE OF MOON"
  990 PRINT
 1000 PRINT
 1010 PRINT"     ALL ANGLES BETWEEN -180 AND 180 DEGREES ARE ACCEPTED."
 1020 GOSUB 2660
 1030 PRINT
 1040 PRINT"1 FUEL UNIT =1 SEC. AT MAX THRUST"
 1050 PRINT"ANY DISCREPANCIES ARE ACCOUNTED FOR IN THE USE OF FUEL"
 1060 PRINT"FOR AN ATTITUDE CHANGE"
 1070 PRINT"AVAILABLE ENGINE POWER: 0 (ZERO) AND ANY VALUE BETWEEN"
 1080 PRINT"10 AND 100 PERCENT."
 1090 PRINT
 1100 PRINT"NEGATIVE THRUST OR TIME IS PROHIBITED."
 1110 GOSUB 2660
 1120 PRINT
 1130 PRINT
 1140 PRINT"INPUT: TIME INTERVAL IN SECONDS ------ (T)"
 1150 PRINT"       PERCENTAGE OF THRUST ---------- (P)"
 1160 PRINT"       ATTITUDE ANGLE IN DEGREES ----- (A)"
 1170 PRINT
 1180 IF Q$="YES" THEN 1240
 1190 PRINT"FOR EXAMPLE"
 1200 PRINT"T,P,A? 10,65,-60"
 1210 PRINT"TO ABORT THE MISSION AT ANY TIME, ENTER 0,0,0"
 1220 GOSUB 2660
 1230 PRINT
 1240 PRINT"OUTPUT: TOTAL TIME ELAPSED IN SECONDS"
 1250 PRINT"        HEIGHT IN ";M$
 1260 PRINT"        DISTANCE FROM LANDING SITE IN ";M$
 1270 PRINT"        VERTICAL VELOCITY IN ";M$;"/SECOND"
 1280 PRINT"        HORIZONTAL VELOCITY IN ";M$;"/SECOND"
 1290 PRINT"        FUEL UNITS REMAINING"
 1300 GOSUB 2660
 1310 PRINT
 1320 GOTO 1540
 1330 GOSUB 2700
 1340 PRINT"T,P,A";
 1350 INPUT T1,F,P
 1360 PASS=PASS+1:IF PASS>=4 THEN CLS
 1370 IF PASS>=4 THEN PASS=0
 1380 F=F/100
 1390 IF T1<0 THEN 2010
 1400 IF ABS(F-.05)>1 THEN 2090
 1410 IF ABS(F-.05)>1 THEN 2090
 1420 IF ABS(F-.05)<.05 THEN 2090
 1430 IF ABS(P)>180 THEN 2050
 1440 N=20
 1450 IF T1<400 THEN 1470
 1460 N=T1/20
 1470 T1=T1/N
 1480 P=P*3.14159/180
 1490 S=SIN(P)
 1500 C=COS(P)
 1510 M2=M0*T1*F/B
 1520 R3=-.5*R0*((V0/R)^2)+R*A1*A1
 1530 A3=2*R1*A1/R
 1540 FOR I=1TON
 1550 IF M1=0 THEN 1630
 1560 M1=M1-M2
 1570 IF M1>0 THEN 1650
 1580 F=F*(1+M1/M2)
 1590 M2=M1+M2
 1600 PRINT"YOU ARE OUT OF FUEL."
 1610 M1=0
 1620 GOTO 1650
 1630 F=0
 1640 M2=0
 1650 M=M-.5*M2
 1660 R4=R3
 1670 R3=-.5*R0*((V0/R)^2)+R*A1*A1
 1680 R2=(3*R3-R4)/2+5.26E-03*F1*F*C/M
 1690 A4=A3
 1700 A3=2*R1*A1/R
 1710 A2=(3*A3-A4)/2+5.6E-03*F1*F*S/(M*R)
 1720 X=R1*T1+.5*R2*T1*T1
 1730 R=R+X
 1740 H0=H0+X
 1750 R1=R1+R2*T1
 1760 A=A+A1*T1+.5*A2*T1*T1
 1770 A1=A1+A2*T1
 1780 M=M-.5*M2
 1790 T=T+T1
 1800 IF H0<3.28783E-04 THEN 1820
 1810 NEXT I
 1820 H=H0*Z
 1830 H1=R1*Z
 1840 D=R0*A*Z
 1850 D1=R*A1*Z
 1860 T2=M1*B/M0
 1870 PRINT" ";T;TAB(6);H;TAB(15);D;
 1880 PRINTTAB(28);H1;TAB(35);D1;TAB(45);T2
 1890 IF H0<3.28783E-04 THEN 1960
 1900 IF R0*A>164.474 THEN 2300
 1910 IF M1>0 THEN 1340
 1920 T1=20
 1930 F=0
 1940 P=0
 1950 GOTO 1440
 1960 IF R1<-8.21957E-04 THEN 2240
 1970 IF ABS(R*A1)>4.93174E-04 THEN 2240
 1980 IF H0<-3.28783E-04 THEN 2240
 1990 IF ABS(D)>10*Z THEN 2330
 2000 GOTO 2190
 2010 PRINT
 2020 CLS:PRINT"THIS SPACECRAFT IS NOT ABLE TO VIOLATE THE SPACE-";
 2030 PRINT"TIME CONTINUUM."
 2040 GOTO 1330
 2050 PRINT
 2060 PRINT"IF YOU WANT TO SPIN AROUND, GO OUTSIDE THE MODULE"
 2070 PRINT"FOR AN E.V.A."
 2080 GOTO 1330
 2090 PRINT
 2100 PRINT"IMPOSSIBLE THRUST VALUE";
 2110 IF F<0 THEN 2170
 2120 IF F-.05<.05 THEN 2150
 2130 PRINT"TOO LARGE"
 2140 GOTO 1330
 2150 PRINT"TOO SMALL"
 2160 GOTO 1330
 2170 PRINT"NEGATIVE"
 2180 GOTO 1330
 2190 PRINT
 2200 CLS:PRINT"TRANQUILITY BASE HERE -- THE EAGLE HAS LANDED"
 2210 PRINT"CONGRATULATIONS -- THERE WAS NO SPACECRAFT DAMAGE"
 2220 PRINT"YOU MAY NOW PROCEED WITH SURFACE EXPLORATION"
 2230 GOTO 2390
 2240 PRINT
 2250 CLS:PRINT"CRASH !!!!!!!!!!!!!!!!!"
 2260 PRINT"YOUR IMPACT CREATED A CRATER";ABS(H);M$;"DEEP."
 2270 X1=SQR(D1*D1+H1*H1)*G3
 2280 PRINT"AT CONTACT YOU WERE TRAVELING";X1;N$;"/HR"
 2290 GOTO 2390
 2300 PRINT
 2310 CLS:PRINT"YOU HAVE BEEN LOST IN SPACE WITH NO HOPE OF RECOVERY"
 2320 GOTO 2390
 2330 CLS:PRINT"YOU ARE DOWN SAFELY-"
 2340 PRINT
 2350 PRINT"BUT YOU MISSED THE LANDING SITE BY";ABS(D/G5);N$
 2360 GOTO 2390
 2370 PRINT
 2380 CLS:PRINT"MISSION ABENDED"
 2390 PRINT
 2400 PRINT"DO YOU WANT TO TRY IT AGAIN (YES/NO)?"
 2410 INPUT Z$
 2420 PASS=0
 2430 IF Z$="YES" THEN 130
 2440 IF Z$="NO" THEN 2460
 2450 GOTO 2400
 2460 PRINT
 2470 CLS:PRINT"TOO BAD, THE SPACE PROGRAM HATES TO LOSE EXPERIENCED"
 2480 PRINT"ASTRONAUTS."
 2490 GOSUB 2700
 2500 END
 2510 PRINT
 2520 CLS:PRINT"OK, DO YOU WANT THE COMPLETE INSTRUCTIONS OR THE INPUT-"
 2530 PRINT"OUTPUT STATEMENTS?"
 2540 PRINT"1=COMPLETE INSTRUCTIONS"
 2550 PRINT"2=INPUT-OUTPUT STATEMENTS"
 2560 PRINT"3=NEITHER"
 2570 INPUT B1
 2580 CLS
 2590 Q$="NO"
 2600 IF B1=1 THEN 500
 2610 Q$="YES"
 2620 IF B1=2 THEN 470
 2630 IF B1=3 THEN 470
 2640 GOTO 2540
 2650 END
 2660 PRINT@914,"HIT ANY KEY TO CONTINUE";
 2670 Q$=INKEY$:IF Q$="" THEN 2670
 2680 CLS
 2690 RETURN
 2700 FOR D=1TO2500:NEXT D
 2710 CLS
 2720 RETURN

# NOTE: Type forced to be:
# trs2 - TRS-80 Level II BASIC tokenized file
   10 CLEAR 700 : CLS : DIM S(2) : PRINT CHR$ (23) : RANDOM : PRINT "********* GREED ii ********" : PRINT "(C) CREATIVE COMPUTING 1979" : PRINT : PRINT : PRINT : INPUT "INSTRUCTIONS 1/YES 2/NO";W : IF W = 2 THEN 99
   11 : REM ' ** INSTRUCTIONS
   30 CLS : PRINT "****************######  G R E E D  II  #######****************" : PRINT : PRINT "     THIS IS AN EXCITING GAME OF PURE SKILL + LUCK!!!!!!!" : PRINT "THE OBJECT IS TO GET A HIGHER SCORE THAN YOUR OPPONENT AT THE "
   31 PRINT "END OF TEN TURNS (EACH). YOU MAY THROW THE DICE AS MANY TIMES" : PRINT "AS YOU WISH == AS LONG AS YOU DON'T MATCH YOUR FIRST THROW OF" : PRINT "THAT TURN!!! IF A MATCH DOES COME UP YOU LOSE ALL YOUR POINTS-"
   32 PRINT "** FOR THAT TURN.**  AS WELL AS YOUR TURN." : PRINT : PRINT "TRY NOT TO BE TO=== G R E E D Y ====" : PRINT "   KNOWING THE ODDS OF THE DICE IS EXTREMELY HELPFULL!!" : PRINT "IT ALSO HELPS TO BE A LITTLE LUCKY........" : PRINT 
   50 INPUT " GOOD LUCK (HIT ENTER)";D$ : CLS 
   99 : REM ' 
  100 CLS : PRINT CHR$ (23) : INPUT "SKILL LEVEL (1-10)";N : IF N < 1 OR N > 10 THEN 100 : ELSE N = N * 10
  122 IF N < 100 THEN 124
  123 N = 100
  124 N = 100 - N
  125 I = RND (0)
  130 IF I < .5 THEN 140
  135 N = - N
  140 S(1) = 0 : S(2) = 0
  141 I = 1
  144 CLS : PRINT CHR$ (23)
  145 PRINT @64, CHR$ (30); : PRINT @64,"TURN #";I;"   GAME TOTAL : ";S(1) : PRINT @128,"TURN TOTAL";0;"     "
  150 GOSUB 400
  160 T = R9 : R1 = R9
  162 PRINT @210, CHR$ (30); : PRINT @210,"YOUR FIRST THROW =";R9;
  164 PRINT @320, CHR$ (30) : PRINT @320,""; : INPUT "ROLL AGAIN  1/YES  2/NO";P8 : PRINT @379,"";
  168 IF P8 = 2 THEN 200
  180 GOSUB 400
  185 PRINT @256, CHR$ (30); : PRINT @256,"ROLL =";R9;
  190 IF R1 = R9 THEN 195
  191 T = T + R9
  192 PRINT @128, CHR$ (30); : PRINT @128,"TURN TOTAL =";T;
  193 GOTO 164
  195 T = 0
  200 S(1) = S(1) + T
  202 : REM ' 
  203 PRINT @110,S(1); : PRINT @384, CHR$ (30); : PRINT @384,"THIS TURN ";T;"  TOTAL =";S(1) : PRINT STRING$ (32, CHR$ (188));
  204 : REM ' 
  205 T = 0
  206 PRINT @576, CHR$ (30); : PRINT @576,"MY TURN #";I;
  210 GOSUB 400
  215 PRINT @640, CHR$ (30); : PRINT @640,"MY FIRST THROW IS ";R9
  220 R1 = R9 : T = R9
  230 N1 = R1 - 1
  231 IF N1 < 6 THEN 240
  235 N1 = 12 - N1
  240 G = 252 / N1 - R1
  241 G = (N * G) / 100 + G
  242 IF (S(2) - S(1)) < ((10 - I) * 50) THEN 244
  243 G = G / 2
  244 IF (S(1) - S(2)) < ((10 - I) * 50) THEN 250
  245 IF G < 50 THEN 248
  246 IF N < 60 THEN 249
  247 GOTO 250
  248 IF N < 60 THEN 250
  249 G = 50
  250 IF T > = G THEN 285
  255 IF I < > 10 THEN 257
  256 IF S(2) + T > S(1) THEN 290
  257 IF T < 100 THEN 261
  258 IF (T + S(2) - S(1)) < 100 THEN 261
  259 IF R1 = 2 THEN 261
  260 IF R1 < 12 THEN 290
  261 IF T < 250 THEN 265
  262 IF (T + S(2) - S(1)) > 100 THEN 290
  265 GOSUB 400
  267 PRINT @704, CHR$ (30); : PRINT @704,"ROLL =";R9;
  270 IF R9 = R1 THEN 280
  271 T = T + R9
  273 PRINT @748, CHR$ (30); : PRINT @744,"TOTAL =";T
  275 GOTO 250
  280 T = 0
  281 GOTO 290
  285 IF I < > 10 THEN 290
  286 IF S(2) + T < S(1) THEN 255
  290 S(2) = S(2) + T
  295 PRINT @768, CHR$ (30); : PRINT @768,"THIS TURN ";T;" MY TOTAL IS ";S(2);
  296 FOR X = 1 TO 1200 : NEXT 
  300 I = I + 1
  301 IF I = 11 THEN 310
  302 GOTO 145
  310 IF S(1) < = S(2) THEN 320
  315 CLS : PRINT CHR$ (23) : PRINT "*********** YOU WIN ***********" : PRINT "*** :  :  : *** :  :  : *** :  :  : *** :  :  : *** :  :  : **"
  316 S1 = S1 + 1 : GOSUB 1200
  317 GOTO 330
  320 IF S(1) < S(2) THEN 324
  321 CLS : PRINT CHR$ (23) : PRINT "************ WE TIED **********" : PRINT 
  322 S1 = S1 + .5 : S2 = S2 + .5 : GOSUB 1200 : GOTO 330
  324 S2 = S2 + 1 : CLS : PRINT CHR$ (23) : PRINT "******* SORRY YOU LOST !! *****" : PRINT "**** :  :  : ***** :  :  : **** :  :  : **** :  :  : **" : PRINT 
  325 GOSUB 1200
  330 PRINT : INPUT "PLAY AGAIN  1/YES  2/NO";YU : IF YU < > 2 THEN GOTO 99
  332 CLS : PRINT "GOODBYE" : END 
  400 LET D3 = RND (6) : LET D2 = RND (6)
  420 R9 = D3 + D2
  421 RETURN 
  999 END 
 1200 PRINT : PRINT " YOUR               MY" : PRINT "*SCORE*          *SCORE*" : PRINT " "S(1);"             ";S(2) : PRINT : PRINT "       GAMES SO FAR" : PRINT "YOU=";S1;"              ME=";S2 : PRINT : RETURN 

# NOTE: Type forced to be:
# trs2 - TRS-80 Level II BASIC tokenized file
   10 REM MUGWUMP
   20 CLS : PRINT @284,"MUGWUMP"
   30 FOR I = 1 TO 2000 : NEXT I
   40 REM 
   50 REM 
   60 CLS : RANDOM 
   70 PRINT @ 403,"* * *  M U G W U M P  * * *"
   80 PRINT @ 960, "DO YOU WANT INSTRUCTIONS? (Y OR N) "; CHR$ (14);
   90 Q$ = INKEY$ : IF Q$ = ""THEN 90 : ELSE PRINT Q$; CHR$ (15);
  100 IF Q$ < > "Y" AND Q$ < > "N" THEN 80 : ELSE IF Q$ < > "Y" THEN 120
  110 GOSUB 860
  120 DIM P(4,2)
  130 GOSUB 740
  140 CLS : GOSUB 1000
  150 T = 0
  160 T = T + 1
  170 PRINT @ 832, CHR$ (30); CHR$ (29);
  180 M$ = "+"
  190 PRINT "TURN NO.";T; CHR$ (24);" :   WHAT'S YOUR GUESS?";
  200 INPUT " (X,Y)";I$,I1$ : I$ = I$ + "," + I1$
  210 IF LEN (I$) < > 3 THEN 250
  220 IF ASC ( MID$ (I$,1,1)) < 48 OR ASC ( MID$ (I$,1,1)) > 57 OR ASC ( MID$ (I$,3,1)) < 48 OR ASC ( MID$ (I$,3,1)) > 57 THEN 250
  230 IF MID$ (I$,2,1) < > "," THEN 250
  240 GOTO 260
  250 PRINT @ 896,"PLEASE INPUT NUMBERS FROM 0 TO 9 ONLY" : GOTO 170
  260 M = VAL ( MID$ (I$,1,1)) : N = VAL ( MID$ (I$,3,1))
  270 PRINT 
  280 M1 = M : N1 = N
  290 FOR I = 1 TO 4
  300 PRINT @ ((I + 1) * 64) + 32,"";
  310 IF P(I,1) = - 1 THEN 420
  320 IF P(I,1) < > M THEN 380
  330 IF P(I,2) < > N THEN 380
  340 P(I,1) = - 1
  350 PRINT "** YOU HAVE FOUND MUGWUMP";I;"**";
  360 M$ = "M"
  370 GOTO 420
  380 D = SQR ((P(I,1) - M) * (P(I,1) - M) + (P(I,2) - N) * (P(I,2) - N))
  390 D1 = INT (D * 100) / 100
  400 PRINT @ ((I + 1) * 64) + 35,"";
  410 PRINT LLIST "###.##";D1; : PRINT " UNITS FROM MUGWUMP";I;
  420 NEXT I
  430 GOSUB 1090
  440 FOR J = 1 TO 4
  450 IF P(J,1) < > - 1 THEN 530
  460 NEXT J
  470 PRINT @ 832,""; CHR$ (30);
  480 FOR B = 1 TO 5
  490 FOR PP = 1 TO 150 : NEXT PP : PRINT @ 768, CHR$ (30); : FOR PP = 1 TO 150 : NEXT PP
  500 PRINT @ 779,"<*><*> YOU GOT THEM ALL IN";T;"TURNS! <*><*>"
  510 NEXT B
  520 GOTO 660
  530 IF T < 10 THEN 160
  540 FOR I% = 2 TO 5 : PRINT @ (I% * 64) + 32, CHR$ (30); : NEXT I%
  550 PRINT @ 164,"";
  560 PRINT "SORRY, THAT'S 10 TRIES."; CHR$ (30);
  570 PRINT @ 228,"";
  580 PRINT " HERE'S WHERE THEY ARE : "; CHR$ (30);
  590 C = 5
  600 FOR I% = 1 TO 4
  610 PRINT @ (C * 64) + 37,"";
  620 IF P(I%,1) = - 1 THEN 650
  630 PRINT "MUGWUMP";I%;"IS AT ("; : PRINT LLIST "#";P(I%,1); : PRINT ","; : PRINT LLIST "#";P(I%,2); : PRINT ")"; CHR$ (30);
  640 C = C + 1
  650 NEXT I%
  660 PRINT @ 832, CHR$ (30) : PRINT TAB 7);
  670 PRINT "THAT WAS FUN!  HOW ABOUT PLAYING AGAIN? (Y OR N) "; CHR$ (14);
  680 Q$ = INKEY$ : IF Q$ = ""THEN 680 : ELSE PRINT Q$; CHR$ (15);
  690 CLS : PRINT @ 384,"";
  700 IF Q$ < > "Y" THEN 1130
  710 PRINT TAB 13);"FOUR MORE MUGWUMPS ARE NOW HIDING..."
  720 FOR PP = 1 TO 1000 : NEXT PP
  730 GOTO 130
  740 FOR J = 1 TO 2
  750 FOR I = 1 TO 4
  760 P(I,J) = RND (10) - 1
  770 NEXT I
  780 NEXT J
  790 FOR I = 1 TO 3
  800 FOR J = 2 TO 4
  810 IF I = J THEN 830
  820 IF P(I,1) = P(J,1) AND P(I,2) = P(J,2) THEN I = 4 : J = 5 : NEXT J,I : GOTO 740
  830 NEXT J
  840 NEXT I
  850 RETURN 
  860 CLS : PRINT @ 192,"";
  870 PRINT "THE OBJECT OF THIS GAME IS TO FIND FOUR MUGWUMPS HIDDEN ON A"
  880 PRINT "   10 BY 10 GRID.  HOMEBASE IS 0,0.  ANY GUESS MUST BE TWO"
  890 PRINT "   NUMBERS, 1,6 FOR EXAMPLE, WITH EACH NUMBER BETWEEN 0 AND 9"
  900 PRINT "   INCLUSIVE.  THE FIRST NUMBER IS THE DISTANCE TO THE RIGHT OF"
  910 PRINT "   HOMEBASE AND THE SECOND NUMBER IS THE DISTANCE ABOVE HOME-"
  920 PRINT "   BASE."
  930 PRINT 
  940 PRINT "YOU GET 10 TRIES.  AFTER EACH TRY, I WILL TELL YOU HOW FAR YOU"
  950 PRINT "   ARE FROM EACH MUGWUMP."
  960 PRINT @ 960,"PRESS 'ENTER' TO CONTINUE "; CHR$ (14);
  970 Q$ = INKEY$ : IF Q$ = ""OR Q$ < > CHR$ (13) THEN 970 : ELSE PRINT CHR$ (15);
  980 RETURN 
  990 REM GRID DISPLAY
 1000 PRINT @ 0,"";
 1010 FOR L = 9 TO 0 STEP - 1
 1020 PRINT " ";L;".  .  .  .  .  .  .  .  .  ."; CHR$ (26); CHR$ (29);
 1030 NEXT L
 1040 PRINT "    0  1  2  3  4  5  6  7  8  9"; CHR$ (26); CHR$ (29);
 1050 PRINT "    X "; CHR$ (94); CHR$ (26); CHR$ (29);
 1060 PRINT @ 512,"["; CHR$ (26); CHR$ (29);"Y";
 1070 PRINT @ 832,"";
 1080 RETURN 
 1090 X = M * 3 + 4 : Y = 9 - N
 1100 PRINT @ (64 * Y) + X,M$;
 1110 PRINT @ 549,"YOUR LAST GUESS WAS "; : PRINT LLIST "#";M1; : PRINT ","; : PRINT LLIST "#";N1;
 1120 RETURN 
 1130 END 

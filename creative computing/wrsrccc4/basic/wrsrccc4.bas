# NOTE: Type forced to be:
# trs2 - TRS-80 Level II BASIC tokenized file
   10 CLS : PRINT @407,"WORD SEARCH PUZZLE"
   20 PRINT : PRINT TAB 14)"COPYRIGHT 1983 GAZZA'S COMPUTING , PERTH"
   30 PRINT @960,""; : INPUT "DO YOU NEED INSTRUCTIONS";I$
   40 IF LEFT$ (I$,1) = "N" THEN CLS : GOTO 190 : ELSE CLS 
   50 PRINT TAB 23);"WORD SEARCH PUZZLE" : PRINT 
   60 PRINT "THIS PROGRAM IS A WORD SEARCH PUZZLE GENERATOR !"
   70 PRINT "THE PROGRAM TAKES A SET OF INPUT STRINGS, PURGES ALL"
   80 PRINT "NON-ALPHABETIC CHARACTERS OUT OF THEN, AND INCORPORATES"
   90 PRINT "THEM INTO A PUZZLE"
  100 PRINT 
  110 PRINT "  IN THE COURSE OF MAKING THE PUZZLE , THE MACHINE MAY"
  120 PRINT "FIND THAT IT CAN'T PUT A PARTICULAR WORD ANYWHERE, AND"
  130 PRINT "SO WILL ASK YOU IF IT SHOULD START THE WHOLE PUZZLE"
  140 PRINT "OVER . IF YOU DON'T WANT IT TO START OVER , TYPING 'NO'
  150 PRINT "WILL THROW AWAY THAT PARTICULAR WORD. IF THIS PERSISTS
  160 PRINT "TRY GIVING LESS WORDS OR BIGGER PUZZLE DIMENSIONS!"
  170 PRINT @979,"PRESS ANY KEY TO CONTINUE";
  180 IF INKEY$ = ""THEN 180 : ELSE CLS 
  190 CLEAR 3000
  200 INPUT "DO YOU WANT THIS TO GO TO THE PRINTER";PR$
  210 IF LEFT$ (PR$,1) = "N" THEN TW = 64 : GOTO 230
  220 INPUT "HOW MANY COLUMNS DOES THE PRINTER HAVE";TW
  230 INPUT "DO YOU WANT AN ANSWER PRINTOUT";X$
  240 INPUT "WHAT IS TO BE THE WIDTH OF THE PUZZLE";W : MD = W
  250 IF W * 2 < TW THEN 270
  260 PRINT TAB 7);"(";TW;"CHARACTERS MAX. )"
  270 IF W < 1 THEN 240
  280 INPUT "THE LENGTH";L : IF L > W THEN MD = L
  290 IF L < 1 THEN 280
  300 INPUT "HOW MANY WORDS IN THE PUZZLE DO YOU WANT";M
  310 IF M > 2 THEN 330
  320 PRINT "I'M SORRY THERE MUST BE AT LEAST TWO WORDS" : GOTO 300
  330 DIM A$(L,W),W$(M)
  340 DIM W(M,3),DXY(8,2),DD(28)
  350 PRINT "NOW ENTER A TITLE FOR THE PUZZLE"
  370 INPUT XY$
  380 CLS 
  390 PRINT "OK . . . ENTER A WORD AT EACH QUESTION MARK"
  400 PRINT "TO REDO THE PREVIOUS WORD TYPE A HYPHEN (-)
  410 PRINT "WHEN YOU RUN OUT OF WORDS TYPE A PERIOD (.)
  420 SC = 320 : FOR I = 1 TO M
  430 PRINT @192 ,"NEXT WORD "; CHR$ (30); : INPUT T$
  440 IF T$ < > "-" THEN 470 : ELSE I = I - 1
  450 SC = SC - 16 : PRINT @192,"REDO ";W$(I); ". . . "; CHR$ (30);
  460 INPUT T$ : GOTO 440
  470 IF T$ = "." THEN M = I - 1 : GOTO 710
  480 IF LEN (T$) = 0 THEN PRINT @192,"INPUT ERROR REDO"; CHR$ (30);
  490 IF LEN (T$) = 0 THEN INPUT T$ : GOTO 440 : ELSE J = 1
  500 TE$ = MID$ (T$,J,1) : IF TE$ > = "A" AND TE$ < = "Z" THEN 580
  510 IF TE$ < "A" OR TE$ > "Z" THEN 540
  520 T$ = LEFT$ (T$,J - 1) + CHR$ ( ASC ( MID$ (T$,J,1))) + RIGHT$ (T$, LEN (T$) - J)
  530 GOTO 580
  540 IF TE$ = T$ THEN T$ = "": GOTO 480
  550 IF J = LEN (T$) THEN T$ = LEFT$ (T$,J - 1) : GOTO 610
  560 IF J = 1 THEN T$ = RIGHT$ (T$, LEN (T$) - 1) : J = J - 1 : GOTO 580
  570 T$ = LEFT$ (T$,J - 1) + RIGHT$ (T$, LEN (T$) - J) : J = J - 1
  580 J = J + 1 : IF J < = LEN (T$) THEN 500
  590 IF LEN (T$) > MD THEN 640
  600 FOR IZ = 1 TO I - 1 : IF W$(IZ) = T$ THEN 660
  610 NEXT : PRINT @SC,"-";T$;"-"; CHR$ (31); : SC = SC + 16 : FL = 2
  620 IF LEN (T$) + FL > 16 THEN SC = SC + 16 : FL = FL - 16 : GOTO 620
  630 GOTO 680
  640 PRINT @192,"STRING TO LONG; REDO :  "; CHR$ (30); : INPUT T$
  650 GOTO 440
  660 PRINT @192,"DUPLICATE ENTRY; REDO : "; CHR$ (30); : INPUT T$
  670 GOTO 440
  680 W$(I) = T$
  690 NEXT I
  700 REM 
  710 CLS : PRINT "NOW LET ME PONDER THIS......."
  720 FOR I = 1 TO M - 1
  730 FOR J = I + 1 TO M
  740 IF LEN (W$(I)) < LEN (W$(J)) THEN HZ$ = W$(I) : W$(I) = W$(J) : W$(J) = HZ$
  750 NEXT J,I
  760 FOR I = 1 TO 8 : READ DXY(I,1),DXY(I,2) : NEXT 
  770 FOR I = 1 TO 28 : READ DD(I) : NEXT 
  780 DATA 0,1,1,1,1,0,1,-1,0,-1,-1,-1,-1,0,-1,1
  790 DATA 2,4,6,8,2,4,6,8,2,4,6,8,2,4,6,8,2,4,6,8,2,4,6,8,1,3,5,7
  800 FOR I = 1 TO M
  810 LN = LEN (W$(I))
  820 NT = 0
  830 SD = DD( RND (28))
  840 SX = RND (W) : X1 = SX + (LN - 1) * DXY(SD,1) : IF X1 < 1 OR X1 > W THEN 830
  850 SY = RND (L) : X1 = SY + (LN - 1) * DXY(SD,2) : IF X1 < 1 OR X1 > L THEN 830
  860 NT = NT + 1 : IF NT < > W * L * 2 THEN 910
  870 PRINT "COULD'NT FIT '"; W$(I);"' IN THE PUZZLE"
  880 INPUT "DO YOU WANT TO START OVER";A$
  890 IF LEFT$ (A$,1) = "Y" THEN 800
  900 W$(I) = "": GOTO 990
  910 J = SY : K = SX
  920 FOR P = 1 TO LN
  930 IF LEN (A$(J,K)) AND A$(J,K) < > MID$ (W$(I),P,1) THEN 830
  940 J = J + DXY(SD,2) : K = K + DXY(SD,1) : NEXT P
  950 J = SY : K = SX
  960 FOR P = 1 TO LN : A$(J,K) = MID$ (W$(I),P,1)
  970 J = J + DXY(SD,2) : K = K + DXY(SD,1) : NEXT P
  980 W(I,1) = SX : W(I,2) = SY : W(I,3) = SD
  990 NEXT I
 1000 FOR I = 1 TO L
 1010 FOR J = 1 TO W
 1015 LE = RND (90) : IF LE < 65 THEN 1015
 1020 IF A$(I,J) = ""THEN A$(I,J) = CHR$ (LE)
 1030 NEXT J,I
 1040 FOR I = 1 TO M - 1 : FOR J = I + 1 TO M
 1050 IF W$(I) < = W$(J) THEN 1080
 1060 HZ$ = W$(I) : W$(I) = W$(J) : W$(J) = HZ$
 1070 FOR K = 1 TO 3 : HZ = W(I,K) : W(I,K) = W(J,K) : W(J,K) = HZ : NEXT K
 1080 NEXT J : NEXT I
 1090 INPUT "HOW MANY COPIES OF THIS PUZZLE DO YOU WANT";N
 1100 PRINT "FOR EACH COPY , HIT ENTER TO BEGIN PRINTING...";
 1110 FOR C = 1 TO N : GOSUB 1120 : NEXT C : GOTO 1400
 1120 PRINT PM$; : INPUT A$ : PRINT : PR$ = LEFT$ (PR$,1)
 1130 PM$ = "HIT ENTER TO CONTINUE"
 1140 T = (TW - 2 * W) / 2 : CLS : PR = (PR$ = "Y") : TS = (64 - 2 * W) / 2
 1150 IF PR THEN LPRINT " "
 1160 PRINT : IF PR THEN LPRINT " "
 1170 CLS : PRINT TAB (64 - LEN (XY$)) / 2);XY$
 1180 IF PR THEN LPRINT TAB (TW - LEN (XY$)) / 2);XY$
 1190 PRINT : PRINT : IF PR THEN LPRINT " ": LPRINT " "
 1200 FOR J = 1 TO L : PRINT TAB TS); : IF PR THEN LPRINT TAB T);
 1210 FOR K = 1 TO W : IF A$(J,K) < > "." THEN 1240
 1220 PRINT ". "; : IF PR THEN LPRINT ". ";
 1230 GOTO 1250
 1240 PRINT A$(J,K);" "; : IF PR THEN LPRINT A$(J,K);" ";
 1250 NEXT K : PRINT : IF PR THEN LPRINT " "
 1260 NEXT J
 1270 PRINT : PRINT : IF PR THEN LPRINT " ": LPRINT " "
 1280 PO = 0 : PRINT "FIND THESE HIDDEN WORDS IN THE ABOVE PUZZLE"
 1290 IF PR THEN LPRINT "FIND THESE WORDS IN THE ABOVE PUZZLE"
 1300 PRINT : IF PR THEN LPRINT " "
 1310 FOR J = 1 TO M : IF LEN (W$(J)) = 0 THEN 1370
 1320 IF POS (0) + LEN (W$(J)) > 62 THEN PRINT 
 1330 IF PR THEN IF PO + LEN (W$(J)) > TW - 2 THEN LPRINT " ": PO = 0
 1340 PRINT W$(J), : IF PR THEN LPRINT W$(J),
 1350 PO = PO + 16
 1370 NEXT J : PRINT : PRINT : PRINT : PRINT 
 1380 IF PR THEN LPRINT " ": LPRINT " ": LPRINT " ": LPRINT " "
 1390 RETURN 
 1400 IF LEFT$ (X$,1) = "Y" THEN 1420
 1410 GOTO 1410
 1420 REM 
 1430 FOR I = 1 TO L : FOR J = 1 TO W : A$(I,J) = "." : NEXT J : NEXT I
 1440 FOR I = 1 TO M
 1450 LN = LEN (W$(I)) : J = W(I,2) : K = W(I,1)
 1460 FOR P = 1 TO LN
 1470 A$(J,K) = MID$ (W$(I),P,1)
 1480 J = J + DXY(W(I,3),2) : K = K + DXY(W(I,3),1) : NEXT P
 1490 NEXT I
 1500 XY$ = "HERE IS THE ANSWER KEY : 
 1510 GOSUB 1120
 1520 PRINT : PRINT 
 1530 END 

# NOTE: Type forced to be:
# trs2 - TRS-80 Level II BASIC tokenized file
   10 CLS : PRINT @284,"MUGWUMP"
   11 PRINT @409,"COPYRIGHT BY"
   12 PRINT @470,"CREATIVE COMPUTING"
   13 PRINT @536,"P.O. BOX 789-M"
   14 PRINT @593,"MORRISTOWN, NEW JERSEY 07960"
   15 FOR I = 1 TO 2000 : NEXT I
   20 REM 
   25 REM 
   30 CLS : RANDOM 
   40 PRINT @ 403,"* * *  M U G W U M P  * * *"
   50 PRINT @ 960, "DO YOU WANT INSTRUCTIONS? (Y OR N) "; CHR$ (14);
   55 Q$ = INKEY$ : IF Q$ = ""THEN 55 : ELSE PRINT Q$; CHR$ (15);
   60 IF Q$ < > "Y" AND Q$ < > "N" THEN 50 : ELSE IF Q$ < > "Y" THEN 80
   70 GOSUB 810
   80 DIM P(4,2)
   90 GOSUB 690
  100 CLS : GOSUB 940
  110 T = 0
  120 T = T + 1
  130 PRINT @ 832, CHR$ (30); CHR$ (29);
  140 M$ = "+"
  150 PRINT "TURN NO.";T; CHR$ (24);" :   WHAT'S YOUR GUESS?";
  160 INPUT " (X,Y)";I$,I1$ : I$ = I$ + "," + I1$
  170 IF LEN (I$) < > 3 THEN 210
  180 IF ASC ( MID$ (I$,1,1)) < 48 OR ASC ( MID$ (I$,1,1)) > 57 OR ASC ( MID$ (I$,3,1)) < 48 OR ASC ( MID$ (I$,3,1)) > 57 THEN 210
  190 IF MID$ (I$,2,1) < > "," THEN 210
  200 GOTO 220
  210 PRINT @ 896,"PLEASE INPUT NUMBERS FROM 0 TO 9 ONLY" : GOTO 130
  220 M = VAL ( MID$ (I$,1,1)) : N = VAL ( MID$ (I$,3,1))
  230 PRINT 
  240 M1 = M : N1 = N
  250 FOR I = 1 TO 4
  260 PRINT @ ((I + 1) * 64) + 32,"";
  270 IF P(I,1) = - 1 THEN 380
  280 IF P(I,1) < > M THEN 340
  290 IF P(I,2) < > N THEN 340
  300 P(I,1) = - 1
  310 PRINT "** YOU HAVE FOUND MUGWUMP";I;"**";
  320 M$ = "M"
  330 GOTO 380
  340 D = SQR ((P(I,1) - M) * (P(I,1) - M) + (P(I,2) - N) * (P(I,2) - N))
  350 D1 = INT (D * 100) / 100
  360 PRINT @ ((I + 1) * 64) + 35,"";
  370 PRINT LLIST "###.##";D1; : PRINT " UNITS FROM MUGWUMP";I;
  380 NEXT I
  390 GOSUB 1030
  400 FOR J = 1 TO 4
  410 IF P(J,1) < > - 1 THEN 490
  420 NEXT J
  430 PRINT @ 832,""; CHR$ (30);
  440 FOR B = 1 TO 5
  450 FOR PP = 1 TO 150 : NEXT PP : PRINT @ 768, CHR$ (30); : FOR PP = 1 TO 150 : NEXT PP
  460 PRINT @ 779,"<*><*> YOU GOT THEM ALL IN";T;"TURNS! <*><*>"
  470 NEXT B
  480 GOTO 620
  490 IF T < 10 THEN 120
  500 FOR I% = 2 TO 5 : PRINT @ (I% * 64) + 32, CHR$ (30); : NEXT I%
  510 PRINT @ 164,"";
  520 PRINT "SORRY, THAT'S 10 TRIES."; CHR$ (30);
  530 PRINT @ 228,"";
  540 PRINT " HERE'S WHERE THEY ARE : "; CHR$ (30);
  550 C = 5
  560 FOR I% = 1 TO 4
  570 PRINT @ (C * 64) + 37,"";
  580 IF P(I%,1) = - 1 THEN 610
  590 PRINT "MUGWUMP";I%;"IS AT ("; : PRINT LLIST "#";P(I%,1); : PRINT ","; : PRINT LLIST "#";P(I%,2); : PRINT ")"; CHR$ (30);
  600 C = C + 1
  610 NEXT I%
  620 PRINT @ 832, CHR$ (30) : PRINT TAB 7);
  630 PRINT "THAT WAS FUN!  HOW ABOUT PLAYING AGAIN? (Y OR N) "; CHR$ (14);
  635 Q$ = INKEY$ : IF Q$ = ""THEN 635 : ELSE PRINT Q$; CHR$ (15);
  640 CLS : PRINT @ 384,"";
  650 IF Q$ < > "Y" THEN 1070
  660 PRINT TAB 13);"FOUR MORE MUGWUMPS ARE NOW HIDING..."
  670 FOR PP = 1 TO 1000 : NEXT PP
  680 GOTO 90
  690 FOR J = 1 TO 2
  700 FOR I = 1 TO 4
  710 P(I,J) = RND (10) - 1
  720 NEXT I
  730 NEXT J
  740 FOR I = 1 TO 3
  750 FOR J = 2 TO 4
  760 IF I = J THEN 780
  770 IF P(I,1) = P(J,1) AND P(I,2) = P(J,2) THEN I = 4 : J = 5 : NEXT J,I : GOTO 690
  780 NEXT J
  790 NEXT I
  800 RETURN 
  810 CLS : PRINT @ 192,"";
  820 PRINT "THE OBJECT OF THIS GAME IS TO FIND FOUR MUGWUMPS HIDDEN ON A"
  830 PRINT "   10 BY 10 GRID.  HOMEBASE IS 0,0.  ANY GUESS MUST BE TWO"
  840 PRINT "   NUMBERS, 1,6 FOR EXAMPLE, WITH EACH NUMBER BETWEEN 0 AND 9"
  850 PRINT "   INCLUSIVE.  THE FIRST NUMBER IS THE DISTANCE TO THE RIGHT OF"
  860 PRINT "   HOMEBASE AND THE SECOND NUMBER IS THE DISTANCE ABOVE HOME-"
  870 PRINT "   BASE."
  880 PRINT 
  890 PRINT "YOU GET 10 TRIES.  AFTER EACH TRY, I WILL TELL YOU HOW FAR YOU"
  900 PRINT "   ARE FROM EACH MUGWUMP."
  910 PRINT @ 960,"PRESS 'ENTER' TO CONTINUE "; CHR$ (14);
  915 Q$ = INKEY$ : IF Q$ = ""OR Q$ < > CHR$ (13) THEN 915 : ELSE PRINT CHR$ (15);
  920 RETURN 
  930 REM GRID DISPLAY
  940 PRINT @ 0,"";
  950 FOR L = 9 TO 0 STEP - 1
  960 PRINT " ";L;".  .  .  .  .  .  .  .  .  ."; CHR$ (26); CHR$ (29);
  970 NEXT L
  980 PRINT "    0  1  2  3  4  5  6  7  8  9"; CHR$ (26); CHR$ (29);
  990 PRINT "    X "; CHR$ (94); CHR$ (26); CHR$ (29);
 1000 PRINT @ 512,"["; CHR$ (26); CHR$ (29);"Y";
 1010 PRINT @ 832,"";
 1020 RETURN 
 1030 X = M * 3 + 4 : Y = 9 - N
 1040 PRINT @ (64 * Y) + X,M$;
 1050 PRINT @ 549,"YOUR LAST GUESS WAS "; : PRINT LLIST "#";M1; : PRINT ","; : PRINT LLIST "#";N1;
 1060 RETURN 
 1070 RUN "MENU"

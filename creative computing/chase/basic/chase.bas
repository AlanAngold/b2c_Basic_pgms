# NOTE: Type forced to be:
# trs2 - TRS-80 Level II BASIC tokenized file
   10 CLS : PRINT @413,"CHASE"
   20 PRINT : PRINT TAB 7) "COPYRIGHT 1979 CREATIVE COMPUTING MORRISTOWN NJ"
   30 PRINT @960,""; : INPUT "DO YOU NEED INSTRUCTIONS";I$
   40 IF LEFT$ (I$,1) = "N" THEN CLS : GOTO 200
   41 CLS : PRINT TAB 29);"CHASE" : PRINT 
   50 PRINT "YOU ARE WITHIN THE WALLS OF A HIGH VOLTAGE MAZE"
   60 PRINT "THERE ARE FIVE SECURITY MACHINES TRYING TO DESTROY YOU"
   70 PRINT "YOU ARE THE '**'. THE INTERCEPTORS ARE THE '++'"
   80 PRINT "THE AREAS MARKED '" STRING$ (2,191)"' ARE HIGH VOLTAGE"
   90 PRINT "YOUR ONLY CHANCE FOR SURVIVAL IS TO MANOUVER EACH"
  100 PRINT "INTERCEPTOR INTO AN '" STRING$ (2,191)"'.----GOOD LUCK----"
  110 PRINT "MOVES ARE  7.8.9"
  120 PRINT "           4.*.6"
  130 PRINT "           1.2.3"
  150 PRINT "10 = NO MOVE FOR THE REST OF THE GAME"
  160 PRINT "-1 = GAVE UP, SITUATION HOPELESS."
  170 PRINT " 0 = A TREMENDOUS (BUT UNFORTUNATELY RANDOM) LEAP"
  180 PRINT 
  190 PRINT @979,"pRESS ANY KEY TO CONTINUE";
  191 IF INKEY$ = ""THEN 191 : ELSE CLS 
  200 DIM A(10,20),A1(10,20),N(12),L(5),M(5),L1(5),M1(5)
  210 : REM ' 
  220 FOR B = 1 TO 10
  230 FOR C = 1 TO 20
  240 X = INT (10 * RND (0))
  250 IF X = 5 THEN 280
  260 A(B,C) = ASC (" ")
  270 GOTO 290
  280 A(B,C) = ASC ("X")
  290 NEXT C
  300 NEXT B
  310 FOR D = 1 TO 10
  320 A(D,1) = ASC ("X") : A(D,20) = ASC ("X")
  330 NEXT D
  340 FOR F = 1 TO 20
  350 A(1,F) = ASC ("X") : A(10,F) = ASC ("X")
  360 NEXT F
  370 GOTO 420
  380 H = INT (2 + 8 * RND (0))
  390 I = INT (2 + 18 * RND (0))
  400 IF A(H,I) < > ASC (" ") THEN 380
  410 RETURN 
  420 GOSUB 380
  430 A(H,I) = ASC ("*")
  440 J = H : K = I
  450 FOR N9 = 1 TO 5
  460 GOSUB 380
  470 A(H,I) = ASC ("+")
  480 L(N9) = H : M(N9) = I
  490 NEXT N9
  500 FOR B1 = 1 TO 10 : FOR B2 = 1 TO 20 : A1(B1,B2) = A(B1,B2) : NEXT B2 : NEXT B1
  510 FOR B1 = 1 TO 5 : L1(B1) = L(B1) : M1(B1) = M(B1) : NEXT B1
  520 J1 = J : K1 = K
  530 Y9 = 0
  540 PRINT @0,""; : FOR D2 = 1 TO 10
  550 FOR B2 = 1 TO 20
  560 N$ = CHR$ (A(D2,B2) - 103 * (A(D2,B2) = 88))
  570 PRINT N$N$;
  580 NEXT B2
  590 PRINT 
  600 NEXT D2
  610 IF Y9 < > 10 THEN 640
  620 PRINT 
  630 GOTO 890
  640 PRINT : INPUT "WHAT IS YOUR MOVE";Y9
  650 J2 = J : K2 = K
  660 IF Y9 = 0 THEN 860
  670 IF Y9 < 0 THEN 1230
  680 IF Y9 = 10 THEN 1070
  690 ON Y9 GOTO 820,800,780,840,890,760,700,720,740
  700 J = J - 1 : K = K - 1
  710 GOTO 890
  720 J = J - 1
  730 GOTO 890
  740 J = J - 1 : K = K + 1
  750 GOTO 890
  760 K = K + 1
  770 GOTO 890
  780 J = J + 1 : K = K + 1
  790 GOTO 890
  800 J = J + 1
  810 GOTO 890
  820 J = J + 1 : K = K - 1
  830 GOTO 890
  840 K = K - 1
  850 GOTO 890
  860 PRINT "$6,000,000 JUMP!!!"
  870 J = INT (2 + 8 * RND (0))
  880 K = INT (2 + 18 * RND (0))
  890 IF A(J,K) = ASC ("X") THEN 1260
  900 A(J2,K2) = ASC (" ")
  910 A(J,K) = ASC ("*")
  920 GOTO 1070
  930 : REM ' INTERCEPTOR MOVEMENT
  940 IF A(X,Y) = ASC ("X") THEN 1040
  950 X2 = X : Y2 = Y
  960 X = SGN (J - X) : Y = SGN (K - Y)
  970 X = X + X2 : Y = Y + Y2
  980 IF A(X,Y) = ASC ("*") THEN 1050
  990 IF A(X,Y) = ASC (" ") THEN 1020
 1000 A(X2,Y2) = ASC (" ")
 1010 RETURN 
 1020 A(X,Y) = ASC ("+")
 1030 A(X2,Y2) = ASC (" ")
 1040 RETURN 
 1050 G9 = 99
 1060 RETURN 
 1070 FOR N9 = 1 TO 5
 1080 X = L(N9) : Y = M(N9)
 1090 G9 = 0
 1100 GOSUB 940
 1110 IF G9 < > 0 THEN 1240
 1120 L(N9) = X : M(N9) = Y
 1130 NEXT N9
 1140 FOR N9 = 1 TO 5
 1150 IF A(L(N9),M(N9)) < > ASC (" ") THEN 1170
 1160 A(L(N9),M(N9)) = ASC ("+")
 1170 NEXT N9
 1180 FOR N9 = 1 TO 5
 1190 IF A(L(N9),M(N9)) < > ASC ("X") THEN 540
 1200 NEXT N9
 1210 PRINT : PRINT "YOU HAVE DESTROYED ALL YOUR OPPONENTS - THE GAME IS YOURS"
 1220 GOTO 1290
 1230 PRINT : PRINT "GIVE UP, EH."
 1240 PRINT : PRINT "*** YOU HAVE BEEN DESTROYED BY A LUCKY COMPUTER ***"
 1250 GOTO 1290
 1260 PRINT : PRINT "HIGH VOLTAGE!!!!!!!!!"
 1270 PRINT "***** ZAP *****  YOU'RE DEAD!!!"
 1280 PRINT 
 1290 PRINT : PRINT "ANOTHER GAME (Y/N)";
 1300 INPUT N9$
 1310 IF N9$ < > "Y" THEN 1390
 1320 PRINT "SAME SETUP (Y/N)";
 1330 INPUT N9$
 1340 CLS : IF N9$ < > "Y" THEN 210
 1350 FOR B1 = 1 TO 10 : FOR B2 = 1 TO 20 : A(B1,B2) = A1(B1,B2) : NEXT B2 : NEXT B1
 1360 FOR B1 = 1 TO 5 : L(B1) = L1(B1) : M(B1) = M1(B1) : NEXT B1
 1370 J = J1 : K = K1
 1380 GOTO 530
 1390 END 

# NOTE: Type forced to be:
# trs2 - TRS-80 Level II BASIC tokenized file
   10 RANDOM 
   20 CLS : PRINT TAB 26)"PUZZLE"
   30 PRINT : PRINT : PRINT 
   40 PRINT "  THIS PROGRAM IS A WORD SEARCH PUZZLE GENERATOR!!"
   50 PRINT "THE PROGRAM TAKES A SET OF INPUT STRINGS,PURGES ALL"
   60 PRINT "NON-ALPHABETIC CHARACTERS OUT OF THEM, AND INCORPORATES"
   70 PRINT "THEM INTO A WORD SEARCH PUZZLE."
   80 PRINT 
   90 FOR K = 1 TO 5000 : NEXT K
  100 CLS 
  110 PRINT "  IN THE COURSE OF MAKING THE PUZZLE, THE MACHINE MAY"
  120 PRINT "FIND THAT IT CAN'T PUT A PARTICULAR WORD ANYWHERE, AND"
  130 PRINT "SO WILL ASK YOU IF IT SHOULD START THE WHOLE PUZZLE"
  140 PRINT "OVER.  IF YOU DON'T WANT IT TO START OVER, TYPING 'NO'"
  150 PRINT "WILL THROW AWAY THAT PARTICULAR WORD.  IF THIS PERSISTS,"
  160 PRINT "TRY EITHER GIVING LESS WORDS OR BIGGER PUZZLE DIMENSIONS!"
  170 PRINT : PRINT 
  180 CLEAR 15000
  190 DEF FN A(Z) = INT ( RND (0) * Z + 1)
  200 INPUT "HOW MANY COLUMNS DOES YOUR PRINTER HAVE";TW
  210 INPUT "DO YOU WANT A SOLUTION PRINTOUT";X$
  220 INPUT "WHAT IS TO BE THE WIDTH OF THE PUZZLE";W : MD = W
  230 IF W * 2 < = TW THEN 250 
  240 PRINT "THAT WILL NOT FIT IN";TW;" COLUMNS." : GOTO 220 
  250 IF W < 1 THEN 220 
  260 INPUT "THE LENGTH";L : IF L > W THEN MD = L
  270 IF L < 1 THEN 260 
  280 INPUT "WHAT IS THE MAXIMUM NUMBER OF WORDS IN THE PUZZLE";M
  290 IF M > = 2 THEN 310 
  300 PRINT "SORRY; THERE MUST BE AT LEAST 2 WORDS."; GOTO 280 
  310 PRINT 
  320 DIM A$(L,W),W$(M)
  330 DIM W(M,3),DXY(8,2),DD(28)
  340 PRINT "NOW ENTER A HEADING THAT WILL BE PRINTED OVER THE PUZZLE : "
  350 PRINT "(";TW;" CHARACTERS MAXIMUM )"
  360 INPUT XY$
  370 PRINT "OK . . . ENTER A WORD AT EACH QUESTION MARK."
  380 PRINT "TO REDO THE PREVIOUS WORD,TYPE A HYPEN (-)."
  390 PRINT "WHEN YOU RUN OUT OF WORDS, TYPE A PERIOD (.)"
  400 FOR I = 1 TO M
  410 INPUT T$ : IF T$ = "-" THEN I = I - 1 : PRINT "REDO ";W$(I);". . ." : GOTO 410 : ELSE 420 
  420 IF T$ = "." THEN M = I - 1 : GOTO 620 : ELSE 430 
  430 IF LEN (T$) = 0 THEN PRINT "INPUT ERROR; REDO : ": GOTO 410 : ELSE 440 
  440 J = 1
  450 TE$ = MID$ (T$,J,1) : IF TE$ > = "A" AND TE$ < = "Z" THEN 520 
  460 IF TE$ < "A" OR TE$ > "Z" THEN 480 
  470 MID$ (T$,J,1) = CHR$ ( ASC ( MID$ (T$,J,1))) : GOTO 520 
  480 IF TE$ = T$ THEN T$ = "": GOTO 430 : ELSE 490 
  490 IF J = LEN (T$) THEN T$ = LEFT$ (T$,J - 1) : GOTO 530 : ELSE 500 
  500 IF J = 1 THEN T$ = RIGHT$ (T$, LEN (T$) - 1) : J = J - 1 : GOTO 520 : ELSE 510 
  510 T$ = LEFT$ (T$,J - 1) + RIGHT$ (T$, LEN (T$) - ) : J = J - 1
  520 J = J + 1 : IF J < = LEN (T$) THEN 450 
  530 PRINT "-";T$;"-"
  540 IF LEN (T$) < = MD THEN 570 
  550 PRINT "THAT'S TOO LONG, I'M AFRAID.";
  560 PRINT " TRY ANOTHER ONE : ": GOTO 410 
  570 FOR IZ = 1 TO I - 1 : IF W$(IZ) < > T$ THEN NEXT : GOTO 590 : ELSE 580 
  580 PRINT "YOU ENTERED THAT ONE ALREADY.  TRY ANOTHER : ": GOTO 410 
  590 W$(I) = T$
  600 NEXT I
  610 PRINT "THAT'S IT . . .";M" WORDS."
  620 PRINT "NOW LET ME PONDER THIS . . . . ."
  630 FOR I = 1 TO M - 1
  640 FOR J = I + 1 TO M
  650 IF LEN (W$(I)) < LEN (W$(J)) THEN HZ$ = W$(I) : W$(I) = W$(J) : W$(J) = HZ$ : ELSE 660 
  660 NEXT : NEXT 
  670 FOR I = 1 TO 8 : READ DXY(I,1),DXY(I,2) : NEXT 
  680 FOR I = 1 TO 28 : READ DD(I) : NEXT 
  690 DATA 0,1,1,1,1,0,1,-1,0,-1,-1,-1,-1,0,-1,1
  700 DATA 2,4,6,8,2,4,6,8,2,4,6,8,2,4,6,8,2,4,6,8,2,4,6,8,1,3,5,7
  710 FOR I = 1 TO M
  720 LN = LEN (W$(I))
  730 NT = 0
  740 SD = DD( RND (27) + 1)
  750 SX = FN A(W) : X1 = SX + (LN - 1) * DXY(SD,1) : IF X1 < 1 OR X1 > W THEN 740 
  760 SY = FN A(L) : X1 = SY + (LN - 1) * DXY(SD,2) : IF X1 < 1 OR X1 > L THEN 740 
  770 NT = NT + 1 : IF NT < > W * L * 2 THEN 820 
  780 PRINT "COULDN'T FIT '";W$(I);"' IN THE PUZZLE."
  790 INPUT "DO YOU WANT ME TO START OVER";A$
  800 IF LEFT$ (A$,1) = "Y" THEN 710 
  810 W$(I) = "": GOTO 900 
  820 J = SY : K = SX
  830 FOR P = 1 TO LN
  840 IF LEN (A$(J,K)) AND A$(J,K) < > MID$ (W$(I),P,1) THEN 750 
  850 J = J + DXY(SD,2) : K = K + DXY(SD,1) : NEXT P
  860 J = SY : K = SX
  870 FOR P = 1 TO LN : A$(J,K) = MID$ (W$(I),P,1)
  880 J = J + DXY(SD,2) : K = K + DXY(SD,1) : NEXT 
  890 W(I,1) = SX : W(I,2) = SY : W(I,3) = SD
  900 NEXT I
  910 FOR I = 1 TO L
  920 FOR J = 1 TO W
  930 IF A$(I,J) = ""THEN A$(I,J) = CHR$ ( RND (26) + 64)
  940 NEXT : NEXT 
  950 FOR I = 1 TO M - 1 : FOR J = I + 1 TO M
  960 IF W$(I) < = W$(J) THEN 990 
  970 HZ$ = W$(I) : W$(I) = W$(J) : W$(J) = HZ$
  980 FOR K = 1 TO 3 : HZ = W(I,K) : W(I,K) = W(J,K) : W(J,K) = HZ : NEXT K
  990 NEXT J : NEXT I
 1000 INPUT "HOW MANY COPIES OF THIS PUZZLE DO YOU WANT";N
 1010 PRINT "POSITION PAPER AT TOP OF FORM.  PRESS ANY KEY WHEN<ERROR>READY." : 
 1020 HT$ = INKEY$ : IF HT$ = ""THEN 1020 
 1030 FOR C = 1 TO N : POKE 16425,0 : GOSUB 1040 : GOSUB 1350 : NEXT : GOTO 1210 
 1040 REM PRINT
 1050 T = (TW - 2 * W) / 2
 1060 LPRINT TAB (TW - LEN (XY$)) / 2);XY$
 1070 LPRINT CHR$ (138); CHR$ (138)
 1080 FOR J = 1 TO L : LPRINT TAB T);
 1090 FOR K = 1 TO W : IF A$(J,K) = "." THEN LPRINT ". "; : GOTO 1110 : ELSE 1100 
 1100 LPRINT A$(J,K);" ";
 1110 NEXT : LPRINT : NEXT 
 1120 LPRINT STRING$ (3, CHR$ (138))
 1130 LPRINT "FIND THESE HIDDEN WORDS IN THE ABOVE PUZZLE : "
 1140 LPRINT " "
 1150 FOR J = 1 TO M
 1160 IF LEN (W$(J)) = 0 THEN 1190 
 1170 LPRINT W$(J),
 1180 UM = INT ((J / 5 - INT (J / 5)) * 5) : IF UM = 0 THEN LPRINT 
 1190 NEXT : LPRINT : LPRINT STRING$ (5, CHR$ (138))
 1200 RETURN 
 1210 IF LEFT$ (X$,1) = "Y" THEN 1230 
 1220 END 
 1230 REM 
 1240 FOR I = 1 TO L : FOR J = 1 TO W : A$(I,J) = "." : NEXT J : NEXT I
 1250 FOR I = 1 TO M
 1260 LN = LEN (W$(I)) : J = W(I,2) : K = W(I,1)
 1270 FOR P = 1 TO LN
 1280 A$(J,K) = MID$ (W$(I),P,1)
 1290 J = J + DXY(W(I,3),2) : K = K + DXY(W(I,3),1) : NEXT P
 1300 NEXT I
 1310 XY$ = "HERE IS THE ANSWER KEY : "
 1320 GOSUB 1040 
 1330 LPRINT CHR$ (138); CHR$ (138)
 1340 END 
 1350 SP = PEEK (16425) : LPRINT STRING$ (60 - SP, CHR$ (138)) : RETURN 

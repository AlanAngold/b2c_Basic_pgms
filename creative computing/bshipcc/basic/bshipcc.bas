# NOTE: Type forced to be:
# trs2 - TRS-80 Level II BASIC tokenized file
   10 CLS 
   20 PRINT @ 284,"BATTLE"
   30 PRINT @ 409,"COPYRIGHT BY"
   40 PRINT @ 534,"CREATIVE COMPUTING"
   50 PRINT @ 657,"MORRISTOWN, NEW JERSEY 07960"
   60 FOR II = 1 TO 1500 : NEXT 
   70 CLS 
   80 CLEAR 150
   90 FOR X = 1 TO 6
  100 FOR Y = 1 TO 6
  110 F(X,Y) = 0
  120 NEXT Y
  130 NEXT X
  140 FOR I = 1 TO 3
  150 N = 4 - I
  160 FOR J = 1 TO 2
  170 A = RND (6)
  180 B = RND (6)
  190 D = RND (4)
  200 IF F(A,B) > 0 THEN 170
  210 M = 0
  220 ON D GOTO 230,440,700,910
  230 B(1) = B
  240 B(2) = 7 : B(3) = 7
  250 FOR K = 1 TO N
  260 IF M > 1 THEN 320
  270 IF B(K) = 6 THEN 310
  280 IF F(A,B(K) + 1) > 0 THEN 310
  290 B(K + 1) = B(K) + 1
  300 GOTO 380
  310 M = 2
  320 IF B(1) < B(2) AND B(1) < B(3) THEN Z = B(1)
  330 IF B(2) < B(1) AND B(2) < B(3) THEN Z = B(2)
  340 IF B(3) < B(1) AND B(3) < B(2) THEN Z = B(3)
  350 IF Z = 1 THEN 170
  360 IF F(A,Z - 1) > 0 THEN 170
  370 B(K + 1) = Z - 1
  380 NEXT K
  390 F(A,B) = 9 - 2 * I - J
  400 FOR K = 1 TO N
  410 F(A,B(K + 1)) = F(A,B)
  420 NEXT K
  430 GOTO 1200
  440 A(1) = A
  450 B(1) = B
  460 A(2) = 0 : A(3) = 0 : B(2) = 0 : B(3) = 0
  470 FOR K = 1 TO N
  480 IF M > 1 THEN 570
  490 IF A(K) = 1 OR B(K) = 1 THEN 560
  500 IF F(A(K) - 1,B(K) - 1) > 0 THEN 560
  510 IF F(A(K) - 1,B(K)) < = 0 THEN 530
  520 IF F(A(K) - 1,B(K)) = F(A(K),B(K) - 1) THEN 560
  530 A(K + 1) = A(K) - 1
  540 B(K + 1) = B(K) - 1
  550 GOTO 680
  560 M = 2
  570 IF A(1) > A(2) AND A(1) > A(3) THEN Z1 = A(1)
  580 IF A(2) > A(1) AND A(2) > A(3) THEN Z1 = A(2)
  590 IF A(3) > A(1) AND A(3) > A(2) THEN Z1 = A(3)
  600 IF B(1) > B(2) AND B(1) > B(3) THEN Z2 = B(1)
  610 IF B(2) > B(1) AND B(2) > B(3) THEN Z2 = B(2)
  620 IF B(3) > B(1) AND B(3) > B(2) THEN Z2 = B(3)
  630 IF Z1 = 6 OR Z2 = 6 THEN 170
  640 IF F(Z1 + 1,Z2 + 1) > 0 THEN 170
  650 IF F(Z1,Z2 + 1) > 0 AND F(Z1,Z2 + 1) = F(Z1 + 1,Z2) THEN 170
  660 A(K + 1) = Z1 + 1
  670 B(K + 1) = Z2 + 1
  680 NEXT K
  690 GOTO 1160
  700 A(1) = A
  710 A(2) = 7 : A(3) = 7
  720 FOR K = 1 TO N
  730 IF M > 1 THEN 790
  740 IF A(K) = 6 THEN 780
  750 IF F(A(K) + 1,B) > 0 THEN 780
  760 A(K + 1) = A(K) + 1
  770 GOTO 850
  780 M = 2
  790 IF A(1) < A(2) AND A(1) < A(3) THEN Z = A(1)
  800 IF A(2) < A(1) AND A (2) < A(3) THEN Z = A(2)
  810 IF A(3) < A(1) AND A(3) < A(2) THEN Z = A(3)
  820 IF Z = 1 THEN 170
  830 IF F(Z - 1,B) > 0 THEN 170
  840 A(K + 1) = Z - 1
  850 NEXT K
  860 F(A,B) = 9 - 2 * I - J
  870 FOR K = 1 TO N
  880 F(A(K + 1),B) = F(A,B)
  890 NEXT K
  900 GOTO 1200
  910 A(1) = A
  920 B(1) = B
  930 A(2) = 7 : A(3) = 7
  940 B(2) = 0 : B(3) = 0
  950 FOR K = 1 TO N
  960 IF M > 1 THEN 1040
  970 IF A(K) = 6 OR B(K) = 1 THEN 1030
  980 IF F(A(K) + 1,B(K) - 1) > 0 THEN 1030
  990 IF F(A(K) + 1,B(K)) > 0 THEN 1030
 1000 A(K + 1) = A(K) + 1
 1010 B(K + 1) = B(K) - 1
 1020 GOTO 1150
 1030 M = 2
 1040 IF A(1) < A(2) AND A(1) < A(3) THEN Z1 = A(1)
 1050 IF A(2) < A(1) AND A(2) < A(3) THEN Z1 = A(2)
 1060 IF A(3) < A(1) AND A(3) < A(2) THEN Z1 = A(3)
 1070 IF B(1) > B(2) AND B(1) > B(3) THEN Z2 = B(1)
 1080 IF B(2) > B(1) AND B(2) > B(3) THEN Z2 = B(2)
 1090 IF B(3) > B(1) AND B(3) > B(2) THEN Z2 = B(3)
 1100 IF Z1 = 1 OR Z2 = 6 THEN 170
 1110 IF F(Z1 - 1,Z2 + 1) > 0 THEN 170
 1120 IF F(Z1,Z2 + 1) > 0 AND F(Z1,Z2 + 1) = F(Z1 - 1,Z2) THEN 170
 1130 A(K + 1) = Z1 - 1
 1140 B(K + 1) = Z2 + 1
 1150 NEXT K
 1160 F(A,B) = 9 - 2 * I - J
 1170 FOR K = 1 TO N
 1180 F(A(K + 1),B(K + 1)) = F(A,B)
 1190 NEXT K
 1200 NEXT J
 1210 NEXT I
 1220 CLS 
 1230 PRINT @ 26,"THE FOLLOWING CODE OF THE BAD GUYS'"
 1240 PRINT @ 90,"FLEET DISPOSITION HAS BEEN CAPTURED"
 1250 PRINT @ 154,"BUT HAS NOT BEEN DECODED." CHR$ (28);
 1260 FOR I = 1 TO 6
 1270 FOR J = 1 TO 6
 1280 H(I,J) = F(J,I) : PRINT H(I,J);
 1290 NEXT J
 1300 PRINT CHR$ (26) CHR$ (29);
 1310 NEXT I
 1320 PRINT @ 282,"DECODE IT AND USE IT IF YOU CAN BUT";
 1330 PRINT @ 346,"KEEP THE DECODING METHOD A SECRET."
 1340 PRINT STRING$ (64,"=");
 1350 FOR I = 1 TO 6
 1360 FOR J = 1 TO 6
 1370 H(I,J) = 0
 1380 NEXT J
 1390 NEXT I
 1400 FOR I = 1 TO 3
 1410 L(I) = 0
 1420 NEXT I
 1430 C(1) = 2 : C(2) = 2
 1440 C(3) = 1 : C(4) = 1
 1450 C(5) = 0 : C(6) = 0
 1460 S = 0 : H = 0
 1470 PRINT @ 512, "(X,Y) COORDINGATES ARE "CHR$ (31); : INPUT X,Y
 1480 IF X < 1 OR X > 6 OR INT (X) < > ABS (X) THEN 1500
 1490 IF Y > 0 AND Y < 7 AND INT (Y) = ABS (Y) THEN 1520
 1500 PRINT "ILLEGAL INPUT. PLEASE TRY AGAIN"
 1510 FOR XZ = 1 TO 1000 : NEXT XZ : GOTO 1470
 1520 R = 7 - Y
 1530 C = X
 1540 IF F(R,C) > 0 THEN 1580
 1550 S = S + 1
 1560 PRINT " . . . . . S P L A S H !"
 1570 GOTO 1510
 1580 IF C(F(R,C)) < 4 THEN 1610
 1590 PRINT "THERE USED TO BE A SHIP AT THERE BUT YOU SUNK IT"
 1600 GOTO 1550
 1610 IF H(R,C) > 0 THEN 1680
 1620 H = H + 1
 1630 H(R,C) = F(R,C)
 1640 PRINT "A DIRECT HIT ON SHIP NUMBER"F(R,C)
 1650 C(F(R,C)) = C(F(R,C)) + 1
 1660 IF C(F(R,C)) > = 4 THEN 1710
 1670 GOTO 1510
 1680 PRINT "YOU ALREADY PUT A HOLE IN SHIP NUMBER"F(R,C);
 1690 PRINT "AT THAT POINT"
 1700 GOTO 1550
 1710 L(( INT (F(R,C) - 1) / 2) + 1) = L(( INT (F(R,C) - 1) / 2) + 1) + 1
 1720 PRINT "AND YOU SUNK IT. HURRAY FOR THE GOOD GUYS."
 1730 PRINT "SO FAR, THE BAD GUYS HAVE LOST : "
 1740 PRINT L(1)"DESTROYERS(S)" TAB 18)L(2)"CRUISERS(S)" TAB 36);
 1750 PRINT "AND"L(3)"AIRCRAFT CARRIER(S)."
 1760 PRINT STRING$ (20,"*")"YOUR CURRENT SPLASH/HIT RATIO IS";
 1770 PRINT S / H : IF L(1) + L(2) + L(3) > = 6 THEN 1790
 1780 GOTO 1510
 1790 FOR ZX = 1 TO 500 : NEXT ZX : PRINT @ 512, CHR$ (31);
 1800 PRINT "YOU HAVE TOTALLY WIPED OUT THE BAD GUYS'"
 1810 PRINT "FLEET WITH A FINAL SPLASH HIT RADIO OF : "
 1820 PRINT STRING$ (40,"-")S / H"%" : PRINT 
 1830 IF S / H > 0 THEN 1850
 1840 PRINT "CONGRATULATIONS ------- A DIRECT HIT EVERY TIME !!"
 1850 PRINT STRING$ (64,"*"); : END 

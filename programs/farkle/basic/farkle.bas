# NOTE: Type forced to be:
# gwba - GW-BASIC tokenized file (or BASICA)
    1 REM *** Written for Mom, February 1997 ***
   10 KEY OFF
   20 DH$(0)="Held":DH$(2)="    ":DH$(1)="Err"
   30 CLS:LOCATE 9,26:PRINTCHR$(201);STRING$(26,205);CHR$(187)
   40 FOR A=10 TO 13:LOCATE A,26:PRINT CHR$(186):LOCATE A,53:PRINT CHR$(186):NEXT
   50 LOCATE 14,26:PRINT CHR$(200);STRING$(26,205);CHR$(188)
  100 P=1:COLOR 9:LOCATE 10,32:PRINT"F  A  R  K  L  E"
  110 COLOR 7:LOCATE 13,28:PRINT"How many players (1-8) ?"
  120 A$=INKEY$:IF A$="" THEN 120
  130 SP=VAL(A$):IF SP<1 OR SP>8 THEN 120
  140 IF SP=1 THEN NP=2 ELSE NP=SP
  199 REM *** MAIN DISPLAY ***
  200 CLS:COLOR 15:LOCATE 19:PRINT STRING$(79,205)
  210 FOR A=1 TO NP:COLOR A:LOCATE 20,1+(A-1)*10:PRINT"Player";A
  220 LOCATE 21,2+(A-1)*10:PRINT"Score:":LOCATE 22,2+(A-1)*10:PRINT S(A):NEXT
  230 COLOR P:LOCATE 18,32:PRINT"PLAYER"STR$(P)"'S TURN"
  250 A$=INKEY$:IF A$="" THEN 250 ELSE IF CPT=1 THEN 710
  299 REM *** ROLL ***
  300 S=0:AS=0:R=1:FOR DN=1 TO 5:DS(DN)=0:SC(DN)=0:AH(A)=0:NEXT
  305 NDH=0:NDH1=0:FOR DN=1 TO 5:DH(DN)=1:AH(A)=0
  310 GOSUB 5000:D(DN)=INT(RND*6+1)
  320 DV=D(DN):GOSUB 1000
  330 LOCATE 8,5+H:PRINTDH$(DH(DN)+1):NEXT
  335 FOR A=1 TO 6:SC(A)=0:NEXT
  337 FOR A=1 TO 5:SC(D(A))=SC(D(A))+1:NEXT
  340 IF SC(1)=1 AND SC(2)=1 AND SC(3)=1 AND SC(4)=1 AND SC(5)=1 THEN BEEP:S=1500:GOTO 675
  390 LOCATE 17,10:PRINT"Select dice to hold. Press Return to roll. Press Esc to end."
  399 REM *** SELECT DICE FOR REROLL ***
  400 FOR A=1 TO 5:VD(A)=0:NEXT
  405 B$=INKEY$:IF B$="" THEN 405
  410 IF B$=CHR$(13) THEN 450 ELSE IF B$=CHR$(27) THEN 600
  420 A=VAL(B$):IF A<1 OR A>5 THEN 405
  430 IF AH(A)=0 THEN DH(A)=DH(A)*-1:LOCATE 8,16*(A-1)+6:PRINT DH$(DH(A)+1)
  440 GOTO 405
  450 REM *** PARTIAL SCORING ***
  460 B=0:FOR A=1 TO 5:B=B+DH(A):VD(A)=0:IF DH(A)=-1 AND AH(A)<1 THEN VD(A)=D(A)
  470 NEXT:IF B=-5 THEN R=2 ELSE R=1
  480 GOTO 600
  499 REM *** REROLL ***
  500 NDH=0:FOR A=1 TO 5:IF DH(A)<1 THEN NDH=NDH+1
  505 NEXT:IF NDH<=NDH1 THEN 400
  510 NDH1=NDH:LOCATE 17:PRINT STRING$(75,32):FOR DN=1 TO 5:IF DH(DN)<1 THEN 550
  520 GOSUB 5000:D(DN)=INT(RND*6+1)
  530 DV=D(DN):GOSUB 1000
  540 LOCATE 8,5+H:PRINT DH$(DH(DN)+1)
  550 NEXT:GOTO 390
  600 REM *** SCORING ***
  610 FOR A=1 TO 5:VD(A)=0:IF DH(A)=-1 AND AH(A)<1 THEN VD(A)=D(A)
  620 NEXT:S=0:FOR A=1 TO 6:SC(A)=0:NEXT:FOR DN=1 TO 5:SC(VD(DN))=SC(VD(DN))+1:NEXT
  640 IF SC(1)>2 THEN SC(1)=SC(1)-3:S=S+1000
  650 FOR A=2 TO 6:IF SC(A)>2 THEN SC(A)=SC(A)-3:S=S+A*100
  660 NEXT:S=S+SC(1)*100:S=S+SC(5)*50:FOR A=1 TO 5:IF DH(A)=-1 THEN AH(A)=1
  665 NEXT:IF R=2 THEN 670 ELSE IF S=0 THEN LOCATE 16,30:PRINT"*** YOU FARKLED ***":AS=0:GOTO 680
  667 LOCATE 16,33:PRINT S;"points":IF B$<>CHR$(27) THEN AS=AS+S:S=0:GOTO 500
  670 IF S=0 OR S(P)+S+AS<500 THEN LOCATE 16,30:PRINT"*** YOU FARKLED ***":S=0:AS=0:GOTO 680
  675 AS=AS+S:LOCATE 16,33:PRINT AS;"POINTS":S(P)=S(P)+AS
  680 P=P+1:IF P>NP THEN P=1 ELSE 685
  682 FOR A=1 TO NP:IF S(A)=>5000 THEN 2000
  683 NEXT
  685 A$=INKEY$:IF A$="" THEN 685
  690 IF SP<>1 OR P<>2 THEN FOR A=1 TO 5:AH(A)=0:NEXT:GOTO 200
  700 REM *** COMPUTER'S TURN ***
  705 CPT=1:GOTO 200
  710 CPT=0:S=0:AS=0:R=1:FOR DN=1 TO 5:DS(DN)=0:SC(DN)=0:AH(DN)=0:DH(DN)=0:NEXT
  720 NDH=0:NDH1=0:FOR DN=1 TO 5:DH(DN)=1:AH(DN)=0
  730 GOSUB 5000:D(DN)=INT(RND*6+1)
  740 DV=D(DN):GOSUB 1000
  750 LOCATE 8,5+H:PRINT DH$(DH(DN)+1):NEXT
  755 S=0:FOR A=1 TO 6:SC(A)=0:NEXT
  757 FOR A=1 TO 5:SC(D(A))=SC(D(A))+1:NEXT
  760 IF SC(1)=1 AND SC(2)=1 AND SC(3)=1 AND SC(4)=1 AND SC(5)=1 THEN S=1500:GOTO 675
  770 FOR A=1 TO 2000:NEXT
  780 REM *** COMPUTER CHOOSES DICE TO REROLL ***
  790 FOR A=1 TO 5:VD(A)=0:NEXT
  800 FOR A=1 TO 6:SC(A)=0:NEXT:FOR DN=1 TO 5:SC(D(DN))=SC(D(DN))+1:NEXT
  810 IF SC(1)=1 AND SC(2)=1 AND SC(3)=1 AND SC(4)=1 AND SC(5)=1 THEN BEEP:NDH=5:AS=1500:GOTO 880
  820 IF SC(1)>2 THEN SC(1)=SC(1)-3:S=S+1000:DT=1:ND=3:GOSUB 1600
  830 FOR A=3 TO 6:IF SC(A)>2 THEN SC(A)=SC(A)-3:S=S+A*100:DT=A:ND=3:GOSUB 1600
  840 NEXT:S=S+SC(1)*100:DT=1:ND=SC(1):SC(1)=0:GOSUB 1600
  850 IF S<50 OR NDH>2 OR S+AS=>200 AND AS+S+S(2)=>500 THEN S=S+SC(5)*50:DT=5:ND=SC(5):SC(5)=0:GOSUB 1600
  860 IF S<50 AND SC(2)>2 THEN S=S+200:SC(2)=SC(2)-3:DT=2:ND=3:GOSUB 1600
  870 IF S<50 THEN LOCATE 16,32:PRINT"*** FARKLED ***":AS=0:GOTO 890
  875 AS=AS+S:IF AS+S(2)<500 AND NDH<5 THEN 900 ELSE IF NDH<5 THEN IF AS<200 OR NDH<3 THEN 900
  877 IF AS+S(2)<500 OR NDH<=NDH1 THEN LOCATE 16,32:PRINT"*** FARKLED ***":AS=0:GOTO 890
  880 LOCATE 16,33:PRINT AS;"POINTS":S(2)=S(2)+AS:P=1:IF S(1)=>5000 OR S(2)=>5000 THEN 2000
  890 A$=INKEY$:IF A$="" THEN 890 ELSE P=1:FOR A=1 TO 5:AH(A)=0:NEXT:GOTO 200
  900 REM *** COMPUTER REROLLS ***
  910 FOR A=1 TO 100000.0:NEXT:LOCATE 16:PRINT STRING$(79,32)
  920 FOR DN=1 TO 5:IF DH(DN)=-1 OR AH(DN)=1 THEN D(DN)=0 ELSE GOSUB 5000:D(DN)=INT(RND*6+1):DV=D(DN):GOSUB 1000
  930 NEXT:GOTO 755
 1000 H=16*(DN-1)+1:ON DV GOTO 1010,1100,1200,1300,1400,1500
 1010 LOCATE 1,H:PRINT STRING$(14,219)
 1020 LOCATE 2,H:PRINT STRING$(14,219)
 1030 LOCATE 3,H:PRINT STRING$(14,219)
 1040 LOCATE 4,H:PRINT STRING$(6,219);"  ";STRING$(6,219)
 1050 LOCATE 5,H:PRINT STRING$(14,219)
 1060 LOCATE 6,H:PRINT STRING$(14,219)
 1070 LOCATE 7,H:PRINT STRING$(14,219)
 1080 RETURN
 1100 LOCATE 1,H:PRINT STRING$(14,219)
 1110 LOCATE 2,H:PRINT STRING$(10,219);"  ";STRING$(2,219)
 1120 LOCATE 3,H:PRINT STRING$(14,219)
 1130 LOCATE 4,H:PRINT STRING$(14,219)
 1140 LOCATE 5,H:PRINT STRING$(14,219)
 1150 LOCATE 6,H:PRINT STRING$(2,219);"  ";STRING$(10,219)
 1160 LOCATE 7,H:PRINT STRING$(14,219)
 1170 RETURN
 1200 LOCATE 1,H:PRINT STRING$(14,219)
 1210 LOCATE 2,H:PRINT STRING$(10,219);"  ";STRING$(2,219)
 1220 LOCATE 3,H:PRINT STRING$(14,219)
 1230 LOCATE 4,H:PRINT STRING$(6,219);"  ";STRING$(6,219)
 1240 LOCATE 5,H:PRINT STRING$(14,219)
 1250 LOCATE 6,H:PRINT STRING$(2,219);"  ";STRING$(10,219)
 1260 LOCATE 7,H:PRINT STRING$(14,219)
 1270 RETURN
 1300 LOCATE 1,H:PRINT STRING$(14,219)
 1310 LOCATE 2,H:PRINT STRING$(2,219);"  ";STRING$(6,219);"  ";STRING$(2,219)
 1320 LOCATE 3,H:PRINT STRING$(14,219)
 1330 LOCATE 4,H:PRINT STRING$(14,219)
 1340 LOCATE 5,H:PRINT STRING$(14,219)
 1350 LOCATE 6,H:PRINT STRING$(2,219);"  ";STRING$(6,219);"  ";STRING$(2,219)
 1360 LOCATE 7,H:PRINT STRING$(14,219)
 1370 RETURN
 1400 LOCATE 1,H:PRINT STRING$(14,219)
 1410 LOCATE 2,H:PRINT STRING$(2,219);"  ";STRING$(6,219);"  ";STRING$(2,219)
 1420 LOCATE 3,H:PRINT STRING$(14,219)
 1430 LOCATE 4,H:PRINT STRING$(6,219);"  ";STRING$(6,219)
 1440 LOCATE 5,H:PRINT STRING$(14,219)
 1450 LOCATE 6,H:PRINT STRING$(2,219);"  ";STRING$(6,219);"  ";STRING$(2,219)
 1460 LOCATE 7,H:PRINT STRING$(14,219)
 1470 RETURN
 1500 LOCATE 1,H:PRINT STRING$(14,219)
 1510 LOCATE 2,H:PRINT STRING$(2,219);"  ";STRING$(6,219);"  ";STRING$(2,219)
 1520 LOCATE 3,H:PRINT STRING$(14,219)
 1530 LOCATE 4,H:PRINT STRING$(2,219);"  ";STRING$(6,219);"  ";STRING$(2,219)
 1540 LOCATE 5,H:PRINT STRING$(14,219)
 1550 LOCATE 6,H:PRINT STRING$(2,219);"  ";STRING$(6,219);"  ";STRING$(2,219)
 1560 LOCATE 7,H:PRINT STRING$(14,219)
 1570 RETURN
 1600 REM *** IDENTIFYING DICE HELD BY COMPUTER *** DT=type of die (1-6). ND=# of dice
 1610 NDH=NDH+ND:B=0:FOR A1=1 TO 5
 1620 IF D(A1)=DT AND B<ND THEN AH(A1)=1:DH(A1)=-1:B=B+1:FOR A2=1 TO 100000.0:NEXT:LOCATE 8,16*(A1-1)+6:PRINT"Held"
 1630 NEXT:RETURN
 2000 REM *** END OF GAME ***
 2010 CLS:COLOR 7:L=12-NP/2:LOCATE L-1,20:PRINT"FINAL SCORES:"
 2020 SS=0:WP=0:FOR A=1 TO NP:COLOR A:LOCATE L+A,20:PRINT"Player"STR$(A)":"S(A)
 2030 IF S(A)>SS THEN SS=S(A):WP=A
 2040 NEXT:COLOR WP+8:LOCATE L+NP+2,20:PRINT"PLAYER"WP"WINS!"
 2050 COLOR 7:END
 5000 RANDOMIZE(P(1)+P(2)+VAL(RIGHT$(TIME$,2))+VAL(RIGHT$(DATE$,1))+A-DN-D(1)+S):RETURN

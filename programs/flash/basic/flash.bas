# NOTE: Type forced to be:
# gwba - GW-BASIC tokenized file (or BASICA)
   10 REM *** FLASHCARDS PROGRAM  written Dec 1, 1991 by Leon Baradat ***
   15 DIM F$(200),S$(200),O(200),P(200)
   20 CLS:KEY OFF
   50 COLOR 0,15:LOCATE 7,31:PRINT " FLASHCARDS ":COLOR 7,0
   60 LOCATE 9,25:PRINT "1.  Enter new flash cards":LOCATE 10,25:PRINT "2.  Call up saved flash cards":LOCATE 11,25:PRINT "3.  Edit flash cards"
   70 LOCATE 12,25:PRINT "4.  Run flash cards":LOCATE 13,25:PRINT "5.  Save flash cards":LOCATE 14,25:PRINT "6.  Analyze last run"
   80 A$=INKEY$:IF A$="" THEN 80 ELSE IF A$=CHR$(27) THEN END ELSE A=VAL(A$)
   90 IF A<1 OR A>6 THEN 80 ELSE CLS:ON A GOTO 100,250,550,400,160,800
  100 PRINT TAB(25);"* ENTER NEW FLASH CARDS *":N=1
  110 COLOR 1,0:LOCATE 3,6:PRINT "First Item:":LOCATE 3,43:PRINT "What it goes with:":COLOR 7,0
  120 PRINT N;TAB(6);:LINE INPUT F$(N):IF F$(N)="" THEN 150
  130 PRINT STRING$(38,29);:LINE INPUT S$(N)
  140 N=N+1:GOTO 120
  150 N=N-1:PRINT:INPUT "Are you all done";A$:IF LEFT$(A$,1)="N" OR LEFT$(A$,1)="n" THEN 140
  160 PRINT:INPUT "Save this under file name";N$:IF N$="" OR VAL(LEFT$(N$,1))>0 THEN 245 ELSE PRINT:PRINT "Preparing..."
  170 FOR A=1 TO N:FOR B=1 TO LEN(F$(A)):IF MID$(F$(A),B,1)="," THEN MID$(F$(A),B,1)=CHR$(1)
  175 NEXT:FOR B=1 TO LEN(S$(A)):IF MID$(S$(A),B,1)="," THEN MID$(S$(A),B,1)=CHR$(1)
  180 NEXT:NEXT:PRINT:PRINT "Saving..."
  185 OPEN "O",#1,N$
  190 PRINT #1,N
  200 FOR A=1 TO N
  210 PRINT #1,F$(A)
  220 PRINT #1,S$(A)
  230 NEXT
  232 CLOSE #1
  235 PRINT:PRINT "Just a moment...":FOR A=1 TO N:FOR B=1 TO LEN(F$(A)):IF MID$(F$(A),B,1)=CHR$(1) THEN MID$(F$(A),B,1)=","
  237 NEXT:FOR B=1 TO LEN(S$(A)):IF MID$(S$(A),B,1)=CHR$(1) THEN MID$(S$(A),B,1)=","
  239 NEXT:NEXT
  240 GOTO 20
  245 PRINT:PRINT "File NOT saved...":FOR B=1 TO 1000:NEXT:GOTO 20
  250 PRINT TAB(25);"* CALL UP SAVED FLASH CARDS *"
  260 LOCATE 6:PRINT "POSSIBLE FILES IN EXISTENCE:":FILES"*."
  270 PRINT:PRINT:INPUT "Call up which file";J$:IF J$="" THEN 10
  280 OPEN "I",#1,J$
  290 INPUT #1,N
  300 FOR A=1 TO N
  310 INPUT #1,F$(A)
  320 INPUT #1,S$(A)
  330 NEXT
  340 CLOSE #1
  350 PRINT:PRINT "Just a moment...":FOR A=1 TO N:FOR B=1 TO LEN(F$(A)):IF MID$(F$(A),B,1)=CHR$(1) THEN MID$(F$(A),B,1)=","
  360 NEXT:FOR B=1 TO LEN(S$(A)):IF MID$(S$(A),B,1)=CHR$(1) THEN MID$(S$(A),B,1)=","
  370 NEXT:NEXT
  380 GOTO 20
  400 COLOR 0,15:LOCATE 1,25:PRINT " <== FLASH CARD SESSION ==> ":COLOR 7,0
  410 FOR A=1 TO N:O(A)=A:NEXT:SC=0:NR=0
  420 FOR A=1 TO N:GOSUB 999:M=INT(RND*(N-A+1)+1):P(A)=O(M):FOR B=M+1 TO N-A+1:O(B-1)=O(B):NEXT
  430 NEXT:FOR A=1 TO N
  440 FOR B=7 TO 14:LOCATE B:PRINT STRING$(79,32):NEXT
  450 GOSUB 999:IF RND*1<0.5 THEN U$=F$(P(A)):V$=S$(P(A)):GOTO 470
  460 U$=S$(P(A)):V$=F$(P(A))
  470 LOCATE 6,12:PRINT A;" out of ";N
  475 LOCATE 7,8:PRINT CHR$(218);STRING$(LEN(U$)+2,196);CHR$(191):LOCATE 8,8:PRINT CHR$(179);" ";U$;" ";CHR$(179):LOCATE 9,8:PRINT CHR$(192);STRING$(LEN(U$)+2,196);CHR$(217):LOCATE 11,10:LINE INPUT T$
  480 IF T$=V$ THEN SC=SC+1:COLOR 0,15:LOCATE 13,18:PRINT " Good! ":COLOR 7,0:GOTO 510
  490 B$="":LOCATE 12,10:PRINT "The answer is: "V$".":IF T$="" THEN 970 ELSE LOCATE 13,12:INPUT "Was your answer really correct";B$
  500 IF LEFT$(B$,1)="Y" OR LEFT$(B$,1)="y" THEN SC=SC+1:GOTO 510
  505 NR=NR+1:O(NR)=P(A)
  510 NEXT
  520 CLS:COLOR 15,0:R=27+LEN(STR$(SC))+LEN(STR$(N)):LOCATE 8,19:PRINT CHR$(201);STRING$(R,205);CHR$(187):FOR A=9 TO 11:LOCATE A,19:PRINT CHR$(186):LOCATE A,R+20:PRINT CHR$(186):NEXT
  525 LOCATE 12,19:PRINT CHR$(200);STRING$(R,205);CHR$(188):LOCATE 9,22:PRINT " Your score is"SC"out of"STR$(N)", "
  530 LOCATE 11,32:PRINT " or"STR$(INT(0.5+100*SC/N))"%. ":COLOR 7,0
  540 A$=INKEY$:IF A$="" THEN 540 ELSE 20
  550 W=1
  560 CLS:PRINT TAB(30);"* EDIT FLASH CARDS *"
  570 FOR A=W TO W+15:IF A<=N THEN PRINT A;TAB(6);F$(A);" º ";S$(A)
  575 NEXT
  580 LOCATE 22:PRINT STRING$(79,32):LOCATE 22:PRINT "Number to edit? (- go up, + go down, * return,";N+1;"to add) ";:LINE INPUT A$
  590 IF A$="" THEN 560 ELSE IF A$="*" THEN 20
  600 IF A$<>"-" THEN 620 ELSE IF W-14<1 THEN W=1 ELSE W=W-14
  610 GOTO 560
  620 IF A$<>"+" THEN 640 ELSE IF W+14>N+1 THEN W=N+1 ELSE W=W+14
  630 GOTO 560
  640 Q=VAL(A$):IF Q<1 THEN 590 ELSE IF Q=N+1 THEN 680 ELSE IF Q>N THEN 560
  650 GOSUB 990:PRINT "`";F$(Q);"' becomes (Return for no change, `d' to delete):":LINE INPUT " ";G$:IF G$="" THEN 660 ELSE IF G$="d" THEN 750 ELSE F$(Q)=G$
  660 GOSUB 990:PRINT "`";S$(Q);"' becomes (Return for no change):":LINE INPUT " ";G$:IF G$="" THEN 670 ELSE S$(Q)=G$
  670 GOTO 560
  680 GOSUB 990:PRINT N+1;:LINE INPUT " First Item:  ";G$:IF G$="" THEN 560
  690 GOSUB 990:PRINT N+1;:LINE INPUT " What it goes with: ";H$:IF H$="" THEN 560 ELSE N=N+1:F$(N)=G$:S$(N)=H$
  700 PRINTSTRING$(79,32):PRINT CHR$(30);STRING$(79,32):PRINT CHR$(30);:GOTO 680
  750 FOR A=Q TO N:F$(A)=F$(A+1):S$(A)=S$(A+1):NEXT:N=N-1:GOTO 560
  800 W=1
  810 CLS:PRINT TAB(22);"* FLASH CARDS YOU MISSED LAST TIME *":PRINT:IF W+16>NR THEN Q=NR ELSE Q=W+16
  820 FOR A=W TO Q:PRINT A;TAB(6);F$(O(A));" º ";S$(O(A)):NEXT
  830 LOCATE 22:PRINT "- to go up, + to go down, * to return"
  840 A$=INKEY$:IF A$="" THEN 840
  850 IF A$="*" THEN 20
  860 IF A$<>"+" THEN 880 ELSE IF W+14<NR THEN W=W+14
  870 GOTO 810
  880 IF A$<>"-" THEN 840 ELSE IF W-14<1 THEN W=1 ELSE W=W-14
  890 GOTO 810
  970 LOCATE 13,10:PRINT "Press any key to continue."
  980 A$=INKEY$:IF A$="" THEN 980 ELSE 505
  990 LOCATE 22:PRINT STRING$(79,32):LOCATE 22:RETURN
  999 RANDOMIZE(VAL(RIGHT$(TIME$,2))):RETURN
